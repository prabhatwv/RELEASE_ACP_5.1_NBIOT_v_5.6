/******************************************************************************/
// @copyright   Copyright Notification
//   No part may be reproduced except as authorized by written permission.
//   The copyright and the foregoing restriction extend to reproduction in all media.
//   Trademark 2019, 3GPP Organizational Partners (ARIB, ATIS, CCSA, ETSI, TSDSI, TTA, TTC).
//   All rights reserved.
// @version: 36.523-3 v15.3.0
// $Date: 2019-01-29 21:46:03 +0100 (Tue, 29 Jan 2019) $
// $Rev: 23623 $
/******************************************************************************/

module EUTRA_Component {

  import from EUTRA_RRC_ASN1_Definitions language "ASN.1:2002" all with {encode "UNALIGNED_PER_OctetAligned"};
  import from CommonAspDefs all;
  import from CommonDefs all;
  import from CommonIratDefs all;
  import from EPS_NAS_TypeDefs all;
  import from EUTRA_ASP_CDMA2000TunnellingDefs all;
  import from EUTRA_ASP_DrbDefs all;
  import from EUTRA_ASP_SrbDefs all;
  import from EUTRA_ASP_TypeDefs all;
  import from EUTRA_ASP_VirtualNoiseDefs all;
  import from EUTRA_CommonDefs all;
  import from EUTRA_NB_ASP_TypeDefs all;
  import from EUTRA_NB_CommonDefs all;
  import from EUTRA_SecurityFunctions all;
  import from IMS_PTC_CoordMsg all;
  import from IP_PTC_CtrlMsgs all;
  import from NAS_CommonTypeDefs all;
  import from NasEmu_CtrlAspTypes all;
  import from TestcaseProperties all;
  import from UpperTesterPtcToMtcDefs all;

  type enumerated Combination_Type {  /* @status    APPROVED (ENDC, IMS, IMS_IRAT, LTE, LTE_A_IRAT, LTE_A_PRO, LTE_A_R10_R11, LTE_A_R12, LTE_IRAT, NBIOT, POS, UTRAN) */
    /* Combinations of system information blocks acc. to 36.508 Table 4.4.3.1.1-1; */
    /* @sic R5-113658 R5-140075, R5-151995, R5-155922: new combination sic@ */
    /* @sic SC-PTM added c25 to c28 sic@ */
    /* @sic R5-175100 V2X added c29 sic@ */
    c1, c2, c3, c4, c5, c6, c7, c8, c9, c10, c10a, c11, c12, c13, c14, c15, c16, c17, c18, c19, c20, c21, c22, c23, c24,
    c25, c26, c27, c28, c29
  };

  type enumerated SysinfoType_Type { sysinfoOnly, sysinfoAndBR }; /* @status    APPROVED (ENDC, IMS, IMS_IRAT, LTE, LTE_A_IRAT, LTE_A_PRO, LTE_A_R10_R11, LTE_A_R12, LTE_IRAT, NBIOT, POS, UTRAN)
                                                                     sysinfoOnly: SS broadcasts only normal SysInfo
                                                                     sysinfoAndBR: SS broadcasts both normal SysInfo and SysInfo-br */

  type record RNTI_Type {                       /* @status    APPROVED (ENDC, IMS, IMS_IRAT, LTE, LTE_A_IRAT, LTE_A_PRO, LTE_A_R10_R11, LTE_A_R12, LTE_IRAT, NBIOT, POS, UTRAN) */
    C_RNTI       C_RNTI
  };

  type  record EUTRA_CellSysInfo_Type {         /* @status    APPROVED (ENDC, IMS, IMS_IRAT, LTE, LTE_A_IRAT, LTE_A_PRO, LTE_A_R10_R11, LTE_A_R12, LTE_IRAT, NBIOT, POS, UTRAN) */
    // type defined to contain all sysinfo related to  a cell
    BcchInfo_Type       BCCH_Info,              // BCCH PDUs
    Combination_Type    Combination,            // Combinations of system information blocks  (combinition acc. 36.508 cl. 4.4.3.1.1)
    boolean             SB1_ValueTagChanged,    // set to TRUE after SB1ValueTag changed, set to FALSE after SB1 delivered to SS.
    SysinfoType_Type    SysinfoType,            // To indicate the type of sysinfo to braodcast: Sysinfo only or Sysinfo+Sysinfo_BR or Sysinfo-BR only @sic R5-167569 sic@
    Bcch_BRInfo_Type    BCCH_BR_Info optional,  // BCCH-BR sysinfo PDUs - omitted if SysinfoType=sysinfoOnly @sic R5-167569 sic@
    boolean             SI_BR_ValueTagChanged optional //set to TRUE afetr SIs-BR have been updated, set to FALSE after SIs-BR delivered to CSS @sic R5-167569 sic@
  };

  type record EUTRA_FDD_TDD_CellInfo_Type {     /* @status    APPROVED (ENDC, IMS, IMS_IRAT, LTE, LTE_A_IRAT, LTE_A_PRO, LTE_A_R10_R11, LTE_A_R12, LTE_IRAT, NBIOT, POS, UTRAN) */
    EUTRA_FDD_TDD_Mode_Type         FDD_TDD,                 // FDD or TDD mode
    CQI_ReportPeriodic              cqi_ReportConfig optional, // @sic R5s160586 sic@
    SoundingRS_UL_ConfigDedicated   soundingRS_UL_Config optional, // @sic R5s160586 sic@
    PUCCH_ConfigDedicated           pucch_Config,
    TDD_SubframeAssignment_Type     TDD_SubframeAssignment  optional,  // @sic R5-096691 sic@
    SR_ConfigIndex_Type             sr_ConfigIndex  // @sic R5-102177 sic@
  };

  type record Frequency_IE_Type {               /* @status    APPROVED (ENDC, IMS, IMS_IRAT, LTE, LTE_A_IRAT, LTE_A_PRO, LTE_A_R10_R11, LTE_A_R12, LTE_IRAT, NBIOT, POS, UTRAN) */
    integer            FrequencyBandIndicator,       // Frequency band indicator @sic R5-163085 FrequencyBandindicator type changed to integer sic@
    Dl_Bandwidth_Type  DL_ChBandwidth,               // DL channel bandwidth
    Ul_Bandwidth_Type  UL_ChBandwidth  optional,     // UL channel bandwidth
    CellCarrierFreqEUTRA_Type   UL_DL_Earfcn         // UL and DL EARFCN @sic R5-165892 Band extension sic@
  };

  type record CellIdentities_Type {             /* @status    APPROVED (ENDC, IMS, IMS_IRAT, LTE, LTE_A_IRAT, LTE_A_PRO, LTE_A_R10_R11, LTE_A_R12, LTE_IRAT, NBIOT, POS, UTRAN) */
    CellIdentity             CellIdentity,           // Cell ID
    PhysCellId               PhysicalCellId          // Physical cell ID
  };

  type record PhysicalParameters_Type {        /* @status    APPROVED (ENDC, IMS, IMS_IRAT, LTE, LTE_A_IRAT, LTE_A_PRO, LTE_A_R10_R11, LTE_A_R12, LTE_IRAT, NBIOT, POS, UTRAN)
                                                  @sic R5s090180 sic@ */
    CfiValue_Type                    Cfi,
    RACH_TimingAdvance_Type          RAR_TA,   // Random access response Timing Advance @sic R5-096691 sic@
    SR_PUCCH_ResourceIndex_Type      sr_PUCCH_ResourceIndex, // @sic R5s090312  sic@
    P_b_Type                         PDSCH_p_b  // @sic R5-100786 sic@
  };

  type record AntennaInfo_Type {               /* @status    APPROVED (ENDC, IMS, IMS_IRAT, LTE, LTE_A_IRAT, LTE_A_PRO, LTE_A_R10_R11, LTE_A_R12, LTE_IRAT, NBIOT, POS, UTRAN) */
    AntennaInfoCommon                               SS_AntennaInfoCommon,
    AntennaInfoCommon                               AntennaInfoCommon   optional,
    AntennaInfoDedicated                            SS_AntennaInfoDedicated,
    PhysicalConfigDedicated_AntennaInfo_Type        AntennaInfoDedicated
  };

  type record EUTRA_CellInfo_Type {             /* @status    APPROVED (ENDC, IMS, IMS_IRAT, LTE, LTE_A_IRAT, LTE_A_PRO, LTE_A_R10_R11, LTE_A_R12, LTE_IRAT, NBIOT, POS, UTRAN) */
    // this structure contain the information describing an EUTRA cell configuration
    CellIdentities_Type            CellIds,                     // cell ID + physical cell ID
    Frequency_IE_Type              Frequency,                   // Band, Bandwidth, UL EARFCN, DL EARFCN
    EUTRA_FDD_TDD_CellInfo_Type    FDD_TDD_Info,                // Include all FDD/TDD related cell information
    EUTRA_CellSysInfo_Type         Sysinfo,                     // Sysinfo
    NAS_Parameter_Type             NAS_Parameters,              // PLMN_List, TAC, Guti @sic R5s100240 delete PcchConfiguration field sic@
    CellTimingInfo_Type            CellTimingInfo,              // Tcell and SFN offset
    ReferenceCellPower_Type        ReferencePower,              // Cell power configuration
    CellConfigCapability_Type      CellConfigCapability,        // broadcastOnlyCell, minimumUplinkCell, fullCell
    RNTI_Type                      RNTI,                        // RNTI associated to the cell
    PhysicalParameters_Type        PhysicalParameters,          // Additional physical parameters
    AntennaInfo_Type               AntennaInfo,                 // Parameters for antenna info @sic R5-100786 sic@
    DrxCtrl_Type                   DrxCtrl,                     // @sic R5-104796 DRX support sic@
    MeasGapCtrl_Type               MeasGapCtrl,                 // @sic R5s110743 sic@
    boolean                        NoUL_Frequency,               // Set to TRUE when no UL frequency is applied in the band (e.g. band 29 or 32) @sic R5-134786 sic@
    integer                        SelectedPlmnIndex            // Set to the selected PLMN Id of the cell as selectedPLMN-Identity used in RRCConnectionSetupComplete @sic R5s160376 sic@
  };

  type record length(tsc_EUTRA_MaxNumberOfCells) of EUTRA_CellInfo_Type EUTRA_CellInfoList_Type;    /* array of Cells;     @status    APPROVED (ENDC, IMS, IMS_IRAT, LTE, LTE_A_IRAT, LTE_A_PRO, LTE_A_R10_R11, LTE_A_R12, LTE_IRAT, NBIOT, POS, UTRAN) */

  type enumerated EUTRA_4_5A_16_State_Type {    /* state of 36.508 procedure 4.5A.16; needed e.g. to connect to 2nd PDN for C2K test cases
                                                   @status    APPROVED (ENDC, IMS, IMS_IRAT, LTE, LTE_A_IRAT, LTE_A_PRO, LTE_A_R10_R11, LTE_A_R12, LTE_IRAT, NBIOT, POS, UTRAN) */
    step1_2,
    step3,
    step4,
    finished
  };

  type record of EPS_BearerIdentity EPS_BearerIdentityList;    /* @status    APPROVED (ENDC, IMS, IMS_IRAT, LTE, LTE_A_IRAT, LTE_A_PRO, LTE_A_R10_R11, LTE_A_R12, LTE_IRAT, NBIOT, POS, UTRAN) */

  type record EUTRA_PdnInfo_Type {              /* @status    APPROVED (ENDC, IMS, IMS_IRAT, LTE, LTE_A_IRAT, LTE_A_PRO, LTE_A_R10_R11, LTE_A_R12, LTE_IRAT, NBIOT, POS, UTRAN) */
    PDN_TypeToBeUsed_Type PdnTypeToBeUsed,
    B3_Type AssignedPdnType[2],                 /* tsc_PdnType_IPv4v6, tsc_PdnType_IPv4v6, tsc_PdnType_IPv4v6 as assigned to the UE;
                                                   @sic R5-112600 sic@
                                                   @sic R5s170307: Array of two to deal with second PDN sic@ */
    EPS_NetworkFeatureSupport NetworkFeatureSupport optional,     /* @sic R5-177127 sic@ NetworkFeatureSupport as indicated to the UE in ATTACH ACCEPT */
    EPS_BearerIdentityList PdnIds optional /* @sic R5-185166 sic@ */
  };

  type record EUTRA_MobileInfo_Type {           /* @status    APPROVED (ENDC, IMS, IMS_IRAT, LTE, LTE_A_IRAT, LTE_A_PRO, LTE_A_R10_R11, LTE_A_R12, LTE_IRAT, NBIOT, POS, UTRAN) */
    NAS_Tmsi    Tmsi,
    NAS_P_Tmsi  P_Tmsi,
    UE_EUTRA_Capability EUTRA_Capability,
    EUTRA_PdnInfo_Type PdnInfo,                 /* @sic R5s170307 sic@ */
    EUTRA_FDD_TDD_Mode_Type FDD_TDD_Info optional,        // Contain the FDD/TDD mode of the EUTRA cell the UE is currently in @sic 5s150293 sic@
    EmergencyNumList    LocalEmergencyNumberList optional,  /* emergency number list sent by UE in the ATTACH in the preamble @sic R5-125276 sic@ @sic R5s141315 sic@ */
    CoOrd_OtherRATState_Type OtherRATState  optional,        // @sic R5-163054 sic@
    boolean  UL_CA  // UE Uplink CA capability @sic R5s170737 sic@
  };

  type record EUTRA_Ctrl_Type {                                         /* @status    APPROVED (ENDC, IMS, IMS_IRAT, LTE, LTE_A_IRAT, LTE_A_PRO, LTE_A_R10_R11, LTE_A_R12, LTE_IRAT, NBIOT, POS, UTRAN) */
    default                             Default,                        /* default behaviour being activated on EUTRA */
    boolean                             TestBody,                       /* true:  => fail may be assigend
                                                                           false: => inconc is assigned instead of fail */
    VerdictCtrl_Type                    VerdictCtrl,                    /* Control the assignment of FAIL/INCOCN in default behaviour */
    HarqErrorHandling_Type              HarqErrorHandling,              /* @sic R5-106675 change 1 sic@ */
    EUTRA_UseBigGrants_Type             GrantType,                      /* @sic R5s140676 Change 2 sic@ */
    EUTRA_HandleUlDataUM_Type           HandleUlDataUM optional,        /* @sic R5s150338 - dynamic DRB configuration sic@ */
    EUTRA_4_5A_16_State_Type            EUTRA_4_5A_16_State,            /* @sic R5-145787, R5s150001 - Implementation for C2K sic@ */
    boolean                             LoopbackIsActivated             /* @sic R5-153971, R5s160716 sic@ */
  };

  type record EUTRA_ReceivedMsg_Type { /* @status    APPROVED (ENDC, IMS, IMS_IRAT, LTE, LTE_A_IRAT, LTE_A_PRO, LTE_A_R10_R11, LTE_A_R12, LTE_IRAT, NBIOT, POS, UTRAN) */
    //@sic R5s141018 sic@
    SRB_COMMON_IND              SRBmessage optional
  };

  type record EUTRA_Global_Type {                                       /* @status    APPROVED (ENDC, IMS, IMS_IRAT, LTE, LTE_A_IRAT, LTE_A_PRO, LTE_A_R10_R11, LTE_A_R12, LTE_IRAT, NBIOT, POS, UTRAN) */
    EUTRA_Ctrl_Type             Ctrl,
    EUTRA_SecurityParams_Type   Security,
    EUTRA_CellInfoList_Type     CellArray,
    EUTRA_MobileInfo_Type       MobileInfo,
    EUTRA_ReceivedMsg_Type      ReceivedMsg optional  //@sic R5s141018 sic@
  };

  type component EUTRA_PTC {                                            /* @status    APPROVED (ENDC, IMS, IMS_IRAT, LTE, LTE_A_IRAT, LTE_A_PRO, LTE_A_R10_R11, LTE_A_R12, LTE_IRAT, NBIOT, POS, UTRAN) */
    var  EUTRA_Global_Type vc_EUTRA_Global;

    port EUTRA_SYSTEM_PORT          SYS;
    port EUTRA_SYSIND_PORT          SYSIND;
    port EUTRA_VNG_PORT             VNG;
    port CDMA2000_TUNNELLING_PORT   C2KTUNNEL;                           /* @sic C2K TUNNELLING sic@ */
    port EUTRA_SRB_PORT             SRB;
    port EUTRA_NB_NASCTRL_PORT      NASCTRL;
    port EUTRA_DRB_PORT             DRB;
    port EUTRA_MRB_PORT             MRB;
    
    port UT_PTC_MTC_PORT            UT;
    
    port IRAT_CO_ORD_PORT           UTRAN;
    port IRAT_CO_ORD_PORT           GERAN;
    port IRAT_CO_ORD_PORT           CDMA2000;
    port IRAT_CO_ORD_PORT           WLAN;
    port IRAT_CO_ORD_PORT           NBIOT;                              // @sic R5-181279 sic@

    port IP_RAT_CTRL_PORT           IP;                                 /* @sic R5s100135 sic@ */
    port IMS_IPCAN_CO_ORD_PORT      IMS[tsc_NoOfPDNs];
  };

  type component EUTRA_SYSTEM {                                         /* @status    APPROVED (ENDC, IMS, IMS_IRAT, LTE, LTE_A_IRAT, LTE_A_PRO, LTE_A_R10_R11, LTE_A_R12, LTE_IRAT, NBIOT, POS, UTRAN) */
    port EUTRA_SYSTEM_PORT          E_SYS;
    port EUTRA_SYSIND_PORT          E_SYSIND;
    port EUTRA_VNG_PORT             E_VNG;
    port CDMA2000_TUNNELLING_PORT   E_C2KTUNNEL;                        /* @sic C2K TUNNELLING sic@ */
    port EUTRA_DRB_PORT             E_DRB;
    port EUTRA_MRB_PORT             E_MRB;
  };

  /*
   * @desc      Map EUTRA's system ports
   * @param     p_System
   * @param     p_Eutra
   * @param     p_C2KTunneling      (default value: false)
   * @status    APPROVED (ENDC, IMS, IMS_IRAT, LTE, LTE_A_IRAT, LTE_A_PRO, LTE_A_R10_R11, LTE_A_R12, LTE_IRAT, NBIOT, POS, UTRAN)
   */
  function f_EUTRA_PTC_Map(EUTRA_SYSTEM p_System,
                           EUTRA_PTC p_Eutra,
                           boolean p_C2KTunneling := false)
  {
    map(p_Eutra:SYS,    p_System:E_SYS);
    map(p_Eutra:SYSIND, p_System:E_SYSIND);
    map(p_Eutra:DRB,    p_System:E_DRB);

    if (f_GetTestcaseAttrib_Qbased_Rsrq(testcasename())) {    /* virtual noise generator for RSRQ test cases */
      map(p_Eutra:VNG,  p_System:E_VNG);
    }
    if (f_GetTestcaseAttrib_Mbms(testcasename())) {    /* Map the MRB port only for MBMS test cases */
      map(p_Eutra:MRB,  p_System:E_MRB);
    }
    if (p_C2KTunneling) {
      map(p_Eutra:C2KTUNNEL, p_System:E_C2KTUNNEL);
    }
  }

}
