/******************************************************************************/
// @copyright   Copyright Notification
//   No part may be reproduced except as authorized by written permission.
//   The copyright and the foregoing restriction extend to reproduction in all media.
//   Trademark 2019, 3GPP Organizational Partners (ARIB, ATIS, CCSA, ETSI, TSDSI, TTA, TTC).
//   All rights reserved.
// @version: 36.523-3 v15.3.0
// $Date: 2019-03-20 13:24:23 +0100 (Wed, 20 Mar 2019) $
// $Rev: 24274 $
/******************************************************************************/

module EPS_NAS_Templates {

  import from CommonDefs all;
  import from EPS_NAS_Constants all;
  import from EPS_NAS_MsgContainers all;
  import from EPS_NAS_TypeDefs all;
  import from EUTRA_NR_Parameters all;
  import from NAS_CommonTemplates all;
  import from NAS_CommonTypeDefs all;
  import from Parameters all;
  import from TestcaseProperties all;

  type enumerated BEARER_CONTEXT_TYPE {                /* @status    APPROVED (ENDC, IMS, IMS_IRAT, LTE, LTE_A_IRAT, LTE_A_PRO, LTE_A_R10_R11, LTE_A_R12, LTE_IRAT, NBIOT, POS) */
    DEF_1, // EPS Default Bearer#1 defined in the first column of table 6.6.1
    DEF_2, // EPS Default Bearer#2 defined in the second column of table 6.6.1
    DEF_3, // EPS Default Bearer#3 defined in the third column of table 6.6.1
    DED_1, // EPS Dedicated Bearer#1 defined in the first column of table 6.6.2
    DED_2, // EPS Dedicated Bearer#2 defined in the second column of table 6.6.2
    DED_3, // EPS Dedicated Bearer#3 defined in the third column of table 6.6.2
    DED_4  // EPS Dedicated Bearer#4 defined in the fourth column of table 6.6.2 @sic R5-123632 sic@
  };

  /*
   * @desc      return bearer context number: if p_BearerContextNumber is not present bearer context number acc. to UE capabilities
   * @param     p_BearerContextNumber
   * @return    BEARER_CONTEXT_TYPE
   * @status    APPROVED (ENDC, IMS, IMS_IRAT, LTE, LTE_A_IRAT, LTE_A_PRO, LTE_A_R10_R11, LTE_A_R12, LTE_IRAT, NBIOT, POS)
   */
  function fl_BearerContextNumber_Def(template (omit) BEARER_CONTEXT_TYPE p_BearerContextNumber) return BEARER_CONTEXT_TYPE
  {
    var BEARER_CONTEXT_TYPE v_BearerContextNumber;

    if (isvalue(p_BearerContextNumber)) {
      v_BearerContextNumber := valueof(p_BearerContextNumber);
    } else {
      if (pc_IMS) {
        v_BearerContextNumber := DEF_2;     /* @sic R5s141367: DEF_2 instead of DEF_1 sic@ */
      } else {
        v_BearerContextNumber := DEF_1;     /* @sic R5s141367: DEF_1 instead of DEF_2 sic@ */
      }
    }
    return v_BearerContextNumber;
  }

  template (value) WLANOffloadAcceptability cs_WLANOffload_EUTRA :=
  { /* @status    APPROVED (ENDC, IMS, IMS_IRAT, LTE, LTE_A_IRAT, LTE_A_PRO, LTE_A_R10_R11, LTE_A_R12, LTE_IRAT, NBIOT, POS) */
    iei       := 'C'H,
    spare     := tsc_Spare2,
    utranOffload := '0'B,
    eutranOffload := '1'B
  };

  /*
   * @desc      return template if testcase supports WLAN, otherwise omit
   * @return    template (omit) WLANOffloadAcceptability
   * @status    APPROVED (ENDC, IMS, IMS_IRAT, LTE, LTE_A_IRAT, LTE_A_PRO, LTE_A_R10_R11, LTE_A_R12, LTE_IRAT, NBIOT, POS)
   */
  function fl_GetWLANOffload_EUTRA() return template (omit) WLANOffloadAcceptability
  {
    var template (omit) WLANOffloadAcceptability v_Offload := omit;
    
    if (f_GetTestcaseAttrib_WLAN(testcasename())) {
      v_Offload := cs_WLANOffload_EUTRA;
    }
    return v_Offload;
  }

  /*
   * @desc      return SecurityHeaderType depending on given cause value
   * @param     p_Cause
   * @return    SecurityHeaderType
   * @status    APPROVED (LTE, LTE_A_R10_R11, LTE_IRAT, NBIOT)
   */
  function f_GetEPS_SecurityHeaderForReject(B8_Type p_Cause) return SecurityHeaderType
  { // @sic R5-174501 sic@
    var SecurityHeaderType v_Header;
    var integer v_CauseValue := bit2int(p_Cause);

    select (v_CauseValue) {
      case (3, 6, 7, 8, 11, 12, 13, 14, 15, 25, 35) {
        v_Header := tsc_SHT_IntegrityProtected_Ciphered;
      }
      case else {
        v_Header := tsc_SHT_NoSecurityProtection;
      }
    }
    return v_Header;
  }

  template (value) NAS_SecurityProtectionInfoDL_Type cs_NAS_SecurityProtectionInfo(SecurityHeaderType p_Status,
                                                                                   boolean p_ForceMacError := false) :=
  { /* @status    APPROVED (ENDC, IMS, IMS_IRAT, LTE, LTE_A_IRAT, LTE_A_PRO, LTE_A_R10_R11, LTE_A_R12, LTE_IRAT, NBIOT, POS) */
    Status := p_Status,
    ForceMacError := p_ForceMacError     // @sic R5s100358 additional changes sic@
  };

  template (present) NAS_SecurityProtectionInfoUL_Type cr_NAS_SecurityProtectionInfo(template (present) SecurityHeaderType p_ExpectedStatus) :=
  { /* @status    APPROVED (ENDC, IMS, IMS_IRAT, LTE, LTE_A_IRAT, LTE_A_PRO, LTE_A_R10_R11, LTE_A_R12, LTE_IRAT, NBIOT, POS) */
    Status := ?,//VZW NBIOT: Added '?' for for matching setup complete with UE
    NasCount := ?
  };

  template (value) NAS_MSG_Request_Type cs_NAS_Request(SecurityHeaderType p_SecurityStatus,
                                                       template (value) NAS_DL_Message_Type p_Msg) :=
  { /* @status    APPROVED (ENDC, IMS, IMS_IRAT, LTE, LTE_A_IRAT, LTE_A_PRO, LTE_A_R10_R11, LTE_A_R12, LTE_IRAT, NBIOT, POS) */
    SecurityProtection := cs_NAS_SecurityProtectionInfo(p_SecurityStatus),
    Pdu := {
      Msg := p_Msg,
      PiggybackedPduList := omit
    }
  };

  template (present) NAS_MSG_Indication_Type cr_NAS_Indication(template (present) SecurityHeaderType p_ExpectedSecurityStatus,
                                                               template (present) NAS_UL_Message_Type p_Msg) :=
  { /* @status    APPROVED (ENDC, IMS, IMS_IRAT, LTE, LTE_A_IRAT, LTE_A_PRO, LTE_A_R10_R11, LTE_A_R12, LTE_IRAT, NBIOT, POS) */
    SecurityProtection := cr_NAS_SecurityProtectionInfo(p_ExpectedSecurityStatus),
    Pdu := {
      Msg := p_Msg,
      PiggybackedPduList := *
    }
  };

  template (value) NAS_MSG_Request_Type cs_NAS_RequestWithPiggybacking(SecurityHeaderType p_SecurityStatus,
                                                                       template (value) NAS_DL_Message_Type p_EmmMsg,
                                                                       template (value) NAS_DL_Message_Type p_EsmMsg) :=
  { /* @status    APPROVED (ENDC, IMS, IMS_IRAT, LTE, LTE_A_IRAT, LTE_A_PRO, LTE_A_R10_R11, LTE_A_R12, LTE_IRAT, NBIOT, POS) */
    SecurityProtection := cs_NAS_SecurityProtectionInfo(p_SecurityStatus),
    Pdu := {
      Msg := p_EmmMsg,
      PiggybackedPduList := {
        {
          Msg := p_EsmMsg,
          PiggybackedPduList := omit
        }
      }
    }
  };

  template (present) NAS_MSG_Indication_Type cr_NAS_IndicationWithPiggybacking(template (present) SecurityHeaderType p_ExpectedSecurityStatus,
                                                                               template (present) NAS_UL_Message_Type p_EmmMsg,
                                                                               template (present) NAS_UL_Message_Type p_EsmMsg) :=
  { /* @status    APPROVED (ENDC, IMS, IMS_IRAT, LTE, LTE_A_IRAT, LTE_A_PRO, LTE_A_R10_R11, LTE_A_R12, LTE_IRAT, NBIOT, POS) */
    SecurityProtection := cr_NAS_SecurityProtectionInfo(p_ExpectedSecurityStatus),
    Pdu := {
      Msg := p_EmmMsg,
      PiggybackedPduList := {
        {
          Msg := p_EsmMsg,
          PiggybackedPduList := omit
        }
      }
    }
  };

  template (value) ServingPLMNRateControl cs_ServingPLMNRateControl(O2_Type p_Rate) :=
  { /* 24.301 cl. 9.9.4.28 */
    /* @status    APPROVED (ENDC, IMS, IMS_IRAT, LTE, LTE_A_IRAT, LTE_A_PRO, LTE_A_R10_R11, LTE_A_R12, LTE_IRAT, NBIOT, POS) */
    iei              := '6E'O,
    iel              := '02'O,
    servingPLMNRate  := p_Rate
  };

  template (present) AdditionalInfoReq cr_AdditionalInfoReqAny :=
  { /* 24.301 cl. 9.9.3.55 */
    /* @status    APPROVED (ENDC, IMS, IMS_IRAT, LTE, LTE_A_IRAT, LTE_A_PRO, LTE_A_R10_R11, LTE_A_R12, LTE_IRAT, NBIOT, POS) */
    iei       := '17'O,
    spare     := tsc_Spare7,
    cipherKey := ?
  };

  template (value) AdditionalUpdateResult cs_AdditionalUpdateResult(B2_Type p_AddUpdateResultValue) :=
  { /* 24.301 cl. 9.9.3.0A */
    /* @status    APPROVED (ENDC, IMS, IMS_IRAT, LTE, LTE_A_IRAT, LTE_A_PRO, LTE_A_R10_R11, LTE_A_R12, LTE_IRAT, NBIOT, POS) */
    /* @sic R5s120156 Additional Changes sic@ */
    iei       := 'F'H,                            // currently only used as TV
    spareBits    := tsc_Spare2,                 // @sic R5s170597 Baseline Moving sic@
    addUpdateResultValue := p_AddUpdateResultValue
  };

  template (value) AdditionalUpdateResult cs_AdditionalUpdateResult_SMSOnly := cs_AdditionalUpdateResult('10'B);   /* @status    APPROVED (ENDC, IMS, IMS_IRAT, LTE, LTE_A_IRAT, LTE_A_PRO, LTE_A_R10_R11, LTE_A_R12, LTE_IRAT, NBIOT, POS) */

  template (present) LinkedEpsBearerIdentity cr_LinkedId(B4_Type p_Id) :=
  { /* @status    APPROVED (LTE, LTE_A_R10_R11, NBIOT) */
    idValue := p_Id
  };

  template (present) EMM_Cause cr_EMM_Cause_v(template (present) B8_Type p_CauseValue := ?) :=
  { /* @status    APPROVED (IMS_IRAT, LTE, LTE_IRAT, NBIOT) */
    iei := omit,
    causeValue := p_CauseValue
  };

  template (value) EMM_Cause cs_EMM_Cause_v(B8_Type p_CauseValue) :=
  { /* @status    APPROVED (IMS_IRAT, LTE, LTE_A_IRAT, LTE_A_R10_R11, LTE_IRAT, NBIOT) */
    iei := omit,
    causeValue := p_CauseValue
  };

  template (value) EMM_Cause cs_EMM_Cause_tv(B8_Type p_CauseValue) :=
  { /* @status    APPROVED (LTE, LTE_A_PRO, LTE_A_R10_R11, LTE_IRAT, NBIOT) */
    iei := '53'O,
    causeValue := p_CauseValue
  };

  template (value) ESM_Cause cs_ESM_Cause_v(B8_Type p_CauseValue) :=
  { /* @status    APPROVED (ENDC, IMS, IMS_IRAT, LTE, LTE_A_IRAT, LTE_A_PRO, LTE_A_R10_R11, LTE_A_R12, LTE_IRAT, NBIOT, POS) */
    iei := omit,
    causeValue := p_CauseValue
  };

  template (value) ESM_Cause cs_ESM_Cause_tv(B8_Type p_CauseValue) :=
  { /* @status    APPROVED (ENDC, IMS, IMS_IRAT, LTE, LTE_A_IRAT, LTE_A_PRO, LTE_A_R10_R11, LTE_A_R12, LTE_IRAT, NBIOT, POS) */
    iei := '58'O,
    causeValue := p_CauseValue
  };

  template (value) ESM_MessageContainer cs_Dummy_ESM_Message :=
  { /* This is just a dummy to use in the EMM message as the NAS Emulator encodes the ESM message;
       @status    APPROVED (ENDC, IMS, IMS_IRAT, LTE, LTE_A_IRAT, LTE_A_PRO, LTE_A_R10_R11, LTE_A_R12, LTE_IRAT, NBIOT, POS) */
    iei := omit,
    iel := '0000'O,
    esmPdu := ''O
  };

  template (present) ESM_MessageContainer cr_ESM_MessageContainer :=
  { /* @status    APPROVED (ENDC, IMS, IMS_IRAT, LTE, LTE_A_IRAT, LTE_A_PRO, LTE_A_R10_R11, LTE_A_R12, LTE_IRAT, NBIOT, POS) */
    iei := '78'O ifpresent, // @sic R5s170364 sic@
    iel := ?,
    esmPdu := ?
  };

  template (present) ESM_InfoTransferFlag cr_ESMInfoTransferFlag_Any :=
  { /* 24.301 cl. 9.9.4.5; */
    /* @status    APPROVED (ENDC, IMS, IMS_IRAT, LTE, LTE_A_IRAT, LTE_A_PRO, LTE_A_R10_R11, LTE_A_R12, LTE_IRAT, NBIOT, POS) */
    iei  := 'D'H,
    spare    := tsc_Spare3,
    eitValue := ?
  };

  template (present) GUTI_Type cr_GUTI_Type_Any :=
  { /* 24.301 cl. 9.9.3.45 @sic R5s110178 Baseline Moving sic@ */
    /* @status    APPROVED (ENDC, IMS, IMS_IRAT, LTE, LTE_A_IRAT, LTE_A_PRO, LTE_A_R10_R11, LTE_A_R12, LTE_IRAT, NBIOT, POS) */
    iei      := 'E'H,
    spare    := tsc_Spare3,
    gutiType := ?
  };

  template (present) EPS_AttachType cr_EPS_AttachType(template (present) NAS_AttDetValue_Type p_ResultValue) :=
  { /* @status    APPROVED (ENDC, IMS, IMS_IRAT, LTE, LTE_A_IRAT, LTE_A_PRO, LTE_A_R10_R11, LTE_A_R12, LTE_IRAT, NBIOT, POS) */
    spare     := tsc_Spare1,
    typeValue := p_ResultValue
  };

  template (value) EPS_AttachResult cs_EPS_AttachResult(NAS_AttDetValue_Type p_ResultValue) :=
  { /* @status    APPROVED (ENDC, IMS, IMS_IRAT, LTE, LTE_A_IRAT, LTE_A_PRO, LTE_A_R10_R11, LTE_A_R12, LTE_IRAT, NBIOT, POS) */
    spare       := tsc_Spare1,
    resultValue := p_ResultValue
  };

  template (value) EPS_NetworkFeatureSupport cs_EPS_NwkFtSup_Common(B1_Type p_Esrps   := '0'B,
                                                                    B2_Type p_CsLCS   := '00'B,
                                                                    B1_Type p_EpcLCS  := '0'B,
                                                                    B1_Type p_EmcBS   := '0'B,
                                                                    B1_Type p_ImsVoPS := '0'B,
                                                                    O1_Type p_IEL     := '01'O, // @sic R5-169126 sic@
                                                                    template (omit) B1_Type p_CP := '0'B, // @sic R5-166331 sic@
                                                                    template (omit) B1_Type p_WO_PDN := '0'B, // @sic R5-166331 sic@
                                                                    template (omit) B1_Type p_Bearers15 := omit,
                                                                    template (omit) B1_Type p_RestrictEC := omit,
                                                                    template (omit) B1_Type p_ExtdPCO := omit,
                                                                    template (omit) B1_Type p_HeaderComp_CP_CIOT := omit,
                                                                    template (omit) B1_Type p_S1_U_data := omit,
                                                                    template (omit) B1_Type p_UP_CIOT := omit,
                                                                    template (omit) B1_Type p_RestrictDCNR := omit,
                                                                    template (omit) B1_Type p_IwkN26 := omit) :=
  { /* @status    APPROVED (ENDC, IMS, IMS_IRAT, LTE, LTE_A_IRAT, LTE_A_PRO, LTE_A_R10_R11, LTE_A_R12, LTE_IRAT, NBIOT, POS) */
    iei           := '64'O, /* present in case of TV; omit in case of V */
    iel           := p_IEL, /* present in case of LV or TLV; omit in case of V */
    cp_CIOT     := p_CP,                      // @sic R5s110176, R5s160711 Baseline Moving sic@
    emmRegistered_withoutPDN := p_WO_PDN,         // @sic R5s160711 Baseline Moving sic@
    esrps         := p_Esrps,                       // @sic R5s120178 Baseline Moving sic@
    csLCS         := p_CsLCS,                       // @sic R5s110176, R5s120178 Baseline Moving sic@
    epcLCS        := p_EpcLCS,                      // @sic R5s110176 Baseline Moving sic@
    emcBS         := p_EmcBS,                       // @sic R5s110176, R5s120178 Baseline Moving; R5-112289: '00'B -> '01'B sic@
    imsVoPS       := p_ImsVoPS,
    bearers15     := p_Bearers15, // @sic R5s160711 Baseline Moving, R5s180552 Baseline Moving 2018 Phase 2 sic@
    iwkN26        := p_IwkN26, // @sic R5s180271 Baseline Moving 2018 Phase 1, R5s180552 Baseline Moving 2018 Phase 2 sic@
    restrictDCNR  := p_RestrictDCNR, // @sic R5s180271 Baseline Moving 2018 Phase 1 sic@
    restrictEC    := p_RestrictEC, // @sic R5s170597 Baseline Moving sic@
    extdPCO       := p_ExtdPCO, // @sic R5s160711 Baseline Moving sic@
    headerComp_CP_CIOT := p_HeaderComp_CP_CIOT, // @sic R5s160711 Baseline Moving sic@
    s1_U_data     := p_S1_U_data, // @sic R5s160711 Baseline Moving sic@
    up_CIOT       := p_UP_CIOT // @sic R5s160711 Baseline Moving sic@
    
  };

  template (value) EPS_NetworkFeatureSupport cs_EPS_NwkFtSup_IMSVoice              := cs_EPS_NwkFtSup_Common(-, -, -, -, '1'B);            /* @status    APPROVED (ENDC, IMS, IMS_IRAT, LTE, LTE_A_IRAT, LTE_A_PRO, LTE_A_R10_R11, LTE_A_R12, LTE_IRAT, NBIOT, POS) */

  template (value) EPS_NetworkFeatureSupport cs_EPS_NwkFtSup_CP := cs_EPS_NwkFtSup_Common(-, -, -, -, -, '02'O, '1'B, '1'B, tsc_Spare1, '0'B, '1'B, '1'B, '0'B, '0'B, '0'B, '0'B);   /* @status    APPROVED (NBIOT) @sic R5s180428 sic@ */

  template (value) EPS_NetworkFeatureSupport cs_EPS_NwkFtSup_UP := cs_EPS_NwkFtSup_Common(-, -, -, -, -, '02'O, '1'B, '0'B, tsc_Spare1, '0'B, '1'B, '0'B, '1'B, '1'B, '0'B, '0'B);   /* @status    APPROVED (NBIOT) @sic R5-172853, R5-174640, R5s180428 sic@ */

  template (value) EPS_NetworkFeatureSupport cs_EPS_NwkFtSup_S1U := cs_EPS_NwkFtSup_Common(-, -, -, -, -, '02'O, '1'B, '0'B, tsc_Spare1, '0'B, '1'B, '0'B, '1'B, '0'B, '0'B, '0'B);  /* @status    APPROVED (NBIOT) @sic R5-174640, R5s180428 sic@ */

  template (present) EPS_UpdateType cr_EPS_UpdateType(template (present) B1_Type p_ActiveFlag,
                                                      template (present) EPS_UpdateTypeValue p_EpsUpdateType) :=
  { /* @status    APPROVED (ENDC, IMS, IMS_IRAT, LTE, LTE_A_IRAT, LTE_A_PRO, LTE_A_R10_R11, LTE_A_R12, LTE_IRAT, NBIOT, POS) */
    activeFlag := p_ActiveFlag,
    typeValue  := p_EpsUpdateType
  };

  template (value) EPS_UpdateResult cs_EPS_UpdateResult(NAS_AttDetValue_Type p_UpdateValue) :=
  { /* @status    APPROVED (ENDC, IMS, IMS_IRAT, LTE, LTE_A_IRAT, LTE_A_PRO, LTE_A_R10_R11, LTE_A_R12, LTE_IRAT, NBIOT, POS) */
    spare       := '0'B,
    resultValue := p_UpdateValue
  };

  template (present) Request_Type cr_EPS_PdnRequestType(template (present) B3_Type p_TypeValue) :=
  { /* @status    APPROVED (ENDC, IMS, IMS_IRAT, LTE, LTE_A_IRAT, LTE_A_PRO, LTE_A_R10_R11, LTE_A_R12, LTE_IRAT, NBIOT, POS) */
    spare     := tsc_Spare1,
    typeValue := p_TypeValue
  };

  template (present) EPS_BearerContextStatus cr_508_EPS_BearerContextStatusTAUReq(template (present) B3_Type p_EBI5_7 := ?, B4_Type p_EBI4_1 := '0000'B) :=
  { /* See 36.508 clause Table 4.7.2-27 */
    /* @status    APPROVED (ENDC, IMS, IMS_IRAT, LTE, LTE_A_IRAT, LTE_A_PRO, LTE_A_R10_R11, LTE_A_R12, LTE_IRAT, NBIOT, POS) */
    iei := '57'O,
    iel := '02'O,
    ebi5_7 := p_EBI5_7, /* EBI(5)- EBI(7) */
    ebi4_1 := p_EBI4_1, /* EBI(1)- EBI(4) @sic R5s180552 Baseline Moving 2018 Phase 2 sic@ */
    spare := tsc_Spare1, /* EBI(0) is spare and shall be coded as zero*/
    ebi8_15 := '00000000'B
  };

  template (present) PDN_Type cr_PDN_Type(template (present) PdnTypeValue p_TypeValue) :=
  { /* @status    APPROVED (ENDC, IMS, IMS_IRAT, LTE, LTE_A_IRAT, LTE_A_PRO, LTE_A_R10_R11, LTE_A_R12, LTE_IRAT, NBIOT, POS) */
    spare      := tsc_Spare1,
    typeValue  := p_TypeValue
  };

  template (present) TrackingAreaId cr_TAI_Any :=
  { /* 24.301 cl. 9.9.3.32;
       @status    APPROVED (ENDC, IMS, IMS_IRAT, LTE, LTE_A_IRAT, LTE_A_PRO, LTE_A_R10_R11, LTE_A_R12, LTE_IRAT, NBIOT, POS) */
    iei := '52'O,
    plmnId := ?,                        /* MCC  + MNC 3 digits each */
    tac := ?                            /* TAC */
  };

  template (present) TrackingAreaId cr_TAI(NAS_PlmnId p_PlmnId,  // O3_Type
                                           NAS_Tac    p_Tac) :=  // O2_Type
  { /* @status    APPROVED (LTE, LTE_A_PRO, LTE_A_R10_R11, LTE_A_R12, LTE_IRAT, NBIOT) */
    iei := '52'O,
    plmnId := p_PlmnId,
    tac := p_Tac
  };

  template (value) PartialTaiType cs_NonConsecutivePartialTaiList (NAS_PlmnId  p_PlmnId,
                                                                   NAS_TacList p_TacList):=
  { /* @status    APPROVED (ENDC, IMS, IMS_IRAT, LTE, LTE_A_IRAT, LTE_A_PRO, LTE_A_R10_R11, LTE_A_R12, LTE_IRAT, NBIOT, POS) */
    type0List := {
      {
        plmnId  := p_PlmnId,
        tacList := p_TacList
      }
    }
  };

  template (value) ListOfPartialTaiList cs_ListNonConsecutivePartialTaiList (NAS_PlmnId  p_PlmnId,
                                                                             NAS_TacList p_TacList):=
  { /* @status    APPROVED (ENDC, IMS, IMS_IRAT, LTE, LTE_A_IRAT, LTE_A_PRO, LTE_A_R10_R11, LTE_A_R12, LTE_IRAT, NBIOT, POS) */
    spareBit         := tsc_Spare1,
    typeOfList       := tsc_NonConsecutiveTacs,
    numberOfElements := int2bit(lengthof(p_TacList) - 1, 5),
    partialTaiList   := cs_NonConsecutivePartialTaiList (p_PlmnId, p_TacList)
  };

  template (value) TrackingAreaIdList cs_TAIListNonConsecutive_lv(NAS_PlmnId  p_PlmnId,
                                                                  NAS_TacList p_TacList) :=
  { /* @status    APPROVED (ENDC, IMS, IMS_IRAT, LTE, LTE_A_IRAT, LTE_A_PRO, LTE_A_R10_R11, LTE_A_R12, LTE_IRAT, NBIOT, POS) */
    iei := omit,
    iel := int2oct(4 + 2 * lengthof(p_TacList), 1),
    listOfPartialTais := {cs_ListNonConsecutivePartialTaiList (p_PlmnId, p_TacList) }
  };

  template (value) TrackingAreaIdList cds_TAIListNonConsecutive_tlv (NAS_PlmnId  p_PlmnId,
                                                                     NAS_TacList p_TacList)
    modifies cs_TAIListNonConsecutive_lv  :=
  { /* @status    APPROVED (ENDC, IMS, IMS_IRAT, LTE, LTE_A_IRAT, LTE_A_PRO, LTE_A_R10_R11, LTE_A_R12, LTE_IRAT, NBIOT, POS) */
    iei := '54'O // TS 24.301 clause 8.2.26
  };

  template (value) ListOfPartialTaiList cs_PartialTaiListMultiplePlmns2 (template(value)  PartialTaiType p_PartialTAIList) :=
  { /* @status    APPROVED (LTE, NBIOT) */
    spareBit         := tsc_Spare1,
    typeOfList       := tsc_MultiplePlmns,
    numberOfElements := '00001'B, // Value 00001 means a list of 2 cells, see 24.301 Table 9.9.3.33.1
    partialTaiList   := p_PartialTAIList
  };

  template (value) TrackingAreaIdList cs_TAIListMultiplePlmns2(template(value)  PartialTaiType p_PartialTAI) :=
  { /* @status    APPROVED (LTE, NBIOT) */
    iei := omit,
    iel := int2oct(11, 1),
    listOfPartialTais := {cs_PartialTaiListMultiplePlmns2(p_PartialTAI)}
  };

  template (value) TrackingAreaIdList cs_TAIListMultiplePlmns3 (template(value) PartialTaiType p_MultiplePlmnPartialTAI,
                                                                integer p_NumOfConsecutiveTACs,
                                                                template(value)  PartialTai p_ConsecutivePartialTAI,
                                                                NAS_PlmnId  p_NonConsecutivePlmnId,
                                                                NAS_TacList p_NonConsecutiveTacList) :=
  {  /* @status    APPROVED (LTE, NBIOT) */
    iei := omit,
    iel := int2oct(32, 1),
    listOfPartialTais := {cs_PartialTaiListMultiplePlmns3 (p_MultiplePlmnPartialTAI),
                          cs_PartialTaiListConsecutive (p_NumOfConsecutiveTACs, p_ConsecutivePartialTAI),
                          cs_ListNonConsecutivePartialTaiList (p_NonConsecutivePlmnId, p_NonConsecutiveTacList)
    }
  };

  template (value) ListOfPartialTaiList cs_PartialTaiListConsecutive (integer p_NumOfTACs, template(value)  PartialTai p_PartialTAI) :=
  { /* @status    APPROVED (LTE, NBIOT) */
    spareBit         := tsc_Spare1,
    typeOfList       := tsc_ConsecutiveTacs,
    numberOfElements := int2bit((p_NumOfTACs - 1), 5),
    partialTaiList := {
      type1List := {
        {
          partialTac := p_PartialTAI
        }
      }
    }
  };

template (value) ListOfPartialTaiList cs_PartialTaiListMultiplePlmns3(template(value)  PartialTaiType p_PartialTAIList) :=
  { /* @status    APPROVED (LTE, NBIOT) */
    spareBit         := tsc_Spare1,
    typeOfList       := tsc_MultiplePlmns,
    numberOfElements := '00010'B, // Value 00010 means a list of 3 cells, see 24.301 Table 9.9.3.33.1
    partialTaiList   := p_PartialTAIList
  };

  template (value) PartialTaiType cs_MultiplePlmnsList (template (value) PlmnAndTacs p_PlmnAndTacs) :=
  { /* @status    APPROVED (LTE, NBIOT) */
    type2List := {
      {
        plmnAndTacs := p_PlmnAndTacs
      }
    }
  };

template (value) PartialTai cs_PartialTAI(NAS_PlmnId  p_PlmnId,
                                            NAS_Tac p_Tac) :=
  { /* @status    APPROVED (LTE, NBIOT) */
    plmnId := p_PlmnId,
    tac := p_Tac
  };

  template (present) UE_NetworkCap cdr_EUTRA_UENetworkCap_Any (template (omit) IEI8_Type p_IEI)
           modifies cr_UENetworkCap_Any :=
  { /* @status    APPROVED (ENDC, IMS, IMS_IRAT, LTE, LTE_A_IRAT, LTE_A_PRO, LTE_A_R10_R11, LTE_A_R12, LTE_IRAT, NBIOT, POS) */
    cIoT_V2X := ('0'B & ? length (2) & f_ConvertBoolToBit(pc_EN_DC) & ? length (4)) ifpresent // @sic R5-182425 sic@
  };

  template (present) HeaderCompressConfig cr_HeaderCompressConfigAny :=
  { /* 24.301 cl. 9.9.4.22 */
    /* @status    APPROVED (ENDC, IMS, IMS_IRAT, LTE, LTE_A_IRAT, LTE_A_PRO, LTE_A_R10_R11, LTE_A_R12, LTE_IRAT, NBIOT, POS) */
    iei                   := '66'O,
    iel                   := ?,
    spare                 := tsc_Spare1,
    profileP0x0104        := ?,
    profileP0x0103        := ?,
    profileP0x0102        := ?,
    profileP0x0006        := ?,
    profileP0x0004        := ?,
    profileP0x0003        := ?,
    profileP0x0002        := ?,
    maxCID                := ?,
    addHeaderCompressContextSetupParamsType := *, // @sic R5s170597 Baseline Moving sic@
    addHeaderCompressContextSetupParams   := *    // @sic R5s170597 Baseline Moving sic@
    };

  template (present) EPS_QualityOfService cr_EPS_QoS_Any_lv :=
  { /* 24.301 cl. 9.9.4.3 */
    /* @status    APPROVED (LTE, LTE_A_R10_R11, NBIOT) */
    iei := omit,
    iel := ?,
    qci                    := ?,
    maxBitRateUl           := *,
    maxBitRateDl           := *,
    guaranteedBitRateUl    := *,
    guaranteedBitRateDl    := *,
    maxBitRateUlExt        := *,
    maxBitRateDlExt        := *,
    guaranteedBitRateUlExt := *,
    guaranteedBitRateDlExt := *,
    maxBitRateUL_Ext2         := *,  // @sic R5s1300195 Baseline Moving, R5s180640 sic@
    maxBitRateDL_Ext2       := *,  // @sic R5s1300195 Baseline Moving sic@
    guaranteedBitRateUL_Ext2  := *,  // @sic R5s1300195 Baseline Moving sic@
    guaranteedBitRateDL_Ext2  := *   // @sic R5s1300195 Baseline Moving sic@
  };

  template (present) EPS_QualityOfService cdr_EPS_QoS_Any_tlv
  modifies cr_EPS_QoS_Any_lv :=
  { /* @status    APPROVED (LTE, LTE_A_R10_R11, NBIOT) */
    iei := '5B'O
  };
//Chenged for arg p_T3346 NBIOT 4.6
  template (value) NAS_DL_Message_Type cs_508_AttachReject(template (value) EMM_Cause p_Cause,
                                                           template (omit) GPRS_Timer2 p_T3346 := omit,
                                                           template (omit) GPRS_Timer2 p_T3402 := omit,
                                                           template (omit) Extd_EMM_Cause p_Extd_EMM_Cause := omit):=
  { /* @status    APPROVED (LTE, LTE_A_R10_R11, LTE_IRAT, NBIOT) */
    aTTACH_REJECT := {
      securityHeaderType := tsc_SHT_NoSecurityProtection,
      protocolDiscriminator := tsc_PD_EMM,
      messageType := tsc_MT_AttachReject,
      emmCause := p_Cause,
      esmMessage := omit,   /* always omit as being added by the NAS emulator (when an ESM message is handed over by using cs_NAS_RequestWithPiggybacking) */
      t3346    := p_T3346,      /* cl. 9.9.3.16A O TLV 3     IEI=0x5F @sic R5s120178 Baseline Moving sic@*/
      t3402    := p_T3402,   /* cl. 9.9.3.16A O TLV 3     IEI=0x16 @sic R5s120178 Baseline Moving sic@*/
      extdEMMCause   := p_Extd_EMM_Cause // @sic R5s150329 Baseline Moving sic@
    }
  };
    
    

  template (value) NAS_DL_Message_Type cs_AUTHENTICATION_REQUEST(NAS_KsiValue p_AsmeKeySetId,
                                                                 B128_Type p_RAND,
                                                                 B128_Type p_AUTN) :=
  { /* 24.301 cl. 8.2.7 */
    /* @status    APPROVED (ENDC, IMS, IMS_IRAT, LTE, LTE_A_IRAT, LTE_A_PRO, LTE_A_R10_R11, LTE_A_R12, LTE_IRAT, NBIOT, POS) */
    aUTHENTICATION_REQUEST := {
      securityHeaderType    := tsc_SHT_NoSecurityProtection,
      protocolDiscriminator := tsc_PD_EMM,
      messageType           := tsc_MT_AuthenticationRequest,
      spareHalfOctet        := tsc_SpareHalfOctet,
      asmeNasKeySetId       := cs_NAS_KeySetIdentifier_lv(p_AsmeKeySetId, tsc_NasKsi_NativeSecurityContext),
      rand                  := cs_RAND_v(p_RAND),
      autn                  := cs_AUTN_lv(p_AUTN)
    }
  };

  template (present) NAS_UL_Message_Type cr_AUTHENTICATION_RESPONSE :=
  { /* @status    APPROVED (ENDC, IMS, IMS_IRAT, LTE, LTE_A_IRAT, LTE_A_PRO, LTE_A_R10_R11, LTE_A_R12, LTE_IRAT, NBIOT, POS) */
    aUTHENTICATION_RESPONSE := {
      securityHeaderType              := tsc_SHT_NoSecurityProtection,
      protocolDiscriminator           := tsc_PD_EMM,
      messageType                     := tsc_MT_AuthenticationResponse,
      authenticationResponseParameter := cr_AuthenticationResponseParameter_lv(?)  // accept any RES in the first place
    }
  };

  template (present) NAS_UL_Message_Type cr_AUTHENTICATION_FAIL_Any (template (present) EMM_Cause p_EMM_Cause := ?,
                                                                     template  AuthenticationFailureParameter p_AuthenticationFailureParameter := omit) :=
  { /* @status    APPROVED (ENDC, IMS, IMS_IRAT, LTE, LTE_A_IRAT, LTE_A_PRO, LTE_A_R10_R11, LTE_A_R12, LTE_IRAT, NBIOT, POS) */
    aUTHENTICATION_FAILURE := {
      securityHeaderType       := tsc_SHT_NoSecurityProtection,
      protocolDiscriminator    := tsc_PD_EMM,
      messageType              := tsc_MT_AuthenticationFailure,
      emmCause                 := p_EMM_Cause,          /* cl. 9.9.3.7  M V 1   */
      authenticationFailureParameter := p_AuthenticationFailureParameter  // @sic R5s140854 sic@
    }
  };
    
  //VZW NBIOT Adding template to handle Sync failure
     template (present) NAS_UL_Message_Type cr_AUTHENTICATION_SYNC_FAIL (template (present) EMM_Cause p_EMM_Cause := ?,
                                                                     template  AuthenticationFailureParameter p_AuthenticationFailureParameter := ?) :=
  { 
    aUTHENTICATION_FAILURE := {
      securityHeaderType       := tsc_SHT_NoSecurityProtection,
      protocolDiscriminator    := tsc_PD_EMM,
      messageType              := tsc_MT_AuthenticationFailure,
      emmCause                 := p_EMM_Cause,          /* cl. 9.9.3.7  M V 1   */
      authenticationFailureParameter := p_AuthenticationFailureParameter  // @sic R5s140854 sic@
    }
  }; 

  template (present) EMM_Cause cr_EMM_Cause_AuthFail_Mac := cr_EMM_Cause_v(tsc_Cause_MAC_Failure); /* @status    APPROVED (IMS_IRAT, LTE, LTE_IRAT, NBIOT) */

  template (present) EMM_Cause cr_EMM_Cause_AuthFail_SQN := cr_EMM_Cause_v(tsc_Cause_SQN_Failure); /* @status    APPROVED (IMS_IRAT, LTE, LTE_IRAT, NBIOT) */

  template (present) EMM_Cause cr_EMM_Cause_AuthFail_nonEPS_AuthUnacceptable := cr_EMM_Cause_v(tsc_Cause_nonEPS_AuthUnacceptable);                                      /* @status    APPROVED (LTE, NBIOT) */

  template (present) NAS_UL_Message_Type cr_AUTHENTICATION_FAIL_synch := cr_AUTHENTICATION_FAIL_Any(cr_EMM_Cause_AuthFail_SQN, cr_AuthFailParam_Any);                   /* @status    APPROVED (IMS_IRAT, LTE, LTE_IRAT, NBIOT) */

  template (present) NAS_UL_Message_Type cr_AUTHENTICATION_FAIL_mac := cr_AUTHENTICATION_FAIL_Any (cr_EMM_Cause_AuthFail_Mac);                                          /* @status    APPROVED (IMS_IRAT, LTE, LTE_IRAT, NBIOT) */

  template (present) NAS_UL_Message_Type cr_AUTHENTICATION_FAIL_nonEPS_AuthUnacc := cr_AUTHENTICATION_FAIL_Any (cr_EMM_Cause_AuthFail_nonEPS_AuthUnacceptable);         /* @status    APPROVED (LTE, NBIOT) */

  template (value) NAS_DL_Message_Type cs_508_SECURITY_MODE_COMMAND(B3_Type p_CipheringType,
                                                                    B3_Type p_IntegrityType,
                                                                    NAS_KsiValue p_AsmeKeySetId,
                                                                    UE_SecurityCapability p_SecurityCap,
                                                                    template (omit) UE_AdditionalSecurityCapability p_AddSecurityCap) :=
  { /* @status    APPROVED (ENDC, IMS, IMS_IRAT, LTE, LTE_A_IRAT, LTE_A_PRO, LTE_A_R10_R11, LTE_A_R12, LTE_IRAT, NBIOT, POS) */
    sECURITY_MODE_COMMAND := {
      securityHeaderType         := tsc_SHT_NoSecurityProtection,
      protocolDiscriminator      := tsc_PD_EMM,
      messageType                := tsc_MT_SecurityModeCommand,
      nasSecurityAlgorithms      := cs_NAS_SecurityAlgorithms(p_CipheringType, p_IntegrityType),
      spareHalfOctet             := tsc_SpareHalfOctet,
      asmeNasKeySetId            := cs_NAS_KeySetIdentifier_lv(p_AsmeKeySetId, tsc_NasKsi_NativeSecurityContext),
      replayedSecurityCapability := p_SecurityCap,  // @sic R5s100135 sic@
      imeisvRequest              := omit,
      replayedNonceUe            := omit,
      nonceMme                   := omit,
      hashMME                    := omit,  // @sic R5s170597 Baseline Moving sic@
      replayedUEAddSecurityCap   := p_AddSecurityCap   // @sic R5s180271 Baseline Moving 2018 Phase 1 sic@
    }
  };

  template (present) NAS_UL_Message_Type cr_508_SECURITY_MODE_COMPLETE :=
  { /* @status    APPROVED (ENDC, IMS, IMS_IRAT, LTE, LTE_A_IRAT, LTE_A_PRO, LTE_A_R10_R11, LTE_A_R12, LTE_IRAT, NBIOT, POS) */
    sECURITY_MODE_COMPLETE := {
      securityHeaderType    := tsc_SHT_NoSecurityProtection,
      protocolDiscriminator := tsc_PD_EMM,
      messageType           := tsc_MT_SecurityModeComplete,
      imeisv                := omit,
      replayedNASMsg        := *  /* cl. 9.9.3.51 O TLV-E 3-n    IEI=0x79 @sic R5s170597 Baseline Moving sic@ */
    }
  };

  template (value) NAS_DL_Message_Type cs_ATTACH_ACCEPT_Common(NAS_AttDetValue_Type p_EpsAttachResultValue,
                                                               template (value) GPRS_Timer p_T3412,
                                                               template (value) TrackingAreaIdList p_TaiList,
                                                               template (omit) MobileIdentity p_Guti,
                                                               template (omit) LocAreaId p_Lai,
                                                               template (omit) MobileIdentity p_MSId,
                                                               template (omit) EMM_Cause p_Cause,
                                                               template (omit) GPRS_Timer p_T3402,
                                                               template (omit) GPRS_Timer p_T3423,
                                                               template (omit) PLMN_List p_EquivalentPlmnList,
                                                               template (omit) EmergNumList p_EmergNumList,
                                                               template (omit) EPS_NetworkFeatureSupport p_NtwkFeatSupport,
                                                               template (omit) AdditionalUpdateResult p_AdditionalUpdateResult,
                                                               template (omit) GPRS_Timer3 p_T3412Extd,
                                                               template (omit) GPRS_Timer2 p_T3324,
                                                               template (omit) ExtdDRXParams p_ExtdDRXParams := omit,
                                                               template (omit) DCN_ID p_DCN_ID := omit,
                                                               template (omit) SMSServicesStatus p_SMSServicesStatus := omit,
                                                               template (omit) Non3GPP_NW_ProvidedPolicies p_Non3GPPPolicies := omit,
                                                               template (omit) GPRS_Timer2 p_T3448 := omit,
                                                               template (omit) NetworkPolicy p_NwkPolicy := omit,
                                                               template (omit) GPRS_Timer3 p_T3447Value := omit,
                                                               template (omit) ExtdEmergNumList p_ExtdEmergNumList := omit,
                                                               template (omit) CipheringKeyData p_CipheringKeyData := omit) :=
  { /* @status    APPROVED (ENDC, IMS, IMS_IRAT, LTE, LTE_A_IRAT, LTE_A_PRO, LTE_A_R10_R11, LTE_A_R12, LTE_IRAT, NBIOT, POS) */
    /* @sic R5s100780 sic@ */
    aTTACH_ACCEPT := {
      securityHeaderType    := tsc_SHT_NoSecurityProtection,
      protocolDiscriminator := tsc_PD_EMM,
      messageType           := tsc_MT_AttachAccept,
      spareHalfOctet        := tsc_SpareHalfOctet,
      epsAttachResult       := cs_EPS_AttachResult(p_EpsAttachResultValue),
      t3412                 := p_T3412,
      taiList               := p_TaiList,
      esmMessage            := cs_Dummy_ESM_Message,    // dummy is needed as esmMessage is mandatory parameter
      guti                  := p_Guti,
      lai                   := p_Lai,
      msIdentity            := p_MSId,
      emmCause              := p_Cause,
      t3402                 := p_T3402,
      t3423                 := p_T3423,
      equivalentPlmns       := p_EquivalentPlmnList,
      emergencyNumberList   := p_EmergNumList,
      epsNetworkFeatureSupport := p_NtwkFeatSupport,  // @sic R5-103674 sic@
      addUpdateResult       := p_AdditionalUpdateResult,      // @sic R5-103681 sic@
      t3412ExtdValue        := p_T3412Extd,   /* cl. 9.9.3.16B O TLV 3     IEI=0x5E @sic R5s120178 Baseline Moving sic@*/
      t3324Value            := p_T3324, // @sic R5s150329 Baseline Moving sic@
      extendedDRXParams     := p_ExtdDRXParams,  // @sic R5s160711 Baseline Moving sic@
      dcnId                  := p_DCN_ID,   /* cl. 9.9.3.48 O TLV 4    IEI=0x65 @sic R5s170597 Baseline Moving sic@ */
      smsServicesStatus      := p_SMSServicesStatus,   /* cl. 9.9.3.4b O TLV 1    IEI=0xE @sic R5s170597 Baseline Moving sic@ */
      non3GPP_NW_ProvidedPolicies := p_Non3GPPPolicies,   /* cl. 9.9.3.49 O TLV 1    IEI=0xD @sic R5s170597 Baseline Moving sic@ */
      t3448                  := p_T3448,    /* cl. 9.9.3.16A O TLV 3    IEI=0x6B @sic R5s170597 Baseline Moving sic@ */
      networkPolicy          := p_NwkPolicy, // @sic R5s180271 Baseline Moving 2018 Phase 1 sic@
      t3447Value             := p_T3447Value, // @sic R5s180271 Baseline Moving 2018 Phase 1 sic@
      xtdEmergencyNumList    := p_ExtdEmergNumList,    /* cl. 9.9.3.37A O TLV-E 6-65538    IEI=0x7A @sic R5s180552 Baseline Moving 2018 Phase 2 sic@ */
      cipheringKeyData       := p_CipheringKeyData     /* cl. 9.9.3.56 O TLV-E 35-2291    IEI=0x7C @sic R5s180552 Baseline Moving 2018 Phase 2 sic@ */
      }
  };

  template (present) NAS_UL_Message_Type cr_508_ATTACH_COMPLETE :=
  { /* @status    APPROVED (ENDC, IMS, IMS_IRAT, LTE, LTE_A_IRAT, LTE_A_PRO, LTE_A_R10_R11, LTE_A_R12, LTE_IRAT, NBIOT, POS) */
    aTTACH_COMPLETE := {
      securityHeaderType    := tsc_SHT_NoSecurityProtection,
      protocolDiscriminator := tsc_PD_EMM,
      messageType           := tsc_MT_AttachComplete,
      esmMessage            := cr_ESM_MessageContainer
    }
  };

  template (value) NAS_DL_Message_Type cs_508_DETACH_REQUEST_MT(template (value) DetachType p_DetachType,
                                                                template (omit) EMM_Cause p_Cause) :=
  { /* @status    APPROVED (IMS_IRAT, LTE, LTE_A_R10_R11, NBIOT) */
    dETACH_REQUEST_MT := {
      securityHeaderType    := tsc_SHT_NoSecurityProtection,
      protocolDiscriminator := tsc_PD_EMM,
      messageType           := tsc_MT_DetachRequest,
      spareHalfOctet        := tsc_SpareHalfOctet,
      detachType            := p_DetachType,
      emmCause              := p_Cause
    }
  };

  template (value) NAS_DL_Message_Type cs_508_DETACH_ACCEPT :=
  { /* @status    APPROVED (IMS_IRAT, LTE, LTE_IRAT, NBIOT) */
    dETACH_ACCEPT := {
      securityHeaderType    := tsc_SHT_NoSecurityProtection,
      protocolDiscriminator := tsc_PD_EMM,
      messageType           := tsc_MT_DetachAccept
    }
  };

  template (present) NAS_UL_Message_Type cr_508_DETACH_ACCEPT :=
  { /* @status    APPROVED (IMS_IRAT, LTE, LTE_A_R10_R11, NBIOT) */
    dETACH_ACCEPT := {
      securityHeaderType    := tsc_SHT_NoSecurityProtection,
      protocolDiscriminator := tsc_PD_EMM,
      messageType           := tsc_MT_DetachAccept
    }
  };

  template (present) NAS_UL_Message_Type cr_508_DETACH_REQUEST_MO(template (present) NAS_KeySetIdentifier p_NasKeySetId,
                                                                  template (present) MobileIdentity p_MobileId,
                                                                  template (present) DetachType p_DetachType) :=  // @sic R5-103675 sic@
  { /* @status    APPROVED (ENDC, IMS, IMS_IRAT, LTE, LTE_A_IRAT, LTE_A_PRO, LTE_A_R10_R11, LTE_A_R12, LTE_IRAT, NBIOT, POS) */
    dETACH_REQUEST_MO := {
      securityHeaderType := tsc_SHT_NoSecurityProtection,
      protocolDiscriminator := tsc_PD_EMM,
      messageType := tsc_MT_DetachRequest,
      asmeNasKeySetId := p_NasKeySetId,
      detachType := p_DetachType,
      epsMobileIdentity := p_MobileId  // @sic R5s110176 Baseline Moving sic@
    }
  };

  template (present) NAS_MessageContainer cr_NAS_MsgContainer_SMS(template (present) octetstring p_SMS_Message,
                                                                  template (omit) IEI8_Type p_IEI := omit) := // @sic R5s180287 sic@
  { /* @status    APPROVED (LTE, LTE_A_PRO, NBIOT) */
    iei         := p_IEI,
    iel         := ?,
    smsmessage  := p_SMS_Message
  };

  template (value) NAS_MessageContainer cs_NAS_MsgContainer_SMS(template (value) octetstring p_SMS_Message) :=
  { /* @status    APPROVED (LTE, LTE_A_PRO, NBIOT) */
    iei         := omit,
    iel         := int2oct(lengthof (p_SMS_Message), 1),
    smsmessage  := p_SMS_Message
  };

  template (value) NAS_DL_Message_Type cs_IDENTITY_REQUEST(NAS_IdType p_IdType) :=
  { /* @status    APPROVED (LTE, LTE_A_R10_R11, NBIOT) */
    iDENTITY_REQUEST :={
      securityHeaderType                := tsc_SHT_NoSecurityProtection,
      protocolDiscriminator             := tsc_PD_EMM,
      messageType                       := tsc_MT_IdentityRequest,
      spareHalfOctet                    := tsc_SpareHalfOctet,
      identityType2                     := cs_IdentityType(p_IdType)
    }
  };

  template (present) NAS_UL_Message_Type cr_IDENTITY_RESPONSE(template (present) MobileIdentity p_MobileId) :=
  { /* @status    APPROVED (LTE, LTE_A_R10_R11, NBIOT) */
    iDENTITY_RESPONSE :={
      securityHeaderType                := tsc_SHT_NoSecurityProtection,
      protocolDiscriminator             := tsc_PD_EMM,
      messageType                       := tsc_MT_IdentityResponse,
      mobileIdentity                    := p_MobileId
    }
  };

  template (value) NAS_DL_Message_Type cds_SECURITY_MODE_COMMAND_IMEISV(B3_Type p_CipheringType,
                                                                        B3_Type p_IntegrityType,
                                                                        NAS_KsiValue p_AsmeKeySetId,
                                                                        UE_SecurityCapability p_SecurityCap,// @sic R5s100135 sic@
                                                                        template (omit) UE_AdditionalSecurityCapability p_AddSecurityCap)
    modifies cs_508_SECURITY_MODE_COMMAND :=
  { /* @status    APPROVED (LTE, NBIOT) */
    sECURITY_MODE_COMMAND := {
      imeisvRequest              := cs_IMEISV_Request_WithIEI( tsc_IMEISV_Requested )//@sic R5s090210 sic@ @sic R5s130438 sic@
    }
  };

  template (present) NAS_UL_Message_Type cdr_SECURITY_MODE_COMPLETE_IMEISV modifies cr_508_SECURITY_MODE_COMPLETE :=
  { /* @status    APPROVED (LTE, NBIOT) */
    sECURITY_MODE_COMPLETE := {
      // securityHeaderType    := tsc_SHT_NoSecurityProtection,
      // protocolDiscriminator := tsc_PD_EMM,
      // messageType           := tsc_MT_SecurityModeComplete,
      imeisv                := ?
    }
  };

  template (present) NAS_UL_Message_Type cr_SECURITY_MODE_REJECT(template (present) NAS_CauseValue_Type p_CauseValue) :=
  { /* @status    APPROVED (LTE, NBIOT) */
    sECURITY_MODE_REJECT := {
      securityHeaderType    := tsc_SHT_NoSecurityProtection,
      protocolDiscriminator := tsc_PD_EMM,
      messageType           := tsc_MT_SecurityModeReject,
      emmCause              := { iei := omit,
                                 causeValue := p_CauseValue } // @sic R5s100182 sic@
    }
  };

  template (value) NAS_DL_Message_Type cs_508_SERVICE_REJECT(NAS_CauseValue_Type p_CauseValue,
                                                             template (omit) GPRS_Timer p_T3442 := omit,
                                                             template (omit) GPRS_Timer2 p_T3346 := omit,
                                                             template (omit) GPRS_Timer2 p_T3448 := omit) :=
  { /* @status    APPROVED (LTE, LTE_A_R10_R11, LTE_IRAT, NBIOT) */
    sERVICE_REJECT := {
      securityHeaderType    := tsc_SHT_NoSecurityProtection,
      protocolDiscriminator := tsc_PD_EMM,
      messageType           := tsc_MT_ServiceReject,
      emmCause              := cs_EMM_Cause_v(p_CauseValue),
      t3442                 := p_T3442,
      t3346                 := p_T3346, /* cl. 9.9.3.16A O TLV 3   IEI=0x5F @sic R5s120178 Baseline Moving sic@ */
      t3448                 := p_T3448    /* cl. 9.9.3.16A O TLV 3    IEI=0x6B @sic R5s170597 Baseline Moving sic@ */
    }
  };

  template (value) NAS_DL_Message_Type cs_TAU_Accept_Common(NAS_AttDetValue_Type p_UpdateValue,
                                                            template (omit) MobileIdentity p_Guti,
                                                            template (omit) TrackingAreaIdList p_TaiList,
                                                            template (omit) EPS_BearerContextStatus p_EPSCtxtStatus, // @sic R5s100608 sic@
                                                            template (omit) LocAreaId p_LAI,
                                                            template (omit) MobileIdentity p_MS_Id,
                                                            template (omit) AdditionalUpdateResult p_AdditionalUpdateResult,
                                                            template (omit) EMM_Cause p_EmmCause := omit,
                                                            template (omit) GPRS_Timer p_T3402 := omit,
                                                            template (omit) PLMN_List p_EPLMNlist := omit,
                                                            template (omit) EPS_NetworkFeatureSupport p_NwkFeatSup := cs_EPS_NwkFtSup_IMSVoice,
                                                            template (omit) GPRS_Timer p_T3412 := cs_GprsTimer_tv('5A'O, tsc_GprsTimerUnit_deact, '00000'B),
                                                            template (omit) EmergNumList p_EmgNumList := omit,  // @sic R5s120178 Baseline Moving sic@
                                                            template (omit) GPRS_Timer3 p_T3412Extd := omit,
                                                            template (omit) GPRS_Timer2 p_T3324 := omit,
                                                            template (omit) ExtdDRXParams p_ExtdDRXParams := omit,
                                                            template (omit) HeaderCompressConfigStatus p_HeaderCompressConfigStatus := omit,
                                                            template (omit) DCN_ID p_DCN_ID := omit,
                                                            template (omit) SMSServicesStatus p_SMSServicesStatus := omit,
                                                            template (omit) Non3GPP_NW_ProvidedPolicies p_Non3GPPPolicies := omit,
                                                            template (omit) GPRS_Timer2 p_T3448 := omit,
                                                            template (omit) NetworkPolicy p_NwkPolicy := omit,
                                                            template (omit) GPRS_Timer3 p_T3447Value := omit,
                                                            template (omit) ExtdEmergNumList p_ExtdEmergNumList := omit,
                                                            template (omit) CipheringKeyData p_CipheringKeyData := omit) :=
  { /* @status    APPROVED (ENDC, IMS, IMS_IRAT, LTE, LTE_A_IRAT, LTE_A_PRO, LTE_A_R10_R11, LTE_A_R12, LTE_IRAT, NBIOT, POS) */
    /* @sic R5s110168 ch 2.2 additional MCC160 comments sic@ */
    tRACKING_AREA_UPDATE_ACCEPT := {
      securityHeaderType     := tsc_SHT_NoSecurityProtection,
      protocolDiscriminator  := tsc_PD_EMM,
      messageType            := tsc_MT_TauAccept,
      spareHalfOctet         := tsc_SpareHalfOctet,
      epsUpdateResult        := cs_EPS_UpdateResult(p_UpdateValue),
      t3412                  := p_T3412,   // @sic R5-101147 sic@  @sic R5-115720 sic@
      guti                   := p_Guti,
      taiList                := p_TaiList,
      epsBearerContextStatus := p_EPSCtxtStatus,
      lai                    := p_LAI,
      msIdentity             := p_MS_Id,
      emmCause               := p_EmmCause,
      t3402                  := p_T3402,
      t3423                  := omit,
      equivalentPlmns        := p_EPLMNlist,
      asmeNasKeySetId        := omit,
      emergencyNumberList    := p_EmgNumList,
      epsNetworkFeatureSupport := p_NwkFeatSup,  // @sic R5-110771 sic@
      addUpdateResult        := p_AdditionalUpdateResult,      // @sic R5-103681 sic@
      t3412ExtdValue         := p_T3412Extd,   /* cl. 9.9.3.16B O TLV 3     IEI=0x5E @sic R5s120178 Baseline Moving sic@*/
      t3324Value             := p_T3324, // @sic R5s150329 Baseline Moving sic@
      extendedDRXParams      := p_ExtdDRXParams, // @sic R5s160711 Baseline Moving sic@
      headerCompressionConfigStatus := p_HeaderCompressConfigStatus, // @sic R5s160711 Baseline Moving sic@
      dcnId                  := p_DCN_ID,   /* cl. 9.9.3.48 O TLV 4    IEI=0x65 @sic R5s170597 Baseline Moving sic@ */
      smsServicesStatus      := p_SMSServicesStatus,   /* cl. 9.9.3.4b O TLV 1    IEI=0xE @sic R5s170597 Baseline Moving sic@ */
      non3GPP_NW_ProvidedPolicies := p_Non3GPPPolicies,   /* cl. 9.9.3.49 O TLV 1    IEI=0xD @sic R5s170597 Baseline Moving sic@ */
      t3448                   := p_T3448,    /* cl. 9.9.3.16A O TLV 3    IEI=0x6B @sic R5s170597 Baseline Moving sic@ */
      networkPolicy          := p_NwkPolicy, // @sic R5s180271 Baseline Moving 2018 Phase 1 sic@
      t3447Value             := p_T3447Value, // @sic R5s180271 Baseline Moving 2018 Phase 1 sic@
      xtdEmergencyNumList    := p_ExtdEmergNumList,    /* cl. 9.9.3.37A O TLV-E 6-65538    IEI=0x7A @sic R5s180552 Baseline Moving 2018 Phase 2 sic@ */
      cipheringKeyData       := p_CipheringKeyData     /* cl. 9.9.3.56 O TLV-E 35-2291    IEI=0x7C @sic R5s180552 Baseline Moving 2018 Phase 2 sic@ */
      }
  };

  template (present) NAS_UL_Message_Type cdr_TAU_Request_ExtdDrx_T3324(template (present) EPS_UpdateTypeValue p_EpsUpdate_TypeValue,
                                                                       NAS_KsiValue p_KsiValue,
                                                                       template DRXparameter p_DRXparameter := cr_DRXparameter_Any ('5C'O) ifpresent,
                                                                       template AdditionalUpdateType p_AdditionalUpdateType,
                                                                       template (present) MobileIdentity p_OldGuti,
                                                                       template (present) TrackingAreaId p_LastTai,
                                                                       template GPRS_Timer2 p_T3324 := omit,
                                                                       template ExtdDRXParams p_EDRX := omit)
    modifies cr_508_TAU_Request :=
  { /* @status    APPROVED (LTE_A_PRO, NBIOT) */
    tRACKING_AREA_UPDATE_REQUEST := {
      oldGuti                  := p_OldGuti,
      lastVisitedRegisteredTai := p_LastTai,
      t3324Value               := p_T3324,
      extendedDRXParams        := p_EDRX //new Param Rel13
    }
  };

  template (present) NAS_UL_Message_Type cr_508_TAU_Complete :=
  { /* @status    APPROVED (ENDC, IMS, IMS_IRAT, LTE, LTE_A_IRAT, LTE_A_PRO, LTE_A_R10_R11, LTE_A_R12, LTE_IRAT, NBIOT, POS) */
    tRACKING_AREA_UPDATE_COMPLETE := {
      securityHeaderType    := tsc_SHT_NoSecurityProtection,
      protocolDiscriminator := tsc_PD_EMM,
      messageType           := tsc_MT_TauComplete
    }
  };

  template (present) NAS_UL_Message_Type cr_508_TAU_Request(template (present) EPS_UpdateTypeValue p_EpsUpdate_TypeValue,
                                                            NAS_KsiValue p_KsiValue,
                                                            template DRXparameter p_DRXparameter, // @sic R5s120210 sic@
                                                            template AdditionalUpdateType p_AdditionalUpdateType) :=
  { /* @status    APPROVED (ENDC, IMS, IMS_IRAT, LTE, LTE_A_IRAT, LTE_A_PRO, LTE_A_R10_R11, LTE_A_R12, LTE_IRAT, NBIOT, POS) */
    tRACKING_AREA_UPDATE_REQUEST := {
      securityHeaderType         := tsc_SHT_NoSecurityProtection,
      protocolDiscriminator      := tsc_PD_EMM,
      messageType                := tsc_MT_TauRequest,
      asmeNasKeySetId            := cr_NAS_KeySetIdentifier(p_KsiValue, tsc_NasKsi_NativeSecurityContext),
      epsUpdateType              := cr_EPS_UpdateType(tsc_EpsUpdate_NotActive, p_EpsUpdate_TypeValue),
      oldGuti                    := cr_MobileIdAny,
      nonCurrentNativeNasKeySetId  := omit,   /* @sic R5s100135 sic@ */
      gprsCipheringKeySeqNum     := cr_CiphKeySeqNum('8'H, ?) ifpresent,        /* @sic R5s100135, R5-135073 sic@ */
      oldPtmsiSignature          := omit,
      additionalGuti             := omit,
      nonce                      := omit,
      ueNetworkCapability        := cdr_EUTRA_UENetworkCap_Any ('58'O) ifpresent, // @sic R5s180271 Baseline Moving 2018 Phase 1 sic@
      lastVisitedRegisteredTai   := cr_TAI_Any ifpresent,
      drxParameter               := p_DRXparameter, // @sic R5s100660, R5s120210 sic@
      ueRadioCapNeeded           := cr_UeRadioCap ifpresent, // @sic R5-112120 sic@
      epsBearerContextStatus     := cr_508_EPS_BearerContextStatusTAUReq(('000'B, '001'B, '011'B))  ifpresent,
      msNetworkCapability        := cr_MS_NetworkCap_Any ifpresent,
      oldLai                     := cr_LAI_Any ifpresent,
      tmsiStatus                 := cr_TMSI_Status ifpresent,
      msClassmark2               := cr_MS_Clsmk2_Any_tlv ifpresent,  // @sic R5s100662 sic@
      msClassmark3               := cr_MSCLSMK3_Any ifpresent,
      supportedCodecList         := cr_CodecListAny ifpresent,
      addUpdateType              := p_AdditionalUpdateType,         /* @sic R5-103681 sic@ */
      voiceDomainPref            := cr_VoiceDomainPref_Any ifpresent, /* @sic R5s110176 Baseline Moving sic@ */
      oldGUTI_Type               := cr_GUTI_Type_Any ifpresent,  /* cl. 9.9.3.45 O TV 1 IEI=0xE- @sic R5s120178 Baseline Moving sic@*/
      deviceProperties           := cr_DeviceProperties('D'H) ifpresent,  /* cl. 9.9.2.0A O TV 1 IEI=0xD- @sic R5s120178 Baseline Moving sic@*/
      msNetworkFeatureSupport    := cr_MS_NetworkFeatureSupport('C'H) ifpresent,   /* cl. 9.9.2.0A O TV 1 IEI=0xC- @sic R5s120178 Baseline Moving sic@*/
      tmsiBasedNRIContainer      := cr_NwkResourceIdAny ifpresent, // @sic R5s130195 Baseline Moving sic@
      t3324Value                 := cr_GPRS_Timer2_3('6A'O) ifpresent, // @sic R5s150329 Baseline Moving sic@
      t3412ExtdValue             := cr_GPRS_Timer2_3 ('5E'O) ifpresent, // @sic R5s150329 Baseline Moving sic@
      extendedDRXParams          := cr_ExtdDRXParamsAny ifpresent,  // @sic R5s160711 Baseline Moving sic@
      ueAddSecurityCap           := f_GetUE_AdditionalSecurityCapability(), // @sic R5s180271 Baseline Moving 2018 Phase 1 sic@
      ueStatus                   := cr_UEStatus_Any ifpresent,    /* cl. 9.9.3.54 O TLV 3    IEI=0x6D @sic R5s180552 Baseline Moving 2018 Phase 2 sic@ */
      addInfoReq                 := cr_AdditionalInfoReqAny ifpresent     /* cl. 9.9.3.55 O TLV 2    IEI=0x17 @sic R5s180552 Baseline Moving 2018 Phase 2 sic@ */
     }
  };

  template (value) NAS_DL_Message_Type cs_508_TRACKING_AREA_UPDATE_REJECT(B8_Type p_EmmCause,
                                                                          template (omit) GPRS_Timer2 p_T3346 := omit,
                                                                          template (omit) Extd_EMM_Cause p_Extd_EMM_Cause := omit) :=
  { /* @status    APPROVED (IMS_IRAT, LTE, LTE_A_IRAT, LTE_A_R10_R11, LTE_IRAT, NBIOT) */
    tRACKING_AREA_UPDATE_REJECT := {
      securityHeaderType        := tsc_SHT_NoSecurityProtection,
      protocolDiscriminator     := tsc_PD_EMM,
      messageType               := tsc_MT_TauReject,
      emmCause                  := cs_EMM_Cause_v(p_EmmCause),
      t3346                     := p_T3346,  // @sic R5s120178 Baseline Moving sic@
      extdEMMCause              := p_Extd_EMM_Cause // @sic R5s150329 Baseline Moving sic@
    }
  };

  template (present) UERadioCapInfoUpdateNeeded cr_UeRadioCap :=
  { /* @status    APPROVED (ENDC, IMS, IMS_IRAT, LTE, LTE_A_IRAT, LTE_A_PRO, LTE_A_R10_R11, LTE_A_R12, LTE_IRAT, NBIOT, POS) */
    iei := 'A'H,
    spare := '000'B,
    urcUpdate := '1'B
  };

  template (present) NAS_UL_Message_Type cr_PDNDisconnectReq(template (present) LinkedEpsBearerIdentity p_LinkedId):=
  { /* @desc PDN DISCONNECT REQUEST
       @status    APPROVED (IMS, IMS_IRAT, LTE, LTE_A_IRAT, LTE_A_PRO, LTE_A_R10_R11, LTE_A_R12, LTE_IRAT, NBIOT, POS) */
    pDN_DISCONNECT_REQUEST := {
      epsBearerId                    := '0'H,
      protocolDiscriminator          := tsc_PD_ESM,
      procedureTransactionIdentifier := ?,
      messageType                    := tsc_MT_PdnDisconnectRequest,
      spareHalfOctet                 := ?,
      linkedEpsBearerId              := p_LinkedId,
      protocolConfigurationOptions   := cr_ProtocolConfigOptionsAny ifpresent,
      extdProtocolConfigurationOptions  := cr_ExtdPCOAny ifpresent  // @sic R5s160711 Baseline Moving sic@
    }
  };

  template (present) NAS_UL_Message_Type cr_PDN_CONNECTIVITY_REQUEST_Common(EPS_BearerIdentity p_BearerId := '0'H,
                                                                            template (present) ProcedureTransactionIdentifier p_TI := ?,
                                                                            template (present) PdnTypeValue p_PDNTypeValue,
                                                                            template (present) B3_Type p_RequestTypeValue,
                                                                            template ESM_InfoTransferFlag p_Flag := omit,
                                                                            template AccessPointName p_APN := omit,
                                                                            template ProtocolConfigOptions p_PCO := cr_ProtocolConfigOptionsAny ifpresent,
                                                                            template DeviceProperties p_DeviceProperties := cr_DeviceProperties('C'H) ifpresent,
                                                                            template NBIFOMContainer p_NBIFOMContainer := cr_NBIFOMContainerAny ifpresent,
                                                                            template HeaderCompressConfig p_HeaderCompressConfig := cr_HeaderCompressConfigAny ifpresent,
                                                                            template ExtdProtocolConfigOptions p_ExtdProtocolConfigOptions := cr_ExtdPCOAny ifpresent) :=
  { /* @status    APPROVED (ENDC, IMS, IMS_IRAT, LTE, LTE_A_IRAT, LTE_A_PRO, LTE_A_R10_R11, LTE_A_R12, LTE_IRAT, NBIOT, POS) */
    pDN_CONNECTIVITY_REQUEST := {
      epsBearerId                    := p_BearerId,
      protocolDiscriminator          := tsc_PD_ESM,
      procedureTransactionIdentifier := p_TI,
      messageType                    := tsc_MT_PdnConnectivityRequest,
      pdnType                        := cr_PDN_Type(p_PDNTypeValue),
      requestType                    := cr_EPS_PdnRequestType(p_RequestTypeValue), // TP: initial attach
      esmInfoTransferFlag            := p_Flag,
      accessPointName                := p_APN,
      protocolConfigurationOptions   := p_PCO,
      deviceProperties               := p_DeviceProperties,  // @sic R5s120178 Baseline Moving sic@
      nbifomContainer                := p_NBIFOMContainer, // @sic R5s160711 Baseline Moving sic@
      headerCompressionConfig        := p_HeaderCompressConfig, // @sic R5s160711 Baseline Moving sic@
      extdProtocolConfigurationOptions  := p_ExtdProtocolConfigOptions   // @sic R5s160711 Baseline Moving sic@
    }
  };

  template (value) NAS_DL_Message_Type cs_508_ActivateDefEpsBearerContextRequestAll(EPS_BearerIdentity p_EpsBearerId,
                                                                                    template (value) ProcedureTransactionIdentifier p_ProcTi,
                                                                                    template (value) EPS_QualityOfService p_EPS_Qos,
                                                                                    template (value) AccessPointName p_APN,
                                                                                    template (omit) PDN_Address p_Address,
                                                                                    template (omit) TransactionIdentifier p_TI,
                                                                                    template (omit) QualityOfService  p_NegQos,
                                                                                    template (omit) LLC_SAPI p_LlcSapi,
                                                                                    template (omit) RadioPriority p_RadioPriority,
                                                                                    template (omit) PacketFlowIdentifier p_PFI,
                                                                                    template (omit) APN_AMBR p_ApnAggMaxBitRate,
                                                                                    template (omit) ESM_Cause p_Cause,
                                                                                    template (omit) ProtocolConfigOptions p_PCO,
                                                                                    template (omit) NBIFOMContainer p_NBIFOMContainer,
                                                                                    template (omit) HeaderCompressConfig p_HeaderCompressConfig,
                                                                                    template (omit) ControlPlaneInd p_ControlPlaneInd,
                                                                                    template (omit) ExtdProtocolConfigOptions p_ExtdProtocolConfigOptions,
                                                                                    template (omit) ServingPLMNRateControl p_ServingPLMNRateControl,
                                                                                    template (omit) Extd_APN_AMBR p_Extd_APN_AMBR) :=
  { /* @status    APPROVED (ENDC, IMS, IMS_IRAT, LTE, LTE_A_IRAT, LTE_A_PRO, LTE_A_R10_R11, LTE_A_R12, LTE_IRAT, NBIOT, POS) */
    aCTIVATE_DEFAULT_EPS_BEARER_CONTEXT_REQUEST := {
      epsBearerId                       := p_EpsBearerId,
      protocolDiscriminator             := tsc_PD_ESM,
      procedureTransactionIdentifier    := p_ProcTi,
      messageType                       := tsc_MT_ActivateDefaultEpsBearerContextRequest,
      epsQos                            := p_EPS_Qos,
      accessPointName                   := p_APN,
      pdnAdress                         := p_Address,
      transactionIdentifier             := p_TI,
      negotiatedQos                     := p_NegQos,
      negotiatedLlcSapi                 := p_LlcSapi,
      radioPriority                     := p_RadioPriority,
      packetFlowIdentifier              := p_PFI,
      apnAggMaxBitRate                  := p_ApnAggMaxBitRate,
      esmCause                          := p_Cause,
      protocolConfigurationOptions      := p_PCO,
      connectivityType                  := omit, // @sic R5s120178 Baseline Moving sic@
      wlanOffloadInd                    := fl_GetWLANOffload_EUTRA(), //  @sic R5-151995 sic@
      nbifomContainer                   := p_NBIFOMContainer, // @sic R5s160711 Baseline Moving sic@
      headerCompressionConfig           := p_HeaderCompressConfig,  // @sic R5s160711 Baseline Moving sic@
      controlPlaneOnlyIndication        := p_ControlPlaneInd,  // @sic R5s160711 Baseline Moving sic@
      extdProtocolConfigurationOptions  := p_ExtdProtocolConfigOptions,  // @sic R5s160711 Baseline Moving sic@
      servingPLMNRateControl            := p_ServingPLMNRateControl,  // @sic R5s160711 Baseline Moving sic@
      extd_APN_AMBR                     := p_Extd_APN_AMBR // @sic R5s180271 Baseline Moving 2018 Phase 1 sic@
    }
  };

  template (present) NAS_UL_Message_Type cr_ActivateDefEpsBearerContextAccept_Common(EPS_BearerIdentity p_EpsBearerId,
                                                                                     template (value) ProcedureTransactionIdentifier p_ProcTi := '00'O) :=
  { /* @status    APPROVED (ENDC, IMS, IMS_IRAT, LTE, LTE_A_IRAT, LTE_A_PRO, LTE_A_R10_R11, LTE_A_R12, LTE_IRAT, NBIOT, POS) */
    aCTIVATE_DEFAULT_EPS_BEARER_CONTEXT_ACCEPT := {
      epsBearerId                       := p_EpsBearerId,
      protocolDiscriminator             := tsc_PD_ESM,
      procedureTransactionIdentifier    := p_ProcTi,
      messageType                       := tsc_MT_ActivateDefaultEpsBearerContextAccept,
      protocolConfigurationOptions      := cr_ProtocolConfigOptionsAny ifpresent,
      extdProtocolConfigurationOptions  := cr_ExtdPCOAny ifpresent  // @sic R5s160711 Baseline Moving sic@
    }
  };

  template (present) NAS_UL_Message_Type cr_508_ActivateDefEpsBearerContextAccept(EPS_BearerIdentity p_EpsBearerId) :=
  /* @status    APPROVED (ENDC, IMS, IMS_IRAT, LTE, LTE_A_IRAT, LTE_A_PRO, LTE_A_R10_R11, LTE_A_R12, LTE_IRAT, NBIOT, POS) */
    cr_ActivateDefEpsBearerContextAccept_Common(p_EpsBearerId);

  template (present) NAS_UL_Message_Type cr_508_BearerResourceModReq(template (present) ProcedureTransactionIdentifier p_ProcTi,
                                                                     EPS_BearerIdentity p_LinkedEpsBearerId,
                                                                     template ESM_Cause p_Cause) :=
  { /* @status    APPROVED (LTE, LTE_A_R10_R11, NBIOT) */
    bEARER_RESOURCE_MODIFICATION_REQUEST := {
      epsBearerId                    := '0'H,
      protocolDiscriminator          := tsc_PD_ESM,
      procedureTransactionIdentifier := p_ProcTi,
      messageType                    := tsc_MT_BearerResourceModificationRequest,
      spareHalfOctet                 := tsc_SpareHalfOctet,
      epsBearerIdentityforPacketFilter := cr_LinkedId(hex2bit(p_LinkedEpsBearerId)),   // of the associated dedicated bearer
      trafficFlowAgg                 := cr_Tft(-, '100'B),  // Replace packet filters from existing TFT
      requiredTrafficFlowQoS         := cdr_EPS_QoS_Any_tlv ifpresent, // @sic R5s100692 sic@ @sic R5s110682 sic@
      esmCause                       := p_Cause,
      protocolConfigurationOptions   := cr_ProtocolConfigOptionsAny ifpresent,
      deviceProperties               := cr_DeviceProperties('C'H) ifpresent,  /* cl. 9.9.2.0A O TV 1 IEI=0xC- @sic R5s120178 Baseline Moving sic@*/
      nbifomContainer                := cr_NBIFOMContainerAny ifpresent, // @sic R5s160711 Baseline Moving sic@
      headerCompressionConfig        := cr_HeaderCompressConfigAny ifpresent, // @sic R5s160711 Baseline Moving sic@
      extdProtocolConfigurationOptions  := cr_ExtdPCOAny ifpresent,  // @sic R5s160711 Baseline Moving sic@
      extd_EPS_QOS                      := cr_Extd_EPS_QOS_Any ifpresent // @sic R5s180271 Baseline Moving 2018 Phase 1 sic@
    }
  };

  template (present) NAS_UL_Message_Type cdr_BearerResourceModReq(template (present) ProcedureTransactionIdentifier p_ProcTi,
                                                                  EPS_BearerIdentity p_LinkedEpsBearerId,
                                                                  template ESM_Cause p_Cause,
                                                                  template TrafficFlowTemplate p_Tft,
                                                                  template EPS_QualityOfService p_QoS := cdr_EPS_QoS_Any_tlv ifpresent,
                                                                  template Extd_EPS_QOS p_ExdQoS := cr_Extd_EPS_QOS_Any ifpresent)
    modifies cr_508_BearerResourceModReq :=
  { /* @status    APPROVED (LTE, NBIOT) */
    bEARER_RESOURCE_MODIFICATION_REQUEST := {
      trafficFlowAgg                 := p_Tft,
      requiredTrafficFlowQoS         := p_QoS, // @sic R5s100692, R5s110682, R5-183265 sic@
      extd_EPS_QOS                   := p_ExdQoS // @sic R5s180271 Baseline Moving 2018 Phase 1 sic@
    }
  };

  template (value) NAS_DL_Message_Type cs_508_DeactivateEPSBearerCxtReq (EPS_BearerIdentity p_Id,
                                                                         ProcedureTransactionIdentifier p_ProcTi,
                                                                         template (value) ESM_Cause p_EsmCause,
                                                                         template (omit) GPRS_Timer3 p_T3396 := omit,
                                                                         template (omit) NBIFOMContainer p_NBIFOMContainer := omit,
                                                                         template (omit) ExtdProtocolConfigOptions p_ExtdProtocolConfigOptions := omit) :=
  { /* @status    APPROVED (ENDC, IMS, IMS_IRAT, LTE, LTE_A_IRAT, LTE_A_PRO, LTE_A_R10_R11, LTE_A_R12, LTE_IRAT, NBIOT, POS) */
    /* @sic R5-144797: new parameter p_T3396 sic@ */
    dEACTIVATE_EPS_BEARER_CONTEXT_REQUEST := {
      epsBearerId                    := p_Id,
      protocolDiscriminator          := tsc_PD_ESM,
      procedureTransactionIdentifier := p_ProcTi,
      messageType                    := tsc_MT_DeactivateEpsBearerContextRequest,
      esmCause                       := p_EsmCause,
      protocolConfigurationOptions   := omit,
      t3396Value                     := p_T3396,  // @sic R5s130195 Baseline Moving sic@
      wlanOffloadInd                 := omit, //  @sic R5s150329 Baseline Moving sic@
      nbifomContainer                := p_NBIFOMContainer, // @sic R5s160711 Baseline Moving sic@
      extdProtocolConfigurationOptions  := p_ExtdProtocolConfigOptions  // @sic R5s160711 Baseline Moving sic@
    }
  };

  template (present) NAS_UL_Message_Type cr_508_DeactivateEPSBearerCxtAccept(EPS_BearerIdentity p_Id,
                                                                             ProcedureTransactionIdentifier p_ProcTi := tsc_PTI_Unassigned) :=
  { /* @status    APPROVED (ENDC, IMS, IMS_IRAT, LTE, LTE_A_IRAT, LTE_A_PRO, LTE_A_R10_R11, LTE_A_R12, LTE_IRAT, NBIOT, POS) */
    dEACTIVATE_EPS_BEARER_CONTEXT_ACCEPT := {
      epsBearerId                    := p_Id,
      protocolDiscriminator          := tsc_PD_ESM,
      procedureTransactionIdentifier := p_ProcTi,
      messageType                    := tsc_MT_DeactivateEpsBearerContextAccept,
      protocolConfigurationOptions   := cr_ProtocolConfigOptionsAny ifpresent,      /* @sic R5-120572 sic@ */
      extdProtocolConfigurationOptions  := cr_ExtdPCOAny ifpresent  // @sic R5s160711 Baseline Moving sic@
    }
  };

  template (value) NAS_DL_Message_Type cs_508_Esm_Information_Request(template (value) ProcedureTransactionIdentifier p_ProcTi) :=
  { /* @status    APPROVED (ENDC, IMS, IMS_IRAT, LTE, LTE_A_IRAT, LTE_A_PRO, LTE_A_R10_R11, LTE_A_R12, LTE_IRAT, NBIOT, POS) */
    eSM_INFORMATION_REQUEST := {
      epsBearerId := bit2hex('0000'B),
      protocolDiscriminator := tsc_PD_ESM,
      procedureTransactionIdentifier := p_ProcTi,//The value indicated in PDN CONNECTIVITY REQUEST message.
      messageType := tsc_MT_EsmInformationRequest
    }
  };

  template (present) NAS_UL_Message_Type cr_508_Esm_Information_Response(template (present) ProcedureTransactionIdentifier p_ProcTi) :=
  { /* @status    APPROVED (ENDC, IMS, IMS_IRAT, LTE, LTE_A_IRAT, LTE_A_PRO, LTE_A_R10_R11, LTE_A_R12, LTE_IRAT, NBIOT, POS) */
    eSM_INFORMATION_RESPONSE := {
      epsBearerId := bit2hex('0000'B),
      protocolDiscriminator := tsc_PD_ESM,
      procedureTransactionIdentifier := p_ProcTi,
      messageType := tsc_MT_EsmInformationResponse,
      accessPointName := cr_AccessPointName_Any ifpresent,
      protocolConfigurationOptions := cr_ProtocolConfigOptionsAny ifpresent,
      extdProtocolConfigurationOptions  := cr_ExtdPCOAny ifpresent  // @sic R5s160711 Baseline Moving sic@
    }
  };

  template (present) NAS_UL_Message_Type cr_508_ModifyEPSBearerCxtAccept(EPS_BearerIdentity p_EpsBearerId,
                                                                         template (present) ProcedureTransactionIdentifier p_ProcTi := '00'O) :=
  { /* @status    APPROVED (ENDC, LTE, LTE_A_PRO, LTE_A_R10_R11, LTE_IRAT, NBIOT) */
    /* @sic R5s180388: p_ProcTi sic@ */
    mODIFY_EPS_BEARER_CONTEXT_ACCEPT := {
      epsBearerId   := p_EpsBearerId,
      protocolDiscriminator := tsc_PD_ESM,
      procedureTransactionIdentifier := p_ProcTi,
      messageType := tsc_MT_ModifyEpsBearerContextAccept,
      protocolConfigurationOptions  := cr_ProtocolConfigOptionsAny ifpresent,      /* @sic R5-120572 sic@ */
      nbifomContainer                := cr_NBIFOMContainerAny ifpresent, // @sic R5s160711 Baseline Moving sic@
      extdProtocolConfigurationOptions  := cr_ExtdPCOAny ifpresent  // @sic R5s160711 Baseline Moving sic@
    }
  };

  template (present) NAS_UL_Message_Type cr_508_ModifyEPSBearerCxtReject(EPS_BearerIdentity p_Id,
                                                                         template ESM_Cause p_EsmCause,
                                                                         template (present) ProcedureTransactionIdentifier p_ProcTi := '00'O) :=
  { /* @status    APPROVED (LTE, NBIOT) */
    // @sic R5s100464 sic@
    mODIFY_EPS_BEARER_CONTEXT_REJECT := {
      epsBearerId                    := p_Id,
      protocolDiscriminator          := tsc_PD_ESM,
      procedureTransactionIdentifier := p_ProcTi,
      messageType                    := tsc_MT_ModifyEpsBearerContextReject,
      esmCause                       := p_EsmCause,
      protocolConfigurationOptions   := cr_ProtocolConfigOptionsAny ifpresent,
      nbifomContainer                := cr_NBIFOMContainerAny ifpresent, // @sic R5s160711 Baseline Moving sic@
      extdProtocolConfigurationOptions  := cr_ExtdPCOAny ifpresent  // @sic R5s160711 Baseline Moving sic@
    }
  };

  template (value) NAS_DL_Message_Type cs_508_ModifyEPSBearerCxtReq(EPS_BearerIdentity p_EpsBearerId,
                                                                    ProcedureTransactionIdentifier p_ProcTi,
                                                                    template (omit) EPS_QualityOfService p_NewEPSQos,
                                                                    template (omit) TrafficFlowTemplate p_Tft) :=
  /* @sic R5-103625 sic@ */
  /* @status    APPROVED (LTE, LTE_A_R10_R11, LTE_IRAT, NBIOT) */
    cs_ModifyEPSBearerCxtReq_AllParams (p_EpsBearerId,
                                        p_ProcTi,
                                        p_NewEPSQos,
                                        p_Tft,
                                        omit,
                                        omit,
                                        omit,
                                        f_Get_508_PFI(), //  // @sic R5-115618 sic@
                                        omit,
                                        omit,
                                        omit,  // @sic R5s160711 Baseline Moving sic@
                                        omit,  // @sic R5s160711 Baseline Moving sic@
                                        omit,  // @sic R5s160711 Baseline Moving sic@
                                        omit,  // @sic R5s180271 Baseline Moving 2018 Phase 1 sic@
                                        omit); // @sic R5s180271 Baseline Moving 2018 Phase 1 sic@

  template (value) NAS_DL_Message_Type cs_ModifyEPSBearerCxtReq_AllParams(EPS_BearerIdentity p_EpsBearerId,
                                                                          ProcedureTransactionIdentifier p_ProcTi,
                                                                          template (omit) EPS_QualityOfService p_NewEPSQos,
                                                                          template (omit) TrafficFlowTemplate p_Tft,
                                                                          template (omit) QualityOfService p_NewQos,
                                                                          template (omit) LLC_SAPI p_LlcSapi,
                                                                          template (omit) RadioPriority p_RadioPriority,
                                                                          template (omit) PacketFlowIdentifier p_PFI,
                                                                          template (omit) APN_AMBR p_APN_AMBR,
                                                                          template (omit) ProtocolConfigOptions p_PCO,
                                                                          template (omit) NBIFOMContainer p_NBIFOMContainer,
                                                                          template (omit) HeaderCompressConfig p_HeaderCompressConfig,
                                                                          template (omit) ExtdProtocolConfigOptions p_ExtdProtocolConfigOptions,
                                                                          template (omit) Extd_APN_AMBR p_Extd_APN_AMBR,
                                                                          template (omit) Extd_EPS_QOS p_Extd_EPS_QOS) :=
  { /* @sic R5-103625 sic@ */
    /* @status    APPROVED (ENDC, LTE, LTE_A_PRO, LTE_A_R10_R11, LTE_IRAT, NBIOT) */
    mODIFY_EPS_BEARER_CONTEXT_REQUEST := {
      epsBearerId   := p_EpsBearerId,
      protocolDiscriminator := tsc_PD_ESM,
      procedureTransactionIdentifier := p_ProcTi,
      messageType := tsc_MT_ModifyEpsBearerContextRequest,
      newEPSQos :=  p_NewEPSQos,
      tft := p_Tft,
      newQos :=  p_NewQos,
      negotiatedLlcSapi:= p_LlcSapi,
      radioPriority  := p_RadioPriority,
      packetFlowIdentifier := p_PFI,
      apnAggMaxBitRate := p_APN_AMBR,
      protocolConfigurationOptions := p_PCO,
      wlanOffloadInd                    := omit, //  @sic R5s150329 Baseline Moving sic@
      nbifomContainer                   := p_NBIFOMContainer, // @sic R5s160711 Baseline Moving sic@
      headerCompressionConfig           := p_HeaderCompressConfig,  // @sic R5s160711 Baseline Moving sic@
      extdProtocolConfigurationOptions  := p_ExtdProtocolConfigOptions,  // @sic R5s160711 Baseline Moving sic@
      extd_APN_AMBR                     := p_Extd_APN_AMBR, // @sic R5s180271 Baseline Moving 2018 Phase 1 sic@
      extd_EPS_QOS                      := p_Extd_EPS_QOS // @sic R5s180271 Baseline Moving 2018 Phase 1 sic@
    }
  };

  template (present) NAS_UL_Message_Type cdr_TAU_Request_OldGuti(template (present) EPS_UpdateTypeValue p_EpsUpdate_TypeValue,
                                                                 NAS_KsiValue p_KsiValue,
                                                                 template DRXparameter p_DRXparameter, // @sic R5s120210 sic@
                                                                 template AdditionalUpdateType p_AdditionalUpdateType,
                                                                 template (present) MobileIdentity p_OldGuti,
                                                                 template (present) TrackingAreaId p_LastTai)
    modifies cr_508_TAU_Request :=
  { /* @status    APPROVED (LTE, LTE_IRAT, NBIOT) */
    tRACKING_AREA_UPDATE_REQUEST := {
      oldGuti := p_OldGuti,
      lastVisitedRegisteredTai := p_LastTai
    }
  };

  template (present) NAS_UL_Message_Type cdr_TAU_Request_OldAddGutiTAI(template (present) EPS_UpdateTypeValue p_EpsUpdate_TypeValue,
                                                                       NAS_KsiValue p_KsiValue,
                                                                       template DRXparameter p_DRXparameter, // @sic R5s120210 sic@
                                                                       template AdditionalUpdateType p_AdditionalUpdateType,
                                                                       template (present) MobileIdentity p_OldGuti,
                                                                       template TrackingAreaId p_TAI,
                                                                       template MobileIdentity p_AdditionalGuti,  // @sic R5s120482 sic@
                                                                       template PTMSI_Signature p_Sig := omit,
                                                                       template Nonce p_Nonce := omit,
                                                                       template CiphKeySeqNum p_CiphKeySeqNum := cr_CiphKeySeqNum('8'H, ?) ifpresent ) // @sic R5-135024 sic@
    modifies cr_508_TAU_Request :=
  { /* @status    APPROVED (LTE_IRAT, NBIOT) */
    tRACKING_AREA_UPDATE_REQUEST := {
      asmeNasKeySetId            := cr_NAS_KeySetIdentifier (p_KsiValue, tsc_NasKsi_NativeSecurityContext),
      oldGuti                    := p_OldGuti,
      gprsCipheringKeySeqNum     := p_CiphKeySeqNum, // @sic R5-135024 sic@
      oldPtmsiSignature          := p_Sig,
      additionalGuti             := p_AdditionalGuti,
      nonce                      := p_Nonce,
      lastVisitedRegisteredTai   := p_TAI
    }
  };

  template (value) NAS_DL_Message_Type cs_508_EMMInformation(template (omit) NetworkName p_FullName,
                                                             template (omit) NetworkName p_ShortName,
                                                             template (omit) TimeZone p_LocalTimeZone,
                                                             template (omit) TimeZoneAndTime p_LocalTimeZongAndTime,
                                                             template (omit) DaylightSavingTime p_DaylightSavingTime):=
  { /* 24.301 cl. 8.2.13    */
    /* @status    APPROVED (LTE) */
    eMM_INFORMATION := {
      securityHeaderType := tsc_SHT_NoSecurityProtection,
      protocolDiscriminator  :=  tsc_PD_EMM,
      messageType            :=  tsc_MT_EmmInfo,
      fullNetworkName        :=  p_FullName,
      shortNetworkName       :=  p_ShortName,
      localTimeZone          :=  p_LocalTimeZone,
      localTimeZoneAndTime   :=  p_LocalTimeZongAndTime,
      daylightSavingTime     :=  p_DaylightSavingTime
    }
  };  
    
  template (present) NAS_UL_Message_Type cr_508_EMMStatus (template EMM_Cause p_EmmCause):=
  { /* 24.301 cl. 8.2.14    */
    /* @status    APPROVED (LTE, NBIOT) */
    eMM_STATUS := {
      securityHeaderType     :=  tsc_SHT_NoSecurityProtection,
      protocolDiscriminator  :=  tsc_PD_EMM,
      messageType            :=  tsc_MT_EmmStatus,
      emmCause               :=  p_EmmCause
    }
  };

  template (value) NAS_DL_Message_Type cs_508_GUTI_REALLOCATION_COMMAND( template (value) MobileIdentity p_Guti,
                                                                         template (omit) TrackingAreaIdList p_TaiList,
                                                                         template (omit) DCN_ID p_DCN_Id := omit) :=
  { /* 24.301 cl. 8.2.16 */
    /* @status    APPROVED (LTE, NBIOT) */
    gUTI_REALLOCATION_COMMAND := {
      securityHeaderType    := tsc_SHT_NoSecurityProtection,
      protocolDiscriminator := tsc_PD_EMM,
      messageType           := tsc_MT_GutiReallocCommand,
      guti                  := p_Guti,
      taiList               := p_TaiList,
      dcnId                 := p_DCN_Id  // @sic R5-177082 sic@
    }
  };

  template (present) NAS_UL_Message_Type cr_508_GUTI_REALLOCATION_COMPLETE :=
  { /* 24.301 cl. 8.2.17 */
    /* @status    APPROVED (LTE, NBIOT) */
    gUTI_REALLOCATION_COMPLETE := {
      securityHeaderType    := tsc_SHT_NoSecurityProtection,
      protocolDiscriminator := tsc_PD_EMM,
      messageType           := tsc_MT_GutiReallocComplete
    }
  };

  template (present) NAS_UL_Message_Type cr_PDNConnReqAdditionalPDN(template (present) PdnTypeValue p_PDNTypeValue,
                                                                    template AccessPointName p_AccessPointName := cr_AccessPointName_Any,
                                                                    B3_Type p_RequestType := '001'B,
                                                                    template ESM_InfoTransferFlag p_EsmInfoTransferFlag := omit) := // 'initial request' 24.008 clause 10.5.6.17
  /* @desc PDN CONNECTIVITY REQUEST requesting an additional PDN   (used e.g. in test 10.5.1)
     @sic R5-115673 sic@
     @status    APPROVED (ENDC, IMS, IMS_IRAT, LTE, LTE_A_IRAT, LTE_A_PRO, LTE_A_R10_R11, LTE_A_R12, LTE_IRAT, NBIOT, POS) */
    cr_PDN_CONNECTIVITY_REQUEST_Common(-, -, p_PDNTypeValue, p_RequestType, p_EsmInfoTransferFlag, p_AccessPointName);

  template (value) NAS_DL_Message_Type cs_PdnConnectReject(EPS_BearerIdentity p_Id,
                                                           ProcedureTransactionIdentifier p_ProcTi,
                                                           template (value) ESM_Cause p_EsmCause,
                                                           template (omit) GPRS_Timer3 p_T3396 := omit,
                                                           template (omit) NBIFOMContainer p_NBIFOMContainer := omit,
                                                           template (omit) ExtdProtocolConfigOptions p_ExtdProtocolConfigOptions := omit) :=
  { /* @status    APPROVED (LTE, LTE_A_R10_R11, NBIOT) */
    pDN_CONNECTIVITY_REJECT:= {
      epsBearerId                    := p_Id,
      protocolDiscriminator          := tsc_PD_ESM,
      procedureTransactionIdentifier := p_ProcTi,
      messageType                    := tsc_MT_PdnConnectivityReject,
      esmCause                       := p_EsmCause,
      protocolConfigurationOptions   := omit,
      backOffTimerValue              := p_T3396,   /* cl. 9.9.3.16B O TLV 3   IEI=0x37 @sic R5s120178 Baseline Moving, R5s150329 Baseline Moving sic@ Was T3396 Value*/
      reattemptInd                   := omit,   // @sic R5s150329 Baseline Moving sic@
      nbifomContainer                := p_NBIFOMContainer, // @sic R5s160711 Baseline Moving sic@
      extdProtocolConfigurationOptions  := p_ExtdProtocolConfigOptions  // @sic R5s160711 Baseline Moving sic@
    }
  };

  template (value) NAS_DL_Message_Type cs_508_BearerResourceModificationReject(ProcedureTransactionIdentifier p_ProcTi,
                                                                               template (value) ESM_Cause p_EsmCause) :=
  { /* @status    APPROVED (LTE, NBIOT) */
    bEARER_RESOURCE_MODIFICATION_REJECT := {
      epsBearerId                    := '0'H,
      protocolDiscriminator          := tsc_PD_ESM,
      procedureTransactionIdentifier := p_ProcTi,
      messageType                    := tsc_MT_BearerResourceModificationReject,
      esmCause                       := p_EsmCause,
      protocolConfigurationOptions   := omit,
      backOffTimerValue              := omit,   /* cl. 9.9.3.16B O TLV 3   IEI=0x37 @sic R5s120178 Baseline Moving, R5s150329 Baseline Moving sic@ Was T3396 Value*/
      reattemptInd                   := omit,   // @sic R5s150329 Baseline Moving sic@
      nbifomContainer                := omit, // @sic R5s160711 Baseline Moving sic@
      extdProtocolConfigurationOptions  := omit  // @sic R5s160711 Baseline Moving sic@
    }
  };

  template (value) EPS_QualityOfService cs_508_EPS_QoS_Default_1 :=
  { /* @status    APPROVED (ENDC, IMS, IMS_IRAT, LTE, LTE_A_IRAT, LTE_A_PRO, LTE_A_R10_R11, LTE_A_R12, LTE_IRAT, NBIOT, POS) */
    iei := omit,
    iel := '09'O,
    qci                    := '00001001'B,
    maxBitRateUl           := '01000000'B,                //64 kbps
    maxBitRateDl           := '01000000'B,                //64 kbps
    guaranteedBitRateUl    := '01000000'B,                //64 kbps
    guaranteedBitRateDl    := '01000000'B,                //64 kbps
    maxBitRateUlExt        := '00000000'B,
    maxBitRateDlExt        := '00000000'B,
    guaranteedBitRateUlExt := '00000000'B,
    guaranteedBitRateDlExt := '00000000'B,
    maxBitRateUL_Ext2         := omit,  // @sic R5s1300195 Baseline Moving, R5s180640 sic@
    maxBitRateDL_Ext2       := omit,  // @sic R5s1300195 Baseline Moving sic@
    guaranteedBitRateUL_Ext2  := omit,  // @sic R5s1300195 Baseline Moving sic@
    guaranteedBitRateDL_Ext2  := omit   // @sic R5s1300195 Baseline Moving sic@
  };

  template (value) EPS_QualityOfService cds_508_EPS_QoS_Default_1_tlv
  modifies cs_508_EPS_QoS_Default_1 :=
  { /* according to table 6.6.2.1 in 36.508, including iei */
    /* @status    APPROVED (LTE, NBIOT) */
    iei     := '5B'O
  };

  template (value) EPS_QualityOfService cds_EPS_QoS_Def_nonGBR_QCI
  modifies cs_508_EPS_QoS_Default_1 :=
  { /* @status    APPROVED (ENDC, IMS, IMS_IRAT, LTE, LTE_A_IRAT, LTE_A_PRO, LTE_A_R10_R11, LTE_A_R12, LTE_IRAT, NBIOT, POS) */
    qci     := '00000101'B // non-GBR QCI
  };

   template (present) UE_AdditionalSecurityCapability cr_UE_AddSecurityCap_Any :=
   { /* @status    APPROVED (ENDC, IMS, IMS_IRAT, LTE, LTE_A_IRAT, LTE_A_PRO, LTE_A_R10_R11, LTE_A_R12, LTE_IRAT, NBIOT, POS) */
      iei    := '6F'O, /* present in case of TV; omit in case of V */
      iel    := ?,
      eaCap5G_0_7    := ?,
      eaCap5G_8_15   := ?,
      iaCap5G_0_7    := ?,
      iaCap5G_8_15   := ?
   };

  template (value) ExtdDRXParams cs_EDRXParams (B4_Type p_PTW, B4_Type p_EDRXVal) :=
  { /* @status    APPROVED (LTE_A_PRO, NBIOT) */
    iei          := '6E'O,
    iel          := '01'O, // length
    pagingTimeWindow := p_PTW,
    eDRXValue    := p_EDRXVal
  };

  /*
   * @desc      Get the Default or Dedicated NegotiatedQoS according to 36.508 clause 6.6.1 and 2
   * @param     p_BearerContextNumber
   * @return    template (omit) QualityOfService
   * @status    APPROVED (ENDC, IMS, IMS_IRAT, LTE, LTE_A_IRAT, LTE_A_PRO, LTE_A_R10_R11, LTE_A_R12, LTE_IRAT, NBIOT, POS)
   */
  function f_Get_508_NegotiatedQualityOfService(template (omit) BEARER_CONTEXT_TYPE p_BearerContextNumber) return template (omit) QualityOfService
  { /* @sic R5s141367: p_BearerContextNumber changed to template (omit) sic@ */
    var template (omit) QualityOfService v_QoS := omit;
    var BEARER_CONTEXT_TYPE v_BearerContextNumber := fl_BearerContextNumber_Def(p_BearerContextNumber);  /* @sic R5s141367 sic@ */
    
    if (pc_UTRA){ // @sic R5-115585 sic@
      
      select (v_BearerContextNumber){
        case (DEF_1, DEF_2) {
          v_QoS := cs_QoS_Negotiated_1;
        }
        case (DED_1) {
          v_QoS := cds_QoS_Converstational; // @sic R5-112698 sic@
        }
        case (DED_2) {
          v_QoS := cds_QoS_Negotiated_2;
        }
        case (DED_3) {
          v_QoS := cds_QoS_Negotiated_3;
        }
        case (DED_4) {
          v_QoS := cds_QoS_Negotiated_4;
        }
        // All other values are currently set to omit
      }
    }
    if (pc_GERAN) {
      select (v_BearerContextNumber){
        case (DEF_1, DEF_2) { // @sic R5-120619 sic@
          v_QoS := cs_QoS_PDP2;
        }
        case (DED_1) {
          v_QoS := cds_QoS_Converstational; // @sic R5-112698 sic@
        }
        case (DED_2) {
          v_QoS := cds_QoS_PDP3;
        }
        case (DED_3) { // @sic R5-120632 sic@
          v_QoS := cds_QoS_Negotiated_3;
        }
        case (DED_4) {
          v_QoS := cds_QoS_Negotiated_4;
        }
        // All other values are currently set to omit
      }
    }
    
    return v_QoS;
  }

  /*
   * @desc      Get the Default or Dedicated NegotiatedQoS according to 36.508 clause 6.6.1 and 2
   * @param     p_BearerContextNumber
   * @return    template (omit) LLC_SAPI
   * @status    APPROVED (ENDC, IMS, IMS_IRAT, LTE, LTE_A_IRAT, LTE_A_PRO, LTE_A_R10_R11, LTE_A_R12, LTE_IRAT, NBIOT, POS)
   */
  function f_Get_508_NegotiatedLLCSAPI(template (omit) BEARER_CONTEXT_TYPE p_BearerContextNumber) return template (omit) LLC_SAPI
  { /* @sic R5s141367: p_BearerContextNumber changed to template (omit) sic@ */
    var template (omit) LLC_SAPI v_LLC_SAPI := omit;
    var BEARER_CONTEXT_TYPE v_BearerContextNumber := fl_BearerContextNumber_Def(p_BearerContextNumber);  /* @sic R5s141367 sic@ */
    
    if (pc_GERAN){   // @sic R5s110168 change 2.16 sic@
      
      select (v_BearerContextNumber){
        case (DEF_1, DEF_2, DED_2) { // @sic R5-120619 sic@
          v_LLC_SAPI         := cs_LLC_SAPI_11 ('32'O);
        }
        case (DED_1, DED_4) { // @sic R5-120632 sic@
          v_LLC_SAPI         := cs_LLC_SAPI_03 ('32'O);
        }
        // All other values are currently set to omit
      }
    }
    
    return v_LLC_SAPI;
  }

  /* @desc      Get the Default QoS according to 36.508 clause 4.7.3-6
   * @param     p_BearerContextNumber
   * @return    template (value) EPS_QualityOfService
   * @status    APPROVED (ENDC, IMS, IMS_IRAT, LTE, LTE_A_IRAT, LTE_A_PRO, LTE_A_R10_R11, LTE_A_R12, LTE_IRAT, NBIOT, POS)
   */
  function f_Get_508_EPSQoS(template (omit) BEARER_CONTEXT_TYPE p_BearerContextNumber) return template (value) EPS_QualityOfService
  { /* @sic R5-144797: parameter p_BearerContextNumber sic@ */
    var template (value) EPS_QualityOfService v_QoS;
    var BEARER_CONTEXT_TYPE v_BearerContextNumber := fl_BearerContextNumber_Def(p_BearerContextNumber);
    
    select (v_BearerContextNumber){   /* @sic R5-144797: v_QoS does not depend on pc_IMS anymore sic@ */
      case (DEF_1) {
        v_QoS := cs_508_EPS_QoS_Default_1;        /* Qos acc. to 36.508 Table 6.6.1-1 column 1 */
      }
      case else {
        v_QoS := cds_EPS_QoS_Def_nonGBR_QCI;      /* Qos acc. to 36.508 Table 6.6.1-1 column 2 */
      }
    }
    return v_QoS;
  }

  /*
   * @desc      Get the Default or Dedicated RadioPriority according to 36.508 clause 6.6.1 and 2
   * @param     p_BearerContextNumber
   * @return    template (omit) RadioPriority
   * @status    APPROVED (ENDC, IMS, IMS_IRAT, LTE, LTE_A_IRAT, LTE_A_PRO, LTE_A_R10_R11, LTE_A_R12, LTE_IRAT, NBIOT, POS)
   */
  function f_Get_508_RadioPriority(template (omit) BEARER_CONTEXT_TYPE p_BearerContextNumber) return template (omit) RadioPriority
  {
    var template (omit) RadioPriority v_RadioPriority := omit;
    var BEARER_CONTEXT_TYPE v_BearerContextNumber := fl_BearerContextNumber_Def(p_BearerContextNumber);  /* @sic R5s141367 sic@ */
    
    if (pc_GERAN) { // @sic R5s110168 change 2.16 sic@
      
      select (v_BearerContextNumber){
        case (DEF_1, DEF_2, DED_2) { // @sic R5-120619 sic@
          v_RadioPriority := cs_RadioPriority ('8'H, '100'B);
        }
        // All other values are currently set to omit
      }
    }
    
    return v_RadioPriority;
  }

  /*
   * @desc      Get the PFI according to 36.508 clause 4.7.3
   * @return    template (omit) PacketFlowIdentifier
   * @status    APPROVED (ENDC, IMS, IMS_IRAT, LTE, LTE_A_IRAT, LTE_A_PRO, LTE_A_R10_R11, LTE_A_R12, LTE_IRAT, NBIOT, POS)
   */
  function f_Get_508_PFI() return template (omit) PacketFlowIdentifier
  {
    var template (omit) PacketFlowIdentifier v_PFI := omit;
    
    if (pc_GERAN){
      v_PFI := cs_PktFlowId;
    }
    return v_PFI;
  }

  /*
   * @desc      Get UE_AdditionalSecurityCapability
   * @return    template UE_AdditionalSecurityCapability
   * @status    APPROVED (ENDC, IMS, IMS_IRAT, LTE, LTE_A_IRAT, LTE_A_PRO, LTE_A_R10_R11, LTE_A_R12, LTE_IRAT, NBIOT, POS)
   */
  function f_GetUE_AdditionalSecurityCapability () return template UE_AdditionalSecurityCapability
  {
    var template UE_AdditionalSecurityCapability v_UE_AdditionalSecurityCapability := cr_UE_AddSecurityCap_Any ifpresent;
    if (pc_EN_DC) {
      v_UE_AdditionalSecurityCapability := cr_UE_AddSecurityCap_Any;
    }
    return v_UE_AdditionalSecurityCapability;
  }

  /*
   * @desc      To convert the EPS_QualityOfService into a charstring for the UT command
   * @param     p_QoS
   * @return    charstring
   * @status    APPROVED (LTE, LTE_A_R10_R11, NBIOT)
   */
  function f_ConvertEPSQoS_ForATCommand(template (value) EPS_QualityOfService p_QoS) return charstring
  {
    var EPS_QualityOfService v_QoS := valueof(p_QoS);
    var charstring v_QoSstring := "";
    v_QoSstring := v_QoSstring &","& int2str(bit2int(v_QoS.qci));
    v_QoSstring := v_QoSstring &","& int2str(f_QoS_MaximumBitRate(v_QoS.guaranteedBitRateDl)); // @sic R5s100557 sic@
    v_QoSstring := v_QoSstring &","& int2str(f_QoS_MaximumBitRate(v_QoS.guaranteedBitRateUl)); // @sic R5s100557 sic@
    v_QoSstring := v_QoSstring &","& int2str(f_QoS_MaximumBitRate(v_QoS.maxBitRateDl)); // @sic R5s100557 sic@
    v_QoSstring := v_QoSstring &","& int2str(f_QoS_MaximumBitRate(v_QoS.maxBitRateUl)); // @sic R5s100557 sic@
    // Extended rates are not dealt with at the moment (all set to 0 in current 36.508)
    return v_QoSstring;
  }

}
