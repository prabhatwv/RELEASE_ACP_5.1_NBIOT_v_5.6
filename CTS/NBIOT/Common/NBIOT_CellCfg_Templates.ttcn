/******************************************************************************/
// @copyright   Copyright Notification
//   No part may be reproduced except as authorized by written permission.
//   The copyright and the foregoing restriction extend to reproduction in all media.
//   Trademark 2019, 3GPP Organizational Partners (ARIB, ATIS, CCSA, ETSI, TSDSI, TTA, TTC).
//   All rights reserved.
// @version: 36.523-3 v15.3.0
// $Date: 2018-11-21 20:05:51 +0100 (Wed, 21 Nov 2018) $
// $Rev: 22971 $
/******************************************************************************/

module NBIOT_CellCfg_Templates {

  import from NBIOT_RRC_ASN1_Definitions language "ASN.1:2002" all with {encode "UNALIGNED_PER_OctetAligned"};
  import from Common4G5G_Templates all;
  import from CommonAspDefs all;
  import from EUTRA_NB_ASP_Templates all;
  import from EUTRA_NB_ASP_TypeDefs all;
  import from NBIOT_ASP_TypeDefs all;
  import from NBIOT_AspCommon_Templates all;
  import from NBIOT_CommonDefs all;
  import from NBIOT_Component all;
  import from NBIOT_Imported_EUTRA_ASN1_Types all;
  import from NBIOT_RRC_Templates all;

  const integer tsc_NB_DciSubcarrierIndicationField_Def := 0;     /* subcarrier indication field according to 36.213 clause 16.5.1.1 and 36.508 Table 8.1.3.6.1.1-1
                                                                     @status    APPROVED (NBIOT) */

  const integer tsc_NB_DciFormatN1_HARQ_ACKResource_Def := 0;     /* 36.213 clause 16.4.2:
                                                                     Table 16.4.2-1: NPUSCH with subcarrier spacing 3.75kHz: k0 = 13, ACK/NACK subcarrier = 38
                                                                     Table 16.4.2-2: NPUSCH with subcarrier spacing 15kHz:   k0 = 13, ACK/NACK subcarrier = 0
                                                                     Value according to 36.508 Table 8.1.3.6.1.2-2
                                                                     @status    APPROVED (NBIOT) */

  template (value) NB_DrxCtrl_Type cs_NB_DrxCtrl_None := { None := true };          /* @status    APPROVED (NBIOT) */

  template (value) NB_ToRS_EPRE_Ratio_Type cs_NB_PowerRatio_Def := 0;               /* @status    APPROVED (NBIOT)
                                                                                       !!!! NBIOT: for LTE signalling tests the Power allocation of downlink physical channels is specified in Table 6.2.1.2-1 of 36.508
                                                                                       !!!!        but there is no table like this for NBIOT yet !!!! */

  template (value) NB_ToRS_EPRE_Ratio_Type cs_NB_PowerRatioNPDSCH_Def :=  -3;       /* @status    APPROVED (NBIOT)
                                                                                       !!!! NBIOT: for LTE signalling tests the Power allocation of downlink physical channels is specified in Table 6.2.1.2-1 of 36.508
                                                                                       !!!!        but there is no table like this for NBIOT yet !!!! */

  template (value) NB_DciDlInfoCommon_Type cs_NB_DciDlInfoCommon_Init(NB_PdcchDciFormat_Type p_PdcchDciFormat,
                                                                      integer p_RepetitionNumber := 0,
                                                                      integer p_DCISubFrameRepetitionNumber := 0,
                                                                      template (omit) integer p_SchedulingDelay := 0,
                                                                      template (omit) integer p_HARQ_ACKResource := tsc_NB_DciFormatN1_HARQ_ACKResource_Def,
                                                                      NB_CRC_ErrorMode_Type p_CrcErrorMode := noError,
                                                                      template (omit) integer p_HARQ_ProcessNumber := omit) :=
  { /* @status    APPROVED (NBIOT) */
    Format := p_PdcchDciFormat,
    RepetitionNumber := p_RepetitionNumber,
    DCISubFrameRepetitionNumber := p_DCISubFrameRepetitionNumber,
    SchedulingDelay := p_SchedulingDelay,
    HARQ_ACKResource := p_HARQ_ACKResource,
    CRC_ErrorMode := p_CrcErrorMode,
    HARQ_ProcessNumber := p_HARQ_ProcessNumber    /* @sic R5w180305: support of more than one HARQ process sic@ */
  };

  template (value) NB_DciDlInfoCommon_Type cs_NB_DciInfo_CcchDcchDtchDL(template (omit) integer p_ProcessNumber := omit) := cs_NB_DciDlInfoCommon_Init(dci_N1, -, -, -, -, -, p_ProcessNumber);  /* acc. to 36.508 Table 8.1.3.6.1.2-2
                                                                                                                                                                                                    @sic R5w180305: p_ProcessNumber sic@
                                                                                                                                                                                                    @status    APPROVED (NBIOT) */

  template (value) NB_DciDlInfoCommon_Type cs_NB_DciInfo_Pcch := cs_NB_DciDlInfoCommon_Init(dci_N2, -, 0, omit, omit);  /* acc. to 36.508 Table 8.1.3.6.1.5-1; @sic R5s170030 change 4 sic@
                                                                                                                           @status    APPROVED (NBIOT) */

  template (value) NB_DciDlInfoCommon_Type cs_NB_DciInfo_RandomAccessResponse := cs_NB_DciInfo_CcchDcchDtchDL;          /* @status    APPROVED (NBIOT) */

  template (value) NB_DciDlInfo_Type cs_NB_DciDlInfo_Auto(template (omit) integer p_ProcessNumber := omit) :=
  { /* @status    APPROVED (NBIOT) */
    /* @sic R5w180305: p_ProcessNumber sic@ */
    Auto := cs_NB_DciInfo_CcchDcchDtchDL(p_ProcessNumber)
  };

  template (value) NB_DciDlInfo_Type cs_NB_DciDlInfo_Explicit(integer p_Imcs,
                                                              integer p_Isf,
                                                              integer p_RepetitionNumber := 0,
                                                              NB_CRC_ErrorMode_Type p_CrcErrorMode := noError) :=
  { /* @status    APPROVED (NBIOT) */
    Explicit := {
      Format := dci_N1,
      Imcs := p_Imcs,
      ResourceAssignment := p_Isf,
      RepetitionNumber := p_RepetitionNumber,
      DCISubFrameRepetitionNumber := 0,
      SchedulingDelay := 0,
      HARQ_ACKResource := tsc_NB_DciFormatN1_HARQ_ACKResource_Def,
      CRC_ErrorMode := p_CrcErrorMode,
      HARQ_ProcessNumber := omit    /* @sic R5w180305: support of more than one HARQ process sic@ */
    }
  };

  template (value) NB_SiSchedul_Type cs_NB_SiSchedul(NB_SiPeriodicity_Type p_SiPeriodicity,
                                                     NB_SiRepetitionPattern_Type p_SiRepetitionPattern,
                                                     NB_SiTransportBlockSize_Type p_TransportBlockSize) :=
  { /* @status    APPROVED (NBIOT) */
    Periodicity       := p_SiPeriodicity,
    RepetitionPattern := p_SiRepetitionPattern,
    SchedulingInfoSI  := p_TransportBlockSize
  };

  template (value) NB_AllSiSchedul_Type cs_NB_SiSchedulList(NB_SiWindowLength_Type p_SiWindowLength,           /* as in Sib1 */
                                                           // NB_SiRadioFrameOffset_Type p_SiRadioFrameOffset,   /* as in Sib1 */
                                                            template (value) NB_SiSchedulList_Type p_SiSchedulList) :=
  { /* @status    APPROVED (NBIOT) */
    /* common template for one or more SIs */
    WindowLength := p_SiWindowLength,
    //RadioFrameOffset := p_SiRadioFrameOffset,//vzw nbiot: commenting for omiting this
    SiList := p_SiSchedulList
  };

  /*
   * @desc      get SIB schedulingfrom SIB1 to be used for SS configuration
   * @param     p_SIB1
   * @return    template (value) NB_AllSiSchedul_Type
   * @status    APPROVED (NBIOT)
   */
  function fl_NBIOT_SS_SetupSchedulingInfo(SystemInformationBlockType1_NB p_SIB1) return template (value) NB_AllSiSchedul_Type
  {
    var template (value) NB_SiSchedulList_Type v_SiSchedulList;
    var integer v_NoOfSIs := lengthof(p_SIB1.schedulingInfoList_r13);
    var integer I;
    
    for (I := 0; I < v_NoOfSIs; I:= I + 1) {
      v_SiSchedulList[I] := cs_NB_SiSchedul(p_SIB1.schedulingInfoList_r13[I].si_Periodicity_r13,
                                            p_SIB1.schedulingInfoList_r13[I].si_RepetitionPattern_r13,
                                            p_SIB1.schedulingInfoList_r13[I].si_TB_r13);
    }
    return cs_NB_SiSchedulList(p_SIB1.si_WindowLength_r13,/* p_SIB1.si_RadioFrameOffset_r13,*/ v_SiSchedulList);
  }

  template (value) NPdcchUESpecificSearchSpace_Type cs_NPdcchUESpecificSearchSpace(template (value) NPDCCH_NumRepetitions_UESS_Type p_NumRepetitions,
                                                                                   template (value) NPDCCH_StartSF_UESS_Type p_StartSF,
                                                                                   template (value) NPDCCH_Offset_UESS_Type p_Offset,
                                                                                   template (value) NPdcchSearchSpaceCandidateIndex_Type p_SearchSpaceCandidateForDL := tsc_NPdcchSearchSpaceCandidateIndex_0,
                                                                                   template (value) NPdcchSearchSpaceCandidateIndex_Type p_SearchSpaceCandidateForUL := tsc_NPdcchSearchSpaceCandidateIndex_0) :=
  { /* @status    APPROVED (NBIOT) */
    NPdcchFormat := npdcchFormat1,
    NumRepetitions := p_NumRepetitions,
    StartSF := p_StartSF,
    Offset := p_Offset,
    SearchSpaceCandidateForDL := p_SearchSpaceCandidateForDL,
    SearchSpaceCandidateForUL := p_SearchSpaceCandidateForUL
  };

  template (value) NPdcchType1CommonSearchSpace_Type cs_NPdcchType1CommonSearchSpace(template (value) NPDCCH_NumRepetitions_Type1CSS_Type p_NumRepetitions) :=
  { /* @status    APPROVED (NBIOT) */
    NumRepetitions := p_NumRepetitions
  };

  /*
   * @desc      gets search space parameters out of SIB2 to be used for SS configuration
   * @param     p_Sib2
   * @return    template (value) NPdcchType1CommonSearchSpace_Type
   * @status    APPROVED (NBIOT)
   */
  function fl_NBIOT_SIB2_GetType1CSS(SystemInformationBlockType2_NB_r13 p_Sib2) return template (value) NPdcchType1CommonSearchSpace_Type
  {
    var PCCH_Config_NB_r13 v_PCCH_Config := p_Sib2.radioResourceConfigCommon_r13.pcch_Config_r13;
    var NPDCCH_NumRepetitions_Type1CSS_Type v_NumRepetitions := v_PCCH_Config.npdcch_NumRepetitionPaging_r13;

    return cs_NPdcchType1CommonSearchSpace(v_NumRepetitions);
  }

  template (value) NPdcchType2CommonSearchSpace_Type cs_NPdcchType2CommonSearchSpace(template (value) NPDCCH_NumRepetitions_Type2CSS_Type p_NumRepetitions,
                                                                                     template (value) NPDCCH_StartSF_Type2CSS_Type p_StartSF,
                                                                                     template (value) NPDCCH_Offset_Type2CSS_Type p_Offset) :=
  { /* @status    APPROVED (NBIOT) */
    NumRepetitions := p_NumRepetitions,
    StartSF := p_StartSF,
    Offset := p_Offset,
    SearchSpaceCandidateForRAR := tsc_NPdcchSearchSpaceCandidateIndex_0,
    SearchSpaceCandidateForMsg4 := tsc_NPdcchSearchSpaceCandidateIndex_0
  };

  /*
   * @desc      gets search space parameters out of SIB2 to be used for SS configuration
   * @param     p_Sib2
   * @return    template (value) NPdcchType2CommonSearchSpace_Type
   * @status    APPROVED (NBIOT)
   */
  function fl_NBIOT_SIB2_GetType2CSS(SystemInformationBlockType2_NB_r13 p_Sib2) return template (value) NPdcchType2CommonSearchSpace_Type
  {
    var NPRACH_Parameters_NB_r13 v_NPRACH_Parameters := p_Sib2.radioResourceConfigCommon_r13.nprach_Config_r13.nprach_ParametersList_r13[0];
    var NPDCCH_NumRepetitions_Type2CSS_Type v_NumRepetitions := v_NPRACH_Parameters.npdcch_NumRepetitions_RA_r13;
    var NPDCCH_StartSF_Type2CSS_Type v_StartSF := v_NPRACH_Parameters.npdcch_StartSF_CSS_RA_r13;
    var NPDCCH_Offset_Type2CSS_Type v_Offset := v_NPRACH_Parameters.npdcch_Offset_RA_r13;

    return cs_NPdcchType2CommonSearchSpace(v_NumRepetitions, v_StartSF, v_Offset);
  }

  template (value) NPdcchConfig_Type cs_NPdcchConfig(template (omit) NPdcchUESpecificSearchSpace_Type  p_UESS := omit,
                                                     template (omit) NPdcchType1CommonSearchSpace_Type p_Type1CSS := omit,
                                                     template (omit) NPdcchType2CommonSearchSpace_Type p_Type2CSS := omit,
                                                     template (omit) NB_ToRS_EPRE_Ratio_Type p_RelativeTxPower := omit) :=
  { /* @status    APPROVED (NBIOT) */
    UESS             := p_UESS,
    Type1CSS         := p_Type1CSS,
    Type2CSS         := p_Type2CSS,
    RelativeTxPower  := p_RelativeTxPower
  };

  template (value) NB_PhysicalLayerConfigDL_Type cs_NB_PhysicalLayerConfigDL(AntennaConfig_Type p_AntennaConfig := AN1,
                                                                             SystemInformationBlockType2_NB_r13 p_Sib2,
                                                                             template (value) NPDCCH_ConfigDedicated_NB_r13 p_NPDCCH_ConfigDedicated) :=
  { /* !!!! NBIOT: there is no table in 36.508 for NBIOT yet as Table 6.2.1.2-1 for LTE !!!! */
    /* @status    APPROVED (NBIOT) */
    Antenna := p_AntennaConfig,
    NPbch := {
      RelativeTxPower := cs_NB_PowerRatio_Def
    },
    NPdcch := cs_NPdcchConfig(cs_NPdcchUESpecificSearchSpace(p_NPDCCH_ConfigDedicated.npdcch_NumRepetitions_r13,
                                                             p_NPDCCH_ConfigDedicated.npdcch_StartSF_USS_r13,
                                                             p_NPDCCH_ConfigDedicated.npdcch_Offset_USS_r13),
                              fl_NBIOT_SIB2_GetType1CSS(p_Sib2),
                              fl_NBIOT_SIB2_GetType2CSS(p_Sib2),
                              cs_NB_PowerRatio_Def),
    NPdsch := {
      RelativeTxPower := {
        RachResponse := cs_NB_PowerRatio_Def,
        BcchOnNPdsch := cs_NB_PowerRatio_Def,
        PcchOnNPdsch := cs_NB_PowerRatio_Def,
        CcchOnNPdsch := cs_NB_PowerRatio_Def,
        DcchDtchOnNPdsch := cs_NB_PowerRatioNPDSCH_Def
      }
    },
    NPss := {
      RelativeTxPower := cs_NB_PowerRatio_Def
    },
    NSss := {
      RelativeTxPower := cs_NB_PowerRatio_Def
    },
    LteCrs := omit             // !!!! to be added for In-band transmission with same PCI !!!!!
  };

  template (present) NB_RachPreamble_Type cr_NB_RachPreamble(template (present) integer p_RAPID := ?,
                                                             template (present) integer p_RepetitionsPerPreambleAttempt := ?,
                                                             template integer p_NonAnchorCarrierId := *) :=
  { /* @status    APPROVED (NBIOT) */
    RAPID := p_RAPID,
    RepetitionsPerPreambleAttempt := p_RepetitionsPerPreambleAttempt,
    NonAnchorCarrierId := p_NonAnchorCarrierId    /* @sic R5w180306 - Support of non-anchor carrier enhancements sic@ */
  };

  template (present) NB_SYSTEM_IND car_NB_RachPreamble_IND(NBIOT_CellId_Type p_CellId,
                                                           template (present) NB_RachPreamble_Type p_RachPreamble := cr_NB_RachPreamble) :=
  { /* @status    APPROVED (NBIOT) */
    Common := cr_NB_IndAspCommonPart_SysInd(p_CellId, cr_TimingInfo_Any),
    Indication := {
      RachPreamble := p_RachPreamble
    }
  };

  template (value) NB_RAR_UplinkGrant_Type cs_NB_InitialGrant_Def :=
  { /* Narrowband random access response grant according to 36.213 clause 16.3.3
       @status    APPROVED (NBIOT)
       !!!! NBIOT: 36.508 does not specify any parameters at all for the initial UL grant !!!! */
    SubCarrierIndication := int2bit(tsc_NB_DciSubcarrierIndicationField_Def, 6),
    SchedulingDelay := '00'B,                      // 36.213 clause 16.3.3:    k0=12
    Msg3RepetitionNumber := '000'B,                // 36.213 Table 16.5.1.1-3: NREP=1
    MCSIndex := '010'B                             // 36.213 Table 16.3.3-1:   TBS=88 bits, NRU=1
  };

  template (value) NB_RandomAccessResponseParameters_Type cs_NB_RandomAccessResponseParameters_Def(template (value) RAR_RapIdCtrl_Type p_Rapid,
                                                                                                   template (value) TempC_RNTI_Type p_TempC_RNTI_Type,
                                                                                                   RACH_TimingAdvance_Type p_RAR_TA := 0) :=
  { /* paramenters to control content of RAR sent to the UE */
    /* @status    APPROVED (NBIOT) */
    RapId := p_Rapid,          /* to control Random Access Preamble Id to be sent back to the UE; used in RAR MAC sub-header */
    InitialGrant := cs_NB_InitialGrant_Def,
    TimingAdvance := p_RAR_TA,
    TempC_RNTI := p_TempC_RNTI_Type
  };

  template (value) NB_RarList_Type cs_NB_RarList_Def(RACH_TimingAdvance_Type p_RAR_TA) :=
  { /* @status    APPROVED (NBIOT) */
    cs_NB_RandomAccessResponseParameters_Def(cs_RapIdCtrl_Auto, cs_TempC_RNTI_SameAsC_RNTI, p_RAR_TA)
  };

  template (value) NB_RandomAccessResponseConfig_Type cs_NB_RandomAccessResponseConfig_Common(template (value) NB_RarList_Type p_RarList) :=
  { /* @status    APPROVED (NBIOT) */
    Ctrl := {
      DciInfo := cs_NB_DciInfo_RandomAccessResponse,
      Rar := {
        List := p_RarList
      },
      BackoffInd := cs_BI_None
    }
  };

  template (value) NB_RandomAccessResponseConfig_Type cs_NB_RandomAccessResponseConfig_Def(RACH_TimingAdvance_Type p_RAR_TA := tsc_RACH_TA_Def) :=
  /* @status    APPROVED (NBIOT) */
    cs_NB_RandomAccessResponseConfig_Common(cs_NB_RarList_Def(p_RAR_TA));

  template (value) NB_ContentionResolutionCtrl_Type cs_NB_ContentionResolutionCtrl_Def(template (value) ContentionResolution_ContainedDlschSdu_Type p_ContainedRlcPdu) :=
  { /* @status    APPROVED (NBIOT) */
    TCRNTI_Based := {
      MacPdu := {
        ContainedId := {
          XorMask := tsc_ContentionResolutionId_Unchanged
        },
        ContainedRlcPdu := p_ContainedRlcPdu
      }
    }
  };

  template (value) NB_ContentionResolutionCtrl_Type cs_NB_ContentionResolutionCtrl_CRNTI(integer p_Iru := 0,
                                                                                         integer p_Imcs := 0) :=
  { /* p_Iru := 0, p_Imcs := 0 result in small grant of 16 bits as asynchronous adaptive HARQ ACK
       @status    APPROVED (NBIOT) */
    CRNTI_Based := {
      AutomaticGrant := cs_NB_DciInfo_CcchDcchDtchUL(p_Iru, p_Imcs)
    }
  };

  template (value) NB_ContentionResolutionCtrl_Type cs_NB_ContentionResolutionCtrl_TCRNTI_None :=
  { /* results in no Contention resolution for T-CRNTI */
    /* @status    APPROVED (NBIOT) */
    TCRNTI_Based := {
      NoContResolID := true //SS shall not include contention resolution ID
    }
  };

  template (value) NB_RachProcedure_Type cs_NB_RachProcedure(template (value) NB_RandomAccessResponseConfig_Type p_RAResponse,
                                                             template (value) NB_ContentionResolutionCtrl_Type p_ContentionResolutionCtrl) :=
  { /* @status    APPROVED (NBIOT) */
    RAResponse := p_RAResponse,
    ContentionResolutionCtrl := p_ContentionResolutionCtrl
  };

  template (value) NB_RachProcedureList_Type cs_NB_RachProcedureList_Def(RACH_TimingAdvance_Type p_RAR_TA,
                                                                         template (value) ContentionResolution_ContainedDlschSdu_Type p_ContainedRlcPdu) :=
  { /* @status    APPROVED (NBIOT) */
    cs_NB_RachProcedure(cs_NB_RandomAccessResponseConfig_Def(p_RAR_TA), cs_NB_ContentionResolutionCtrl_Def(p_ContainedRlcPdu))
  };

  template (value) NB_RachProcedureConfig_Type cs_NB_RachProcedureConfig(template (omit) NB_RACH_ConfigCommon_Type p_RACH_ConfigCommon,
                                                                         template (value) NB_RachProcedureList_Type p_RachProcedureList) :=
  { /* @status    APPROVED (NBIOT) */
    RACH_ConfigCommon := p_RACH_ConfigCommon,
    RachProcedureList := p_RachProcedureList
  };

  template (value) NB_StaticCellInfo_Type cs_NB_StaticCellInfo(NBIOT_CellInfo_Type p_CellInfo,
                                                               template (omit) NB_CommonStaticLteCellInfo_Type p_LTEInfo) :=
  { /* @status    APPROVED (NBIOT) */
    Common := {
      PhysicalCellId := p_CellInfo.CellIds.PhysicalCellId,
      NB_Band        := p_CellInfo.Frequency.FrequencyBandIndicator,
      CellTimingInfo := {
        TcOffset := 0,               /* @sic R5w180111 sic@ */
        Tcell := p_CellInfo.CellTimingInfo.Tcell,
        SfnOffset := p_CellInfo.CellTimingInfo.SfnOffset,
        HsfnOffset := p_CellInfo.CellTimingInfo.HsfnOffset
      },
      OperationModeInfo := p_CellInfo.Sysinfo.BCCH_Info.MIB_NB.message_.operationModeInfo_r13,
      LteCellInfo := p_LTEInfo
    },
    Earfcn := p_CellInfo.Frequency.UL_DL_Earfcn
  };

  template (value) NB_CommonStaticLteCellInfo_Type cs_NB_CommonStaticLteCellInfo (integer p_Earfcn,
                                                                                  EUTRA_ASN1_Dl_Bandwidth_Type p_Bandwidth,
                                                                                  EUTRA_CyclicPrefix_Type p_CyclicPrefix,
                                                                                  EUTRA_ASN1_PhysCellId_Type p_PhysicalCellId,
                                                                                  NBIOT_CellInfo_Type p_CellInfo) :=
  { /* @status    APPROVED (NBIOT) */
    Earfcn := p_Earfcn,
    Bandwidth := p_Bandwidth,
    CyclicPrefix := p_CyclicPrefix,
    PhysicalCellId := p_PhysicalCellId,
    EutraControlRegionSize := p_CellInfo.Sysinfo.BCCH_Info.SIB1_NB.message_.c1.systemInformationBlockType1_r13.eutraControlRegionSize_r13  /* @sic R5s170030 change 5: SIB1_NB sic@ */
  };

  template (value) NB_UL_TransRetransmission_Type cs_NB_UL_TransRetransmission_ReTransmission(integer p_RedundancyVersion) :=
  { /* @status    APPROVED (NBIOT) */
    ReTransmissionAdaptive := {
      RedundancyVersion := p_RedundancyVersion
    }
  };

  template (value) NB_UL_TransRetransmission_Type cs_NB_UL_TransRetransmission_NewTransmission := { NewTransmission := true };                 /* @status    APPROVED (NBIOT) */

  template (value) NB_UL_TransRetransmissionList_Type cs_NB_TransRetransmissionList_Def := { cs_NB_UL_TransRetransmission_NewTransmission };   /* @status    APPROVED (NBIOT) */

  template (value) NB_DciUlInfo_Type cs_NB_DciInfo_CcchDcchDtchUL(integer p_Iru := 5,
                                                                  integer p_Imcs := 10,
                                                                  template (value) NB_UL_TransRetransmissionList_Type p_TransRetransmissionList := cs_NB_TransRetransmissionList_Def,
                                                                  template (omit) integer p_HarqProcessNumber := omit) :=
  { /* DCI Format N0 acc. to 36.212 clause 6.4.3.1 with values acc. to 36.508 Table 8.1.3.6.1.1-1;
       Resource assignment (p_Iru) and Imcs (p_Imcs) to be chosen according to test requirements, with p_Iru := 5, p_Imcs := 10 a maximum grant of 1000 bits is assigned
       (taking 6 RUs or 6ms for 15 kHz subcarrier spacing)
       NOTE: according to 36.508 Table 8.1.3.3.4-1 NBIOT signalling conformance test are using single tone and 15 kHz subcarrier spacing
       @sic R5s170030 change 6: for single tone (N(RU, sc) == 1) the maximum value for Itbs is 10 sic@ */
    /* @status    APPROVED (NBIOT) */
    SubCarrierIndication := tsc_NB_DciSubcarrierIndicationField_Def,
    ResourceAssignment := p_Iru,
    SchedulingDelay := 0,
    Imcs := p_Imcs,
    RedundancyVersion := 0,
    RepetitionNumber := 0,
    DCISubFrameRepetitionNumber := 0,
    TransRetransmissionList := p_TransRetransmissionList,
    HARQ_ProcessNumber := p_HarqProcessNumber              /* @sic R5w180305: support of two HARQ processes sic@ */
  };

  template (value) NB_SYSTEM_CTRL_REQ cas_NB_CellConfig_Def_REQ(NBIOT_CellId_Type                p_CellId,
                                                                template (value) TimingInfo_Type p_TimingInfo,
                                                                NBIOT_CellInfo_Type              p_CellInfo,
                                                                template (value) ContentionResolution_ContainedDlschSdu_Type p_ContentionResolution_ContainedRlcPdu,
                                                                template (value) NPDCCH_ConfigDedicated_NB_r13 p_NPDCCH_ConfigDedicated := cs_NPDCCH_ConfigDedicated_NB_DEFAULT,
                                                                template (value) NPUSCH_ConfigDedicated_NB_r13 p_NPUSCH_ConfigDedicated := cs_NPUSCH_ConfigDedicated_NB_DEFAULT,
                                                                template (omit) NB_CommonStaticLteCellInfo_Type p_LTEInfo) :=
  { /* @status    APPROVED (NBIOT) */
    Common := cs_NB_ReqAspCommonPart_CellCfg(p_CellId, p_TimingInfo),
    Request := {
      Cell := {
        AddOrReconfigure := {
          Basic := {
            StaticCellInfo := cs_NB_StaticCellInfo(p_CellInfo, p_LTEInfo), // @sic R5w180205r1 sic@
            PhysicalLayerConfigDL := cs_NB_PhysicalLayerConfigDL(-,
                                                                 p_CellInfo.Sysinfo.BCCH_Info.SIs[0].message_.c1.systemInformation_r13.criticalExtensions.systemInformation_r13.sib_TypeAndInfo_r13[0].sib2_r13,
                                                                 p_NPDCCH_ConfigDedicated),
            InitialCellPower := {
              MaxReferencePower := p_CellInfo.ReferencePower.MaxReferencePower,
              Attenuation := p_CellInfo.ReferencePower.Attenuation
            },
            DownlinkBitmapConfig := {
              None := true
            },
            BcchConfig := {
              SIConfig := {
                SchedulingInfoSIB1_NB := p_CellInfo.Sysinfo.BCCH_Info.MIB_NB.message_.schedulingInfoSIB1_r13,
                SiSchedul := fl_NBIOT_SS_SetupSchedulingInfo(p_CellInfo.Sysinfo.BCCH_Info.SIB1_NB.message_.c1.systemInformationBlockType1_r13)  /* @sic R5s170030 change 5: SIB1_NB sic@ */
              },
              BcchInfo := p_CellInfo.Sysinfo.BCCH_Info
            },
            PcchConfig := {
              DciInfoPaging := cs_NB_DciInfo_Pcch,
              NoOfDciRepetitionsForDirectIndication := 1    /* !!!! NBIOT: not specified anywhere !!!! */
            }
          },
          Active := {
            C_RNTI := p_CellInfo.RNTI,
            PhysicalLayerConfigUL := {
              NPrach := cs_NPRACH_Config_r13(p_CellInfo.Sysinfo.BCCH_Info.SIs[0].message_.c1.systemInformation_r13.criticalExtensions.systemInformation_r13.sib_TypeAndInfo_r13[0].sib2_r13.radioResourceConfigCommon_r13.nprach_Config_r13),
              NPusch := {
                Common := cs_NPUSCH_ConfigCommon_r13(p_CellInfo.Sysinfo.BCCH_Info.SIs[0].message_.c1.systemInformation_r13.criticalExtensions.systemInformation_r13.sib_TypeAndInfo_r13[0].sib2_r13.radioResourceConfigCommon_r13.npusch_ConfigCommon_r13),
                Dedicated := cs_NPUSCH_ConfigDedicated_r13(p_NPUSCH_ConfigDedicated)
              },
              TimingAdvance := {
                InitialValue := 0
              },
              SubCarrierSpacingUL := p_CellInfo.PhysicalParameters.SubCarrierSpacingUL
            },
            RachProcedureConfig := cs_NB_RachProcedureConfig(cs_NB_RACH_ConfigCommon_r13(p_CellInfo.Sysinfo.BCCH_Info.SIs[0].message_.c1.systemInformation_r13.criticalExtensions.systemInformation_r13.sib_TypeAndInfo_r13[0].sib2_r13.radioResourceConfigCommon_r13.rach_ConfigCommon_r13),
                                                             cs_NB_RachProcedureList_Def(p_CellInfo.PhysicalParameters.RAR_TA, p_ContentionResolution_ContainedRlcPdu)),
            CcchDcchDtchConfig := {
              DL := {
                DciInfo := cs_NB_DciDlInfo_Auto,
                GapConfig := {
                  None := true
                }
              },
              UL := {
                UplinkTimeAlignment_Synch := {
                  None  := true
                },
                UESS_GrantScheduling := {
                  None := true
                }
              },
              DrxCtrl := cs_NB_DrxCtrl_None
            },
            NonAnchorCarrier := {
              None := true
            },
            CarrierConfigCommonDL := {  /* @sic R5w180306 - Support of non-anchor carrier enhancements sic@ */
              None := true
            },
            CarrierConfigCommonUL := {  /* @sic R5w180306 - Support of non-anchor carrier enhancements sic@ */
              None := true
            }
          }
        }
      }
    }
  };

  template (value) NB_ActiveCellConfig_Type  cs_NB_NewC_RNTI_ConfigReq(template (value) EUTRA_ASN1_C_RNTI_Type p_CRNTI) :=
  { /* @status    APPROVED (NBIOT) */
    C_RNTI  := p_CRNTI,
    PhysicalLayerConfigUL := omit,
    RachProcedureConfig   := omit,
    CcchDcchDtchConfig    := omit,
    NonAnchorCarrier      := omit,
    CarrierConfigCommonDL := omit,  /* @sic R5w180306 - Support of non-anchor carrier enhancements sic@ */
    CarrierConfigCommonUL := omit   /* @sic R5w180306 - Support of non-anchor carrier enhancements sic@ */
  };

  template (value) NB_SYSTEM_CTRL_REQ cas_NB_SYSTEM_CTRL_REQ_CellConfigOmit(NBIOT_CellId_Type p_CellId,
                                                                            template (value) TimingInfo_Type  p_TimingInfo := cs_TimingInfo_Now,
                                                                            template (omit) boolean p_CnfFlag := omit) :=
  { /* @status    APPROVED (NBIOT) */
    Common := cs_NB_ReqAspCommonPart_CellCfg(p_CellId, p_TimingInfo, p_CnfFlag),
    Request := {
      Cell := {
        AddOrReconfigure := {
          Basic := omit,
          Active := omit
        }
      }
    }
  };

  template (value) NB_SYSTEM_CTRL_REQ cads_NB_ActiveCellConfig_REQ(NBIOT_CellId_Type p_CellId,
                                                                   template (value) TimingInfo_Type p_TimingInfo,
                                                                   template (omit) boolean p_CnfFlag := omit,
                                                                   template (value) NB_ActiveCellConfig_Type p_ActiveCellConfig)
    modifies cas_NB_SYSTEM_CTRL_REQ_CellConfigOmit :=
  { /* @status    APPROVED (NBIOT) */
    Request := {
      Cell := {
        AddOrReconfigure := {
          Active := p_ActiveCellConfig
        }
      }
    }
  };

  template (value) NB_SYSTEM_CTRL_REQ cads_NB_ULGrantScheduling_REQ(NBIOT_CellId_Type   p_CellId,
                                                                    template (value) TimingInfo_Type p_TimingInfo := cs_TimingInfo_Now,
                                                                    template (omit)  boolean p_CnfFlag := false,//omit,
                                                                    template (omit)  UplinkTimeAlignment_Synch_Type p_UplinkTimeAlignment_Synch := cs_UplinkTimeAlignment_Synch_None,
                                                                    template (omit)  NB_UESS_GrantScheduling_Type p_UL_GrantScheduling)
    modifies cas_NB_SYSTEM_CTRL_REQ_CellConfigOmit :=
  { /* common template to be used in all UL grant configurations;
       acc. to 36.508 Table 8.1.8.2.1.5-1 per default timeAlignmentTimerDedicated is set to 'infinity' in MAC-MainConfig-NB-SRB
       => p_UplinkTimeAlignment_Synch is cs_UplinkTimeAlignment_Synch_None per default */
    /* @status    APPROVED (NBIOT) */
      //VZW NBIOT ADDED
   Request := {
      Cell := {
        AddOrReconfigure := {
          Active := {
            C_RNTI := omit,
            PhysicalLayerConfigUL := omit,
            RachProcedureConfig  := omit,
            CcchDcchDtchConfig := {
              DL := omit,
              UL := {
                UplinkTimeAlignment_Synch := p_UplinkTimeAlignment_Synch,
                UESS_GrantScheduling := p_UL_GrantScheduling
              },
                DrxCtrl := omit
            },
              NonAnchorCarrier := omit,
					CarrierConfigCommonDL := omit,
					CarrierConfigCommonUL := omit
          }
        }
      }
    }
  };

  template (value) NB_SYSTEM_CTRL_REQ cas_NB_ULGrantAllocation_Stop_REQ(NBIOT_CellId_Type p_CellId) := cads_NB_ULGrantScheduling_REQ(p_CellId, -, -, -, cs_NB_UL_GrantScheduling_Stop);  /* @status    APPROVED (NBIOT) */

  template (value) NB_SYSTEM_CTRL_REQ cads_NB_CcchDcchDtchDL_Config_REQ(NBIOT_CellId_Type p_CellId,
                                                                        template (value) TimingInfo_Type p_TimingInfo,
                                                                        template (omit)  boolean p_CnfFlag := omit,
                                                                        template (value) NB_DciDlInfo_Type p_DciDlInfo)
    modifies cas_NB_SYSTEM_CTRL_REQ_CellConfigOmit :=
  { /* @status    APPROVED (NBIOT) */
    Request := {
      Cell := {
        AddOrReconfigure := {
          Active := {
            CcchDcchDtchConfig := {
              DL := {
                DciInfo := p_DciDlInfo
              }
            }
          }
        }
      }
    }
  };

  template (value) NB_PeriodicGrant_Type cs_NB_ContinuousGrant(template (value) NB_DciUlInfo_Type p_DciUlInfo := cs_NB_DciInfo_CcchDcchDtchUL,
                                                              integer p_Periodicity := 2) :=
  { /* @status    APPROVED (NBIOT) */
    DciInfo := p_DciUlInfo,
    NoOfRepetitions := cs_TransmissionRepetition_Continuous,
    Periodicity := p_Periodicity
  };

  template (value) NB_PeriodicGrant_Type cs_NB_PeriodicGrant(template (value) NB_DciUlInfo_Type p_DciUlInfo := cs_NB_DciInfo_CcchDcchDtchUL,
                                                             integer p_Cycles,
                                                             integer p_Periodicity := 1) :=
  { /* @status    APPROVED (NBIOT) */
    DciInfo := p_DciUlInfo,
    NoOfRepetitions := cs_TransmissionRepetitionCyclic(p_Cycles),
    Periodicity := p_Periodicity
  };

  template (value) NB_PeriodicGrant_Type cs_NB_SingleGrant(template (value) NB_DciUlInfo_Type p_DciUlInfo := cs_NB_DciInfo_CcchDcchDtchUL) :=
  { /* @status    APPROVED (NBIOT) */
    DciInfo := p_DciUlInfo,
    NoOfRepetitions := cs_TransmissionRepetitionCyclic(1),
    Periodicity := omit
  };

  template (value) NB_UESS_GrantScheduling_Type cs_NB_UL_GrantScheduling_Start(template (value) NB_PeriodicGrant_Type p_PeriodicGrant := cs_NB_ContinuousGrant) :=
  { /* @status    APPROVED (NBIOT) */
    Periodic := p_PeriodicGrant
  };

  template (value) NB_UESS_GrantScheduling_Type cs_NB_UL_GrantScheduling_Stop :=
  { /* @status    APPROVED (NBIOT) */
    None  := true
  };

  template (value) NB_SYSTEM_CTRL_REQ cads_NB_DrxCtrl_REQ(NBIOT_CellId_Type p_CellId,
                                                          template (value) TimingInfo_Type  p_TimingInfo := cs_TimingInfo_Now,
                                                          template (omit) boolean p_CnfFlag := omit,
                                                          template (value) NB_DrxCtrl_Type p_DrxCtrl) modifies cas_NB_SYSTEM_CTRL_REQ_CellConfigOmit :=
  { /* @status    APPROVED (NBIOT) */
    Request := {
      Cell := {
        AddOrReconfigure := {
          Active := {
            CcchDcchDtchConfig := {
              DrxCtrl := p_DrxCtrl
            }
          }
        }
      }
    }
  };

  template (value) NB_SYSTEM_CTRL_REQ cads_NB_ModifySysInfo_REQ(NBIOT_CellId_Type p_CellId,
                                                                template (value) TimingInfo_Type p_TimingInfo,
                                                                template (omit) boolean p_CnfFlag := omit,
                                                                NBIOT_CellSysInfo_Type p_SysInfo)
    modifies cas_NB_SYSTEM_CTRL_REQ_CellConfigOmit :=
  { /* @status    APPROVED (NBIOT) */
    Request := {
      Cell := {
        AddOrReconfigure := {
          Basic := {
            StaticCellInfo := omit,
            PhysicalLayerConfigDL := omit,
            InitialCellPower := omit,
            DownlinkBitmapConfig := omit,
            BcchConfig := {
              SIConfig := {
                SchedulingInfoSIB1_NB := p_SysInfo.BCCH_Info.MIB_NB.message_.schedulingInfoSIB1_r13,
                SiSchedul := fl_NBIOT_SS_SetupSchedulingInfo(p_SysInfo.BCCH_Info.SIB1_NB.message_.c1.systemInformationBlockType1_r13)  /* @sic R5s170030 change 5: SIB1_NB sic@ */
              },
              BcchInfo := p_SysInfo.BCCH_Info
            },
            PcchConfig := omit
          }
        }
      }
    }
  };

  template (value) NB_SYSTEM_CTRL_REQ cads_NB_CellConfig_UpdatePRACH_Info_REQ(NBIOT_CellId_Type p_CellId,
                                                                              template (value) TimingInfo_Type p_TimingInfo,
                                                                              template (omit)  boolean p_CnfFlag := omit,
                                                                              template (value) NPRACH_ConfigSIB_NB_r13 p_NPRACH_ConfigSIB)
    modifies cas_NB_SYSTEM_CTRL_REQ_CellConfigOmit  :=
  { /* @status    APPROVED (NBIOT) */
    Request := {
      Cell := {
        AddOrReconfigure := {
          Active := {
            PhysicalLayerConfigUL := {
              NPrach := cs_NPRACH_Config_r13(p_NPRACH_ConfigSIB)
            }
          }
        }
      }
    }
  };

  template (value) NB_SYSTEM_CTRL_REQ cads_NB_RachProcedure_Config_REQ(NBIOT_CellId_Type p_CellId,
                                                                       template (value) TimingInfo_Type p_TimingInfo,
                                                                       template (omit)  boolean p_CnfFlag := omit,
                                                                       template (omit) NB_RACH_ConfigCommon_Type p_RACH_ConfigCommon := omit,
                                                                       template (value) NB_RachProcedureList_Type p_RachProcedureList)
    modifies cas_NB_SYSTEM_CTRL_REQ_CellConfigOmit :=
  { /* @status    APPROVED (NBIOT) */
    Request := {
      Cell := {
        AddOrReconfigure := {
          Active := {
            RachProcedureConfig := cs_NB_RachProcedureConfig(p_RACH_ConfigCommon, p_RachProcedureList)
          }
        }
      }
    }
  };

  template (value) NB_SYSTEM_CTRL_REQ cas_NB_CellConfig_Power_REQ(template (value) NB_CellAttenuationList_Type p_CellAttenuationList,
                                                                  template (value) TimingInfo_Type p_TimingInfo,
                                                                  NBIOT_CellId_Type p_CellId := nbiot_Cell_NonSpecific,
                                                                  template (omit) boolean p_CnfFlag := omit) :=
  { /* ASPs to modify cell power */
    /* @status    APPROVED (NBIOT) */
    Common := cs_NB_ReqAspCommonPart_CellCfg( p_CellId, p_TimingInfo, p_CnfFlag),
    Request := {
      CellAttenuationList := p_CellAttenuationList
    }
  };

}
