/******************************************************************************/
// @copyright   Copyright Notification
//   No part may be reproduced except as authorized by written permission.
//   The copyright and the foregoing restriction extend to reproduction in all media.
//   Trademark 2019, 3GPP Organizational Partners (ARIB, ATIS, CCSA, ETSI, TSDSI, TTA, TTC).
//   All rights reserved.
// @version: 36.523-3 v15.3.0
// $Date: 2018-09-08 00:42:37 +0200 (Sat, 08 Sep 2018) $
// $Rev: 22263 $
/******************************************************************************/
module NBIOT_Idle_CellSelection {

  import from NBIOT_RRC_ASN1_Definitions language "ASN.1:2002" all with {encode "UNALIGNED_PER_OctetAligned"};
  import from CommonDefs all;
  import from EPS_NAS_Constants all;
  import from EPS_NAS_MsgContainers all;
  import from EUTRA_NB_AuxiliaryFunctions all;
  import from EUTRA_NB_CommonDefs all;
  import from NAS_CommonTemplates all;
  import from NAS_CommonTypeDefs all;
  import from NBIOT_CellInfo all;
  import from NBIOT_CommonDefs all;
  import from NBIOT_CommonProcedures all;
  import from NBIOT_Component all;
  import from NBIOT_ConfigurationSteps all;
  import from NBIOT_Imported_EUTRA_ASN1_Types all;
  import from NBIOT_NASSteps all;
  import from NBIOT_RRCSteps all;
  import from NBIOT_RRC_Templates all;
  import from NBIOT_RSRQ_Functions all;
  import from NBIOT_SRB_DRB_Templates all;
  import from NBIOT_SecuritySteps all;
  import from NBIOT_SysInfo_Templates all;
  import from NBIOT_Timing all;
  import from UpperTesterFunctions all;

  /*
   * @desc      UE Registration acc. to 36.508 cl. 8.1.5.2
   * @param     p_CellId
   * @param     p_IOT_State
   * @param     p_Type
   * @param     p_State             (default value: STATE2_NB_CONNECTED_UPDATED)
   * @param     p_EquivalentPlmnList
   * @status    APPROVED (NBIOT)
   */
  function fl_NBIOT_InitialRegistration_EPLMN(NBIOT_CellId_Type p_CellId,
                                             IOT_STATE_Type p_IOT_State,
                                             IDLEUPDATED_Type p_Type,
                                             NBIOT_IDLEUPDATED_STATE_Type p_State := STATE2_NB_CONNECTED_UPDATED,
                                             template (value) PLMN_List p_EquivalentPlmnList) runs on NBIOT_PTC
  {
    var NAS_MSG_Indication_Type v_NAS_Ind;

    v_NAS_Ind := f_NBIOT_InitialRegistration_Step2_4(p_CellId, p_IOT_State, p_Type);

    f_NBIOT_InitialRegistration_Step5_12(p_CellId, p_IOT_State, p_State, v_NAS_Ind);

    f_NBIOT_InitialRegistration_Step13(p_CellId,
                                       p_IOT_State,
                                       v_NAS_Ind,
                                       -,
                                       -,
                                       -,
                                       -,
                                       -,
                                       -,
                                       -,
                                       -,
                                       -,
                                       -,
                                       p_EquivalentPlmnList);
    
    f_NBIOT_InitialRegistration_Step14(p_CellId, p_IOT_State, tsc_EpsDefaultBearerId);
    
    select (p_State) {
      case (STATE3_NB_IDLEUPDATED,
            STATE3A_NB_IDLEUPDATED_TESTLOOP_ModeA,
            STATE3A_NB_IDLEUPDATED_TESTLOOP_ModeB,
            STATE3A_NB_IDLEUPDATED_TESTLOOP_ModeG,
            STATE3A_NB_IDLEUPDATED_TESTLOOP_ModeH) {
        f_NBIOT_RRC_ConnectionRelease(p_CellId);
      }
    }
  }

  /*
   * @desc      REFERENCE TS 36.523-1 clause 22.2.1
   * @status    APPROVED (NBIOT)
   */
  function f_TC_22_2_1_NBIOT() runs on NBIOT_PTC
  {/*NB-IoT / PLMN selection of RPLMN, HPLMN / EHPLMN, UPLMN and OPLMN / Automatic mode*/
    var template (value) NB_CellPowerList_Type v_CellPowerList_AtT1;
    var template (value) NB_CellPowerList_Type v_CellPowerList_AtT2;
    var template (value) NB_CellPowerList_Type v_CellPowerList_AtT3;
    var template (value) NB_CellPowerList_Type v_CellPowerList_AtT4;
    var SystemInformationBlockType3_NB_r13 v_SIB3;
    timer t_HpPlmn_lower;
    timer t_HpPlmn_upper;
    timer t_IdleMode_GenericTimer := 360.0;
    
    v_CellPowerList_AtT1 := {
      cs_NB_CellPower(nbiot_Cell1, tsc_ServingCellRS_EPRE),
      cs_NB_CellPower(nbiot_Cell2, -79),
      cs_NB_CellPower(nbiot_Cell4, tsc_ServingCellRS_EPRE)
    };
    v_CellPowerList_AtT2 := {
      cs_NB_CellPower(nbiot_Cell1, -79),
      cs_NB_CellPower(nbiot_Cell2, tsc_ServingCellRS_EPRE)
    };
    v_CellPowerList_AtT3 := {
      cs_NB_CellPower(nbiot_Cell1, tsc_NonSuitableOffNBIOTCellRS_EPRE),
      cs_NB_CellPower(nbiot_Cell4, -79)
    };
    v_CellPowerList_AtT4 := {
      cs_NB_CellPower(nbiot_Cell4, tsc_ServingCellRS_EPRE),
      cs_NB_CellPower(nbiot_Cell11, -79)
    };
    
    //Initialise all Ncell, security and mobile parameters
    f_NBIOT_Init(c2);
    
    //Set Sys info and cell info for Ncell 2, 4 & 11 to be used @ creation
    f_NBIOT_CellInfo_SetPLMN_1Entry(nbiot_Cell2, cs_HPLMN_001_11);  //PLMN1
    f_NBIOT_CellInfo_SetPLMN_1Entry(nbiot_Cell4, cs_HPLMN_001_21);  //PLMN2
    f_NBIOT_CellInfo_SetPLMN_1Entry(nbiot_Cell11, cs_HPLMN_001_31);  //PLMN3

    //Set TAC for Ncell1, Ncell2, Ncell4 & Ncell11
    //TAC for Ncell1 is set to 1 in f_NBIOT_Init
    f_NBIOT_CellInfo_SetTAC(nbiot_Cell2, tsc_Idle_TAC_Cell2);
    f_NBIOT_CellInfo_SetTAC(nbiot_Cell4, tsc_Idle_TAC_Cell4);
    f_NBIOT_CellInfo_SetTAC(nbiot_Cell11, tsc_Idle_TAC_Cell11);

    f_NBIOT_CellInfo_InitMaxReferencePower(nbiot_Cell1, -79); // @sic R5s170478 sic@
    f_NBIOT_CellInfo_InitMaxReferencePower(nbiot_Cell2, -79);
    f_NBIOT_CellInfo_InitMaxReferencePower(nbiot_Cell4, -79);
    f_NBIOT_CellInfo_InitMaxReferencePower(nbiot_Cell11, -79);
    
    //Table 22.2.1.3.3-1 SystemInformationBlockType3 for Ncells 2 and 4 ,preamble and all steps
    v_SIB3 := f_NBIOT_CellInfo_GetSIB3(nbiot_Cell2);
    v_SIB3.cellReselectionInfoCommon_r13.q_Hyst_r13 := dB24;
    f_NBIOT_CellInfo_SetSIB3 (nbiot_Cell2, v_SIB3);
    
    v_SIB3 := f_NBIOT_CellInfo_GetSIB3(nbiot_Cell4);
    v_SIB3.cellReselectionInfoCommon_r13.q_Hyst_r13 := dB24;
    f_NBIOT_CellInfo_SetSIB3 (nbiot_Cell4, v_SIB3);
    
    //Configure and activate all Ncells
    f_NBIOT_CellConfig_Def(nbiot_Cell2, CONTROL_PLANE);
    f_NBIOT_CellConfig_Def(nbiot_Cell1, CONTROL_PLANE);
    f_NBIOT_CellConfig_Def(nbiot_Cell4, CONTROL_PLANE);
    f_NBIOT_CellConfig_Def(nbiot_Cell11, CONTROL_PLANE);
    
    //Equip UE with a USIM containing default values except for those shown in Table 22.2.1.3.1-1
    f_UT_USIM_Insert(UT, "36.523-1 Table 22.2.1.3.1-1");
    
    //Preamble: The UE is made to camp on Ncell 2 and then in state Switched OFF (State 1)
    f_NBIOT_Preamble(nbiot_Cell2, CONTROL_PLANE, STATE2_NB_CONNECTED_UPDATED);
    f_NBIOT_UE_Detach_SwitchOffUe(nbiot_Cell2, CONTROL_PLANE, RRC_CONNECTED);
    
    f_NBIOT_TestBody_Set(true);
    
    //@siclog "Step 1" siclog@
    //Adjust cell configuration according to T1
    f_NBIOT_SetCellPowerList(v_CellPowerList_AtT1);
    
    //@siclog "Step 2" siclog@
    //Power on UE
    f_UT_SwitchOnUE(UT, false);
    t_HpPlmn_lower.start(f_NBIOT_SetTimerToleranceMin(nonProtocolTimer, 120.0));  //120s
    t_HpPlmn_upper.start(f_NBIOT_SetTimerToleranceMax(nonProtocolTimer, 7500.0)) ; //125 mins
    t_IdleMode_GenericTimer.start;
    
    //@siclog "Step 3-15" siclog@
    alt {
      [] SRB.check (receive(car_NB_SRB0_RrcPdu_IND(nbiot_Cell2, cr_RRCConnectionRequest_NB(cr_EstablishmentCause_NB_Any, ?))))
        {
          t_IdleMode_GenericTimer.stop;
          //* @verdict pass RRCConnectionRequest-NB message received on Ncell 2
          f_NBIOT_PreliminaryPass (__FILE__, __LINE__, "Test Case 22.2.1 Step 3");
        }
    }
    f_NBIOT_InitialRegistration(nbiot_Cell2, CONTROL_PLANE, PREAMBLE);
    
    //@siclog "Step 15A" siclog@
    f_NBIOT_RRC_ConnectionRelease(nbiot_Cell2);
    
    //@siclog "Step 16" siclog@
    //Check if PLMN1 is indicated by UE
    f_UT_PLMN_Check(UT, "00111");
    //* @verdict pass PLMN1 indicated by UE
    f_NBIOT_PreliminaryPass (__FILE__, __LINE__, "Test Case 22.2.1 Step 16");
    
    //@siclog "Step 16A" siclog@
    //Adjust cell configuration according to T2
    f_NBIOT_SetCellPowerList(v_CellPowerList_AtT2);
    
    //@siclog "Step 17-22" siclog@
    //Check for RRCConnectionRequest-NB on Ncell1 / PLMN4
    alt {
      [] SRB.receive( car_NB_SRB0_RrcPdu_IND(nbiot_Cell1, cr_RRCConnectionRequest_NB(cr_EstablishmentCause_NB_Any, ?)))
        {
          //* @verdict fail RRCConnectionRequest-NB received on PLMNN4 / Ncell 1 within t_HpPlmn_lower time
          f_NBIOT_SetVerdictFailOrInconc (__FILE__, __LINE__, "Test Case 22.2.1 Step 17");
        }
      [] t_HpPlmn_lower.timeout
        {
          alt {
            [] SRB.check (receive(car_NB_SRB0_RrcPdu_IND(nbiot_Cell1, cr_RRCConnectionRequest_NB(cr_EstablishmentCause_NB_Any, ?))))
              {
                t_HpPlmn_upper.stop; // @sic R5s170478 sic@
                //* @verdict pass RRCConnectionRequest-NB received on PLMN4 / Ncell 1 after t_HpPlmn_lower time but before t_HpPlmn_upper
                f_NBIOT_PreliminaryPass (__FILE__, __LINE__, "Test Case 22.2.1 Step 17");
              }
            [] t_HpPlmn_upper.timeout
              {
                //* @verdict fail No RRCConnectionRequest-NB message received on Ncell 1 within t_HpPlmn_upper time
                f_NBIOT_SetVerdictFailOrInconc (__FILE__, __LINE__, "Test Case 22.2.1 Step 17");
              }
          }
        }
    }
    
    //Perform TAU procedure on Ncell 1
    f_NBIOT_TrackingAreaUpdate(nbiot_Cell1, CONTROL_PLANE);
    
    //@siclog "Step 23" siclog@
    //Check if PLMN4 is indicated by UE
    f_UT_PLMN_Check(UT, "00101");
    //* @verdict pass PLMN4 is indicated by UE
    f_NBIOT_PreliminaryPass (__FILE__, __LINE__, "Test Case 22.2.1 Step 23");
    
    //@siclog "Step 24" siclog@
    //Adjust cell configuration according to T3
    f_NBIOT_SetCellPowerList(v_CellPowerList_AtT3);
    
    //@siclog "Step 25" siclog@
    //Check if UE camps on Ncell 4
    f_NBIOT_508CheckCampOnNewNBIOTCell (nbiot_Cell4, CONTROL_PLANE);
    t_HpPlmn_lower.start(f_NBIOT_SetTimerToleranceMin(nonProtocolTimer, 120.0));  //120s
    t_HpPlmn_upper.start(f_NBIOT_SetTimerToleranceMax(nonProtocolTimer, 7500.0)) ; //125 mins
    
    //@siclog "Step 26" siclog@
    //Adjust cell configuration according to T4
    f_NBIOT_SetCellPowerList(v_CellPowerList_AtT4);
    
    //@siclog "Step 27" siclog@
    //Check if PLMN2 is indicated by UE
    f_UT_PLMN_Check(UT, "00121");
    //* @verdict pass PLMN2 is indicated by UE
    f_NBIOT_PreliminaryPass (__FILE__, __LINE__, "Test Case 22.2.1 Step 27");
     
    //@siclog "Step 28-33" siclog@
    //Check for RRCConnectionRequest-NB on Ncell 11
    alt {
      [] SRB.receive( car_NB_SRB0_RrcPdu_IND(nbiot_Cell11, cr_RRCConnectionRequest_NB(cr_EstablishmentCause_NB_Any, ?)))
        {
          //* @verdict fail RRCConnectionRequest-NB received on PLMNN4 / Ncell 11 within t_HpPlmn_lower time
          f_NBIOT_SetVerdictFailOrInconc (__FILE__, __LINE__, "Test Case 22.2.1 Step 28");
        }
      [] t_HpPlmn_lower.timeout
        {
          alt {
            [] SRB.check(receive(car_NB_SRB0_RrcPdu_IND(nbiot_Cell11, cr_RRCConnectionRequest_NB(cr_EstablishmentCause_NB_Any, ?))))
              {
                t_HpPlmn_upper.stop; // @sic R5s170478 sic@
                //* @verdict pass RRCConnectionRequest-NB received on PLMN4 / Ncell 11 after t_HpPlmn_lower time but before t_HpPlmn_upper
                f_NBIOT_PreliminaryPass (__FILE__, __LINE__, "Test Case 22.2.1 Step 28");
              }
            [] t_HpPlmn_upper.timeout
              {
                //* @verdict fail No RRCConnectionRequest-NB message received on Ncell 11 within t_HpPlmn_upper time
                f_NBIOT_SetVerdictFailOrInconc (__FILE__, __LINE__, "Test Case 22.2.1 Step 28");
              }
          }
        }
    }
    
    //Perform TAU procedure on Ncell 11
    f_NBIOT_TrackingAreaUpdate(nbiot_Cell11, CONTROL_PLANE);
    
    //@siclog "Step 34" siclog@
    //Check if PLMN3 is indicated by UE
    f_UT_PLMN_Check(UT, "00131");
    //* @verdict PLMN3 is indicated by UE
    f_NBIOT_PreliminaryPass (__FILE__, __LINE__, "Test Case 22.2.1 Step 34");
    
    f_NBIOT_TestBody_Set(false);
    
    //Switch/power off UE
    f_NBIOT_Postamble(nbiot_Cell11, CONTROL_PLANE, N1_IDLE);
    
  }

  /*
   * @desc      REFERENCE TS 36.523-1 clause 22.2.2
   * @status    APPROVED (NBIOT)
   */
  function f_TC_22_2_2_NBIOT() runs on NBIOT_PTC
  { //NB-IoT / PLMN selection of RPLMN, HPLMN / EHPLMN, UPLMN and OPLMN / Manual mode
    const EUTRA_ASN1_TrackingAreaCode_Type tsc_Idle_TAC_Cell23 := int2bit(23, 16);//@sic R5-174072, R5s170727 sic@
    var template (value) NB_CellPowerList_Type v_CellPowerList_AtT1;
    var template (value) NB_CellPowerList_Type v_CellPowerList_AtT2;
    var template (value) NB_CellPowerList_Type v_CellPowerList_AtT3;
    var template (value) SystemInformationBlockType5_NB_r13 v_SIB5;
    var NB_CarrierFreq_Type v_CarrierFreq_Cell1;
    var NB_CarrierFreq_Type v_CarrierFreq_Cell23;//@sic R5-174072, R5s170727 sic@
    var NB_CarrierFreq_Type v_CarrierFreq_Cell13;
    
    v_CellPowerList_AtT1 := {
      cs_NB_CellPower(nbiot_Cell3, tsc_ServingCellRS_EPRE)//@sic R5-174072, R5s170727 sic@
    };
    v_CellPowerList_AtT2 := {
      cs_NB_CellPower(nbiot_Cell1, tsc_SuitableCellRS_EPRE),
      cs_NB_CellPower(nbiot_Cell3, tsc_NonSuitableOffNBIOTCellRS_EPRE),//@sic R5-174072, R5s170727 sic@
      cs_NB_CellPower(nbiot_Cell23, tsc_ServingCellRS_EPRE),//@sic R5-174072, R5s170727 sic@
      cs_NB_CellPower(nbiot_Cell13, -73)
    };
    v_CellPowerList_AtT3 := {
      cs_NB_CellPower(nbiot_Cell1, tsc_ServingCellRS_EPRE),
      cs_NB_CellPower(nbiot_Cell23, tsc_SuitableCellRS_EPRE),//@sic R5-174072, R5s170727 sic@
      cs_NB_CellPower(nbiot_Cell13, tsc_NonSuitableOffNBIOTCellRS_EPRE)
    };
    
    //Initialise all Ncell, security and mobile parameters
    f_NBIOT_Init(c3);
    
    f_NBIOT_CellInfo_InitMaxReferencePower (nbiot_Cell13, -73); // @sic R5s170490, R5s170819 sic@

    //Set Sys info and Ncell info for Ncell 3, 23 & 13 to be used
    f_NBIOT_CellInfo_SetPLMN_1Entry(nbiot_Cell1, cs_HPLMN_004_31);  //PLMN4
    f_NBIOT_CellInfo_SetPLMN_1Entry(nbiot_Cell3, cs_HPLMN_002_11);  //PLMN2 //@sic R5-174072, R5s170727 sic@
    f_NBIOT_CellInfo_SetPLMN_1Entry(nbiot_Cell23, cs_HPLMN_003_21);  //PLMN3 //@sic R5-174072, R5s170727 sic@
    f_NBIOT_CellInfo_SetPLMN_1Entry(nbiot_Cell13, cs_HPLMN_004_31);  //PLMN4
    
    //Set TAC for Ncell1, Ncell3, Ncell23 & Ncell13
    //TAC for Ncell1 is set to 1 in f_NBIOT_Init
    f_NBIOT_CellInfo_SetTAC(nbiot_Cell3, tsc_Idle_TAC_Cell3);//@sic R5-174072, R5s170727 sic@
    f_NBIOT_CellInfo_SetTAC(nbiot_Cell23, tsc_Idle_TAC_Cell23);//@sic R5-174072, R5s170727 sic@
    f_NBIOT_CellInfo_SetTAC(nbiot_Cell13, tsc_Idle_TAC_Cell13);
    
    //SIB5-NB according to table 22.2.2.3.3-1 and table 22.2.2.3.3-2
    v_CarrierFreq_Cell1 := f_NBIOT_CellInfo_GetEARFCN(nbiot_Cell1);
    v_CarrierFreq_Cell23 := f_NBIOT_CellInfo_GetEARFCN(nbiot_Cell23);//@sic R5-174072, R5s170727 sic@
    v_CarrierFreq_Cell13 := f_NBIOT_CellInfo_GetEARFCN(nbiot_Cell13);
    v_SIB5 := f_NBIOT_CellInfo_GetSIB5(nbiot_Cell1);
    v_SIB5 := cs_508_SystemInformationBlockType5_NB_Def({cs_NB_InterFreqCarrierFreq (v_CarrierFreq_Cell23), cs_NB_InterFreqCarrierFreq (v_CarrierFreq_Cell13)});//@sic R5-174072, R5s170727 sic@
    f_NBIOT_CellInfo_SetSIB5( nbiot_Cell1, v_SIB5);
    
    v_SIB5 := f_NBIOT_CellInfo_GetSIB5(nbiot_Cell23);  //@sic R5-174072, R5s170727 sic@
    v_SIB5 := cs_508_SystemInformationBlockType5_NB_Def({cs_NB_InterFreqCarrierFreq (v_CarrierFreq_Cell1), cs_NB_InterFreqCarrierFreq (v_CarrierFreq_Cell13)});
    f_NBIOT_CellInfo_SetSIB5( nbiot_Cell23, v_SIB5);   //@sic R5-174072, R5s170727 sic@
    
    //Configure and activate all Ncells
    f_NBIOT_CellConfig_Def(nbiot_Cell1,CONTROL_PLANE);
    f_NBIOT_CellConfig_Def(nbiot_Cell3,CONTROL_PLANE);//@sic R5-174072, R5s170727 sic@
    f_NBIOT_CellConfig_Def(nbiot_Cell23,CONTROL_PLANE);//@sic R5-174072, R5s170727 sic@
    f_NBIOT_CellConfig_Def(nbiot_Cell13,CONTROL_PLANE);
    
    //Equip UE with a USIM A containing default values except for those shown in Table 22.2.2.3.1-2
    f_UT_USIM_Insert(UT, "36.523-1 Table 22.2.2.3.1-2");
    
    //switch on
    f_UT_SwitchOnUE(UT, false, CNF_REQUIRED);
    
    // Set UE in manual mode by selecting a PLMN Id. "If the selected operator is not available, no operator shall be selected."
    f_UT_ManualPLMN_Select(UT,
                           f_Asn2Nas_PlmnId (f_NBIOT_CellInfo_GetGutiPLMN (nbiot_Cell1)),
                           -,
                           CNF_REQUIRED);
    
    //switch off
    f_UT_SwitchOffUE(UT, false, CNF_REQUIRED);
    
    //Preamble: The UE is made to camp on Ncell 1 (registered to PLMN4) and then in state Switched OFF (State 1-NB)
    f_NBIOT_Preamble(nbiot_Cell1, CONTROL_PLANE, STATE2_NB_CONNECTED_UPDATED);
    
    f_NBIOT_UE_Detach_SwitchOffUe (nbiot_Cell1, CONTROL_PLANE, RRC_CONNECTED);
    
    //Give correct PLMN identities for Ncell 1 according to Table 22.2.2.3.1-1
    f_NBIOT_CellInfo_SetPLMN_1Entry(nbiot_Cell1, cs_HPLMN_Def); //PLMN1
    f_NBIOT_ModifySysinfoUE_Off (nbiot_Cell1);
    
    f_NBIOT_TestBody_Set(true);
    
    //@siclog "Step 1" siclog@
    //The SS adjusts cell levels according to row T1 of table 22.2.2.3.2-1.
    f_NBIOT_SetCellPowerList(v_CellPowerList_AtT1);
    
    //@siclog "Step 2" siclog@
    //Power on the UE with USIM A inserted.
    f_UT_SwitchOnUE(UT);
    
    //@siclog "Step 3 - 17" siclog@
    //Check: Does the UE transmits an RRCConnectionRequest-NB on Ncell 3?
    f_NBIOT_InitialRegistration(nbiot_Cell3 ,CONTROL_PLANE, NORMAL, STATE3_NB_IDLEUPDATED);//@sic R5-174072, R5s170727 sic@
    f_NBIOT_PreliminaryPass(__FILE__, __LINE__, "Test Case 22.2.2 Step 3");
    
    //@siclog "Step 18" siclog@
    //Check: Is PLMN 2 indicated by the UE?
    f_UT_PLMN_Check(UT, "00211");//PLMN2
    f_NBIOT_PreliminaryPass(__FILE__, __LINE__, "Test Case 22.2.2 Step 18");
    
    //@siclog "Step 19 - 20a1 - 20a4 siclog@
    //If possible switch off is performed or the USIM is removed, otherwise the power is removed.
    //If pc_SwitchOnOff or pc_USIM_Removal then switch off procedure defined in TS 36.523-3 Table 10.5.2.1 Steps 2a1-2a4 is performed.
    f_NBIOT_UE_Detach_SwitchOffUe(nbiot_Cell3, CONTROL_PLANE, RRC_IDLE);//@sic R5-174072, R5s170727 sic@
    
    // Reset Security at NAS EMU
    f_NBIOT_SS_NAS_SecurityRelease();
    
    //@siclog "Step 21" siclog@
    //The UE is brought back to operation with USIM B inserted.
    f_UT_USIM_Insert(UT, "36.523-1 Table 22.2.2.3.1-3");
    
    f_UT_SwitchOnUE(UT);
    
    //@siclog "Step 22 - 36" siclog@
    //Check: Does the UE transmits an RRCConnectionRequest-NB on Ncell 1?
    fl_NBIOT_InitialRegistration_EPLMN(nbiot_Cell1 ,CONTROL_PLANE, PREAMBLE, STATE3_NB_IDLEUPDATED, cs_PLMN_List_1PLMN (f_Asn2Nas_PlmnId (f_NBIOT_CellInfo_GetGutiPLMN (nbiot_Cell23))));//@sic R5-174072, R5s170727 sic@
    f_NBIOT_PreliminaryPass(__FILE__, __LINE__, "Test Case 22.2.2 Step 22");
    
    //@siclog "Step 37" siclog@
    //Check: Is PLMN 1 indicated by the UE?
    f_UT_PLMN_Check(UT, "00101");//PLMN1
    f_NBIOT_PreliminaryPass(__FILE__, __LINE__, "Test Case 22.2.2 Step 37");
    
    //@siclog "Step 38" siclog@
    //The SS adjusts Ncell levels according to row T2 of table 22.2.2.3.2-1.
    f_NBIOT_SetCellPowerList(v_CellPowerList_AtT2);
    
    //@siclog "Step 39 - 44" siclog@
    //Check: Does the UE transmits an RRCConnectionRequest-NB on Ncell 23?
    f_NBIOT_TrackingAreaUpdate_Step1_5(nbiot_Cell23, CONTROL_PLANE, ?, cs_PLMN_List_1PLMN (f_Asn2Nas_PlmnId (f_NBIOT_CellInfo_GetGutiPLMN (nbiot_Cell1))));//@sic R5-174072, R5s170727 sic@
    f_NBIOT_PreliminaryPass (__FILE__, __LINE__, "Test Case 22.2.2 Step 39");
    
    //@siclog "Step 45" siclog@
    f_NBIOT_RRC_ConnectionRelease(nbiot_Cell23);//@sic R5-174072, R5s170727 sic@
    
    //@siclog "Step 46" siclog@
    //Check: Is PLMN 3 indicated by the UE?
    f_UT_PLMN_Check(UT, "00321");//PLMN3
    f_NBIOT_PreliminaryPass(__FILE__, __LINE__, "Test Case 22.2.2 Step 46");
    
    //@siclog "Step 47" siclog@
    //Check for no RRCConnectionRequest-NB message on Ncell 1 or Ncell 13
    if (f_NBIOT_RRC_RRCConnectionRequest_Check((nbiot_Cell1, nbiot_Cell13), 120.0)) {
      //* @verdict fail UE sent RRCConnectionRequest-NB message on Ncell 1 or Ncell 13 within 120s
      f_NBIOT_SetVerdictFailOrInconc (__FILE__, __LINE__, "Test Case 22.2.2 Step 47");
    }
    
    //@siclog "Step 48" siclog@
    //The SS adjusts Ncell levels according to row T3 of table 22.2.2.3.2-1.
    f_NBIOT_SetCellPowerList(v_CellPowerList_AtT3);
    
    //@siclog "Step 49" siclog@
    //Set UE to Automatic PLMN selection mode.
    f_UT_AutomaticPLMN_Select(UT);
    
    //@siclog "Step 50" siclog@
    //Check if UE camps on Ncell 1
    f_NBIOT_508CheckCampOnNewNBIOTCell (nbiot_Cell1, CONTROL_PLANE);
    f_NBIOT_PreliminaryPass (__FILE__, __LINE__, "Test Case 22.2.2 Step 50");
    
    f_NBIOT_TestBody_Set(false);
    
    //Switch/Power off UE
    f_NBIOT_Postamble(nbiot_Cell1, CONTROL_PLANE, N1_IDLE);
  }

  /*
   * @desc      REFERENCE TS 36.523-1 clause 22.2.3
   * @status    APPROVED (NBIOT)
   */
  function f_TC_22_2_3_NBIOT() runs on NBIOT_PTC
  { // NB-IoT / PLMN selection / Periodic reselection / MinimumPeriodicSearchTimer
    // @sic R5-171951 R5s170492 R5s170783 sic@
    var template (value) NB_CellPowerList_Type v_CellPowerList_AtT1;
    var template (value) NB_CellPowerList_Type v_CellPowerList_AtT2;
    
    timer t_HpPlmn_lower;
    timer t_HpPlmn_upper;
    
    v_CellPowerList_AtT1 := {
      cs_NB_CellPower(nbiot_Cell12, tsc_ServingCellRS_EPRE),
      cs_NB_CellPower(nbiot_Cell13, tsc_ServingCellRS_EPRE)
    };
    v_CellPowerList_AtT2 := {
      cs_NB_CellPower(nbiot_Cell1, tsc_ServingCellRS_EPRE),
      cs_NB_CellPower(nbiot_Cell12, tsc_ServingCellRS_EPRE),
      cs_NB_CellPower(nbiot_Cell13, tsc_ServingCellRS_EPRE)
    };
    
    //Initialise all cell, security and mobile parameters
    f_NBIOT_Init(c3);
    
    //Set Sys info and cell info for cell 1,12,13 to be used @ creation
    f_NBIOT_CellInfo_SetPLMN_1Entry(nbiot_Cell1, cs_HPLMN_Def);  //PLMN4
    f_NBIOT_CellInfo_SetPLMN_1Entry(nbiot_Cell12, cs_HPLMN_001_11);  //PLMN1
    f_NBIOT_CellInfo_SetPLMN_1Entry(nbiot_Cell13, cs_HPLMN_001_21);  //PLMN2
    
    //Set TAC according to 36.508 Table 8.1.4.2-6: Tracking Area Code (TAC) for NBIOT cells */
    f_NBIOT_CellInfo_SetTAC(nbiot_Cell12, tsc_Idle_TAC_Cell12);
    f_NBIOT_CellInfo_SetTAC(nbiot_Cell13, tsc_Idle_TAC_Cell13);
    
    //Configure and activate all cells
    //Cell power level will be -85 dBm/15kHz & attenuation set so that cell is seen as OFF
    f_NBIOT_CellConfig_Def(nbiot_Cell1, CONTROL_PLANE);
    f_NBIOT_CellConfig_Def(nbiot_Cell12, CONTROL_PLANE);
    f_NBIOT_CellConfig_Def(nbiot_Cell13, CONTROL_PLANE);
    
    //Equip UE with a USIM containing default values except for those shown in Table 22.2.3.3.1-2: USIM configuration
    f_UT_USIM_Insert(UT, "36.523-1 Table 22.2.3.3.1-2");
    //Make sure the UE is in automatic PLMN selection mode
    f_UT_AutomaticPLMN_Select_WithSwitchOnOff (UT);    //@sic R5s180214 sic@

    //Preamble to ensure UE is in known state - PLMN1
    f_NBIOT_Preamble(nbiot_Cell12, CONTROL_PLANE, STATE2_NB_CONNECTED_UPDATED);
    f_NBIOT_UE_Detach_SwitchOffUe(nbiot_Cell12, CONTROL_PLANE, RRC_CONNECTED);
    
    f_NBIOT_TestBody_Set(true);
    
    //@siclog "Step 1" siclog@
    // SS adjusts cell levels according to row T1 of table 22.2.3.3.2-1
    f_NBIOT_SetCellPowerList(v_CellPowerList_AtT1);
    
    //@siclog "Step 2" siclog@
    //Power on the UE
    t_HpPlmn_lower.start(f_NBIOT_SetTimerToleranceMin(nasTimer, 120.0));
    f_UT_SwitchOnUE(UT, false);
    t_HpPlmn_upper.start(f_NBIOT_SetTimerToleranceMax(nasTimer, 7200.0));
    
    //@siclog "Step 3-15b1" siclog@
    // Steps 2 to 14b1 of the registration procedure described in TS 36.508 subclause 8.1.5.2 are performed on Ncell 12
    f_NBIOT_InitialRegistration(nbiot_Cell12, CONTROL_PLANE, PREAMBLE, STATE2_NB_CONNECTED_UPDATED);
    
    //@siclog "Step 16" siclog@
    // The SS transmits an RRCConnectionRelease-NB message to release RRC connection and move to RRC_IDLE
    f_NBIOT_RRC_ConnectionRelease (nbiot_Cell12);
    
    //@siclog "Step 17" siclog@
    // Check: Is PLMN1 indicated by the UE?
    f_UT_PLMN_Check(UT, "00111");
    f_NBIOT_PreliminaryPass(__FILE__, __LINE__, "Test Case 22.2.3 Step 17");
    
    //@siclog "Step 18" siclog@
    // Check: Does the UE send an RRCConnectionRequest-NB on Ncell 13 after 120 seconds, but before [120mins] (Note 1) from power on?
    alt {
      [] SRB.check(receive(car_NB_SRB0_RrcPdu_IND(nbiot_Cell13, cr_RRCConnectionRequest_NB(cr_EstablishmentCause_NB_Any, ?))))  // @sic R5-174462 sic@
        {
          //* @verdict fail RRCConnectionRequest received on nbiot_Cell1 within t_HpPlmn_lower time
          f_NBIOT_SetVerdictFailOrInconc(__FILE__, __LINE__, "Test Case 22.2.3 Step 18");
        }
      [] t_HpPlmn_lower.timeout
        {
            alt{
              [] SRB.check(receive(car_NB_SRB0_RrcPdu_IND(nbiot_Cell13, cr_RRCConnectionRequest_NB(cr_EstablishmentCause_NB_Any, ?))))
             {
              //* @verdict pass RRC Connection request received on nbiot_Cell1 after t_HpPlmn_lower time but before t_HpPlmn_upper
                f_NBIOT_PreliminaryPass(__FILE__, __LINE__, "Test Case 22.2.3 Step 18");
             }

          [] t_HpPlmn_upper.timeout
               {
                //* @verdict fail No RRCConnectionRequest received
                f_NBIOT_SetVerdictFailOrInconc(__FILE__, __LINE__, "Test Case 22.2.3 Step 18");
              }
            }
        }
    }
    t_HpPlmn_upper.start(f_NBIOT_SetTimerToleranceMin(nasTimer, 7200.0));
    
    //@siclog "Step 19 - 23" siclog@
    //Steps 2 to 6 of the generic test procedure in TS 36.508 subclause 8.1.5A.5 are performed on Ncell 13
    // NOTE: The UE performs a TAU procedure and the RRC connection is released.
    f_NBIOT_TrackingAreaUpdate (nbiot_Cell13, CONTROL_PLANE);

    //@siclog "Step 24" siclog@
    // Check: Is PLMN2 indicated by the UE?
    f_UT_PLMN_Check(UT, "00121");
    f_NBIOT_PreliminaryPass(__FILE__, __LINE__, "Test Case 22.2.3 Step 24");

    //@siclog "Step 25" siclog@
    // SS adjusts cell levels according to row T2 of table 22.2.3.3.2-1
    f_NBIOT_SetCellPowerList(v_CellPowerList_AtT2);
    
    //@siclog "Step 26" siclog@
    //Check: Does the UE send an RRCConnectionRequest-NB on Ncell 1 after [120 mins] from step 18
    alt {
      [] SRB.receive( car_NB_SRB0_RrcPdu_IND(nbiot_Cell1, cr_508_RRCConnectionRequest_NB))
        {
          //* @verdict fail RRCConnectionRequest received on Ncell 1 within t_HpPlmn_Upper time
          f_NBIOT_SetVerdictFailOrInconc(__FILE__, __LINE__, "Test Case 22.2.3 Step 26");
        }
      [] t_HpPlmn_upper.timeout
        {
          //@siclog "Step 26 - 31" siclog@
          // Steps 2 to 6 of the generic test procedure in TS 36.508 subclause 8.1.5A.5 are performed.
          // NOTE: The UE performs a TAU procedure and the RRC connection is released.
          f_NBIOT_TrackingAreaUpdate (nbiot_Cell1, CONTROL_PLANE);
          //* @verdict pass RRC Connection request received on Ncell 1 after t_HpPlmn_Upper time
          f_NBIOT_PreliminaryPass(__FILE__, __LINE__, "Test Case 22.2.3 Step 26");
        }
    }
    
    //@siclog "Step 32" siclog@
    //Check if PLMN4 is indicated by UE
    f_UT_PLMN_Check(UT, "00101");
    f_NBIOT_PreliminaryPass(__FILE__, __LINE__, "Test Case 22.2.3 Step 32");
    
    f_NBIOT_TestBody_Set(false);
    
    //Switch/power off UE
    f_NBIOT_Postamble(nbiot_Cell1, CONTROL_PLANE, N1_IDLE);
  }

  /*
   * @desc      REFERENCE TS 36.523-3 clause 22.2.4
   * @status    APPROVED (NBIOT)
   */
  function f_TC_22_2_4_NBIOT() runs on NBIOT_PTC
  { /*NB-IoT / Cell selection / Qrxlevmin and Qqualmin / Serving cell becomes non-suitable (S<0 or barred or Srxlev > 0 and Squal < 0)*/
    // @sic R5-181161 sic@
    var float v_TimerValue := 60.0;
    var NB_T_Reselection_Type v_T_Reselection := s21;
    var NB_CellBarred_Type v_CellBarred := barred;
    var NB_CellBarred_Type v_CellNotBarred := notBarred;
    var NB_IntraFreqRes_Type v_IntraFreqRes := allowed;
    var NB_Q_Hyst_Type v_Q_Hyst := dB0;
    var template (value) NB_CellPowerList_Type v_CellPowerList_AtT1T2;
    var template (value) NB_CellPowerList_Type v_CellPowerList_AtT3;
    var template (value) NB_CellPowerList_Type v_CellPowerList_AtT4;
    var template (value) NB_CellPowerList_Type v_CellPowerList_AtT5;
    var template (value) NB_CellPowerList_Type v_CellPowerList_AtT6;
    var SystemInformationBlockType1_NB v_SIB1;
    var SystemInformationBlockType3_NB_r13 v_SIB3;
    var float v_ModificationPeriod_s;
    
    v_CellPowerList_AtT1T2 := {
      cs_NB_CellPower(nbiot_Cell1, -95),
      cs_NB_CellPower(nbiot_Cell2, tsc_NonSuitableOffNBIOTCellRS_EPRE)
    };
    v_CellPowerList_AtT3 := {
      cs_NB_CellPower(nbiot_Cell1, -65)
    };
    v_CellPowerList_AtT4 := {
      cs_NB_CellPower(nbiot_Cell1, tsc_NonSuitableOffNBIOTCellRS_EPRE),
      cs_NB_CellPower(nbiot_Cell2, -85)
    };
    v_CellPowerList_AtT5 := {
      cs_NB_CellPower(nbiot_Cell3, -91)
    };
    v_CellPowerList_AtT6 := {
      cs_NB_CellPower(nbiot_Cell3, -97)
    };

    //Initialise all Ncells, security and mobile parameters
    f_NBIOT_Init(c2);

    //Set TAC according to 36.508 Table 8.1.4.2-6: Tracking Area Code (TAC) for NBIOT cells */
    f_NBIOT_CellInfo_SetTAC(nbiot_Cell2, tsc_Idle_TAC_Cell2);

    //Set maximum cell power level for NCell 1 to be used in creation
    f_NBIOT_CellInfo_InitMaxReferencePower (nbiot_Cell1, -65);

    //Create and configure NCell 1 & 2 & 3
    f_NBIOT_CellConfig_Def(nbiot_Cell1, CONTROL_PLANE);
    f_NBIOT_CellConfig_Def(nbiot_Cell2, CONTROL_PLANE);
    f_NBIOT_CellConfig_Def(nbiot_Cell3, CONTROL_PLANE);
    f_NBIOT_ConfigureVNG (nbiot_Cell1, -75);
    f_NBIOT_ConfigureVNG (nbiot_Cell3, -75);
    
    //Perform Idle updated procedure to make sure UE is in a known state before test case starts
    f_NBIOT_Preamble (nbiot_Cell1, CONTROL_PLANE, STATE2_NB_CONNECTED_UPDATED);
    
    //Bring UE to initial state
    f_NBIOT_UE_Detach_SwitchOffUe (nbiot_Cell1, CONTROL_PLANE, RRC_CONNECTED);

    f_NBIOT_TestBody_Set (true);

    //@siclog "Step 0A-0B" siclog@
    //Set SIB1-NB according to Table 22.2.4.3.3-1
    v_SIB1 := f_NBIOT_CellInfo_GetSIB1(nbiot_Cell1);
    v_SIB1.cellSelectionInfo_r13.q_RxLevMin_r13 := -42;
    f_NBIOT_CellInfo_SetSIB1( nbiot_Cell1, v_SIB1);

    v_ModificationPeriod_s := int2float(f_NB_CalculateModificationPeriod (nbiot_Cell1))/100.0; // Modification period in second
    f_NBIOT_ModifySysinfoUE_Off(nbiot_Cell1);
    //Wait the duration of 1.1 modification periods to ensure new SYSINFO takes effect
    f_Delay (1.1 * v_ModificationPeriod_s);
    f_NBIOT_SetCellPowerList (v_CellPowerList_AtT1T2);
    
    //@siclog "Step 1" siclog@
    f_UT_SwitchOnUE(UT);

    //@siclog "Step 2" siclog@
    //Check for no RRCConnectionRequest-NB message on Ncell 1
    if (f_NBIOT_RRC_RRCConnectionRequest_Check(nbiot_Cell1, v_TimerValue)) {
      //* @verdict fail UE sent RRCConnectionRequest-NB message on Ncell 1 within v_TimerValue
      f_NBIOT_SetVerdictFailOrInconc (__FILE__, __LINE__, "Test Case 22.2.4 Step 2");
    }

    //@siclog "Step 3-3A" siclog@
    //Change Ncell settings according to T2
    v_SIB1 := f_NBIOT_CellInfo_GetSIB1(nbiot_Cell1);
    v_SIB1.cellSelectionInfo_r13.q_QualMin_r13 := -18;
    v_SIB1.cellSelectionInfo_r13.q_RxLevMin_r13 := -53;
    f_NBIOT_CellInfo_SetSIB1( nbiot_Cell1, v_SIB1);

    v_ModificationPeriod_s := int2float(f_NB_CalculateModificationPeriod (nbiot_Cell1))/100.0; // Modification period in second
    // Activate White Noise
    f_NBIOT_ActivateVNG (nbiot_Cell1);
    f_NBIOT_ModifySysinfoUE_Off(nbiot_Cell1);
    //Wait the duration of 1.1 modification periods to ensure new SYSINFO takes effect
    f_Delay (1.1 * v_ModificationPeriod_s);
    f_NBIOT_SetCellPowerList (v_CellPowerList_AtT1T2);

    //@siclog "Step 4" siclog@
    //Check for no RRCConnectionRequest-NB message on Ncell 1
    if (f_NBIOT_RRC_RRCConnectionRequest_Check(nbiot_Cell1, v_TimerValue)) {
      //* @verdict fail UE sent RRCConnectionRequest-NB message on Ncell 1 within v_TimerValue
      f_NBIOT_SetVerdictFailOrInconc (__FILE__, __LINE__, "Test Case 22.2.4 Step 4");
    }

    //@siclog "Step 5" siclog@
    //Change Ncell settings according to T3
    f_NBIOT_SetCellPowerList (v_CellPowerList_AtT3);

    //@siclog "Step 6-18" siclog@
    f_NBIOT_InitialRegistration(nbiot_Cell1 ,CONTROL_PLANE, PREAMBLE);
    f_NBIOT_PreliminaryPass (__FILE__, __LINE__, "Test Case 22.2.4 Step 6");

    //@siclog "Step 18A" siclog@
    f_NBIOT_RRC_ConnectionRelease(nbiot_Cell1);

    // deactivate White Noise
    f_NBIOT_DeactivateVNG (nbiot_Cell1);

    //@siclog "Step 19" siclog@
    //Change Ncell settings according to T4
    f_NBIOT_SetCellPowerList (v_CellPowerList_AtT4);

    //SIB1-NB according to Table 22.2.4.3.3-3
    v_SIB1 := f_NBIOT_CellInfo_GetSIB1(nbiot_Cell1);
    v_SIB1.cellSelectionInfo_r13.q_RxLevMin_r13 := -55;
    f_NBIOT_CellInfo_SetSIB1( nbiot_Cell1, v_SIB1);

    v_SIB1 := f_NBIOT_CellInfo_GetSIB1(nbiot_Cell2);
    v_SIB1.cellSelectionInfo_r13.q_RxLevMin_r13 := -55;
    f_NBIOT_CellInfo_SetSIB1( nbiot_Cell2, v_SIB1);

    //SIB3-NB according to Table 22.2.4.3.3-4
    v_SIB3 := f_NBIOT_CellInfo_GetSIB3(nbiot_Cell1);
    v_SIB3.cellReselectionInfoCommon_r13.q_Hyst_r13 := v_Q_Hyst;
    v_SIB3.intraFreqCellReselectionInfo_r13.q_RxLevMin_r13 := -55;
    v_SIB3.intraFreqCellReselectionInfo_r13.t_Reselection_r13 := v_T_Reselection;
    f_NBIOT_CellInfo_SetSIB3 (nbiot_Cell1, v_SIB3);

    v_SIB3 := f_NBIOT_CellInfo_GetSIB3(nbiot_Cell2);
    v_SIB3.cellReselectionInfoCommon_r13.q_Hyst_r13 := v_Q_Hyst;
    v_SIB3.intraFreqCellReselectionInfo_r13.q_RxLevMin_r13 := -55;
    v_SIB3.intraFreqCellReselectionInfo_r13.t_Reselection_r13 := v_T_Reselection;
    f_NBIOT_CellInfo_SetSIB3 (nbiot_Cell2, v_SIB3);

    f_NBIOT_ModifySysinfo (nbiot_Cell1, false);

    //@siclog "Step 20" siclog@
    //Check if UE camps on Ncell 2
    f_NBIOT_508CheckCampOnNewNBIOTCell (nbiot_Cell2, CONTROL_PLANE);
    f_NBIOT_PreliminaryPass (__FILE__, __LINE__, "Test Case 22.2.4 Step 20");

    //@siclog "Step 21" siclog@
    //Change Ncell settings according to T5
    f_NBIOT_SetCellPowerList (v_CellPowerList_AtT5);
    f_NBIOT_CellInfo_SetSysInfo_CellBarred(nbiot_Cell2, v_CellBarred);
    f_NBIOT_CellInfo_SetIntraFreqReselection(nbiot_Cell2, v_IntraFreqRes);
    f_NBIOT_ModifySysinfo(nbiot_Cell2, true);

    //@siclog "Step 22" siclog@
    //Check if UE camps on Ncell 3
    f_NBIOT_508CheckCampOnNewNBIOTCell (nbiot_Cell3, CONTROL_PLANE);
    f_NBIOT_PreliminaryPass (__FILE__, __LINE__, "Test Case 22.2.4 Step 22");

    //@siclog "Step 23" siclog@
    //Change Ncell settings according to T6
    f_NBIOT_SetCellPowerList (v_CellPowerList_AtT6);
    f_NBIOT_CellInfo_SetSysInfo_CellBarred(nbiot_Cell2, v_CellNotBarred);
    f_NBIOT_CellInfo_SetIntraFreqReselection(nbiot_Cell2, notAllowed);

    v_SIB1 := f_NBIOT_CellInfo_GetSIB1(nbiot_Cell2);
    v_SIB1.cellSelectionInfo_r13.q_RxLevMin_r13 := -46;
    v_SIB1.cellSelectionInfo_r13.q_QualMin_r13 := -20;
    f_NBIOT_CellInfo_SetSIB1( nbiot_Cell2, v_SIB1);

    v_SIB3 := f_NBIOT_CellInfo_GetSIB3(nbiot_Cell2);
    v_SIB3.cellReselectionInfoCommon_r13.q_Hyst_r13 := v_Q_Hyst;
    v_SIB3.intraFreqCellReselectionInfo_r13.q_RxLevMin_r13 := -46;
    v_SIB3.intraFreqCellReselectionInfo_r13.q_QualMin_r13 := -20;
    v_SIB3.intraFreqCellReselectionInfo_r13.t_Reselection_r13 := v_T_Reselection;
    f_NBIOT_CellInfo_SetSIB3 (nbiot_Cell2, v_SIB3);

    f_NBIOT_ActivateVNG (nbiot_Cell3);
    f_NBIOT_ModifySysinfo(nbiot_Cell2, false); // @sic R5s170763 sic@

    //@siclog "Step 24" siclog@
    //Check if UE camps on Ncell 2
    f_NBIOT_508CheckCampOnNewNBIOTCell (nbiot_Cell2, CONTROL_PLANE);
    f_NBIOT_PreliminaryPass (__FILE__, __LINE__, "Test Case 22.2.4 Step 24");

    // deactivate White Noise
    f_NBIOT_DeactivateVNG (nbiot_Cell3);

    f_NBIOT_TestBody_Set(false);

    //Switch/power off UE
    f_NBIOT_Postamble(nbiot_Cell2, CONTROL_PLANE, N1_IDLE);
  }

}
