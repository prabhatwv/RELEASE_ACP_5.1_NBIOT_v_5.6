/******************************************************************************/
// @copyright   Copyright Notification
//   No part may be reproduced except as authorized by written permission.
//   The copyright and the foregoing restriction extend to reproduction in all media.
//   Trademark 2019, 3GPP Organizational Partners (ARIB, ATIS, CCSA, ETSI, TSDSI, TTA, TTC).
//   All rights reserved.
// @version: 36.523-3 v15.3.0
// $Date: 2019-02-10 14:13:46 +0100 (Sun, 10 Feb 2019) $
// $Rev: 23731 $
/******************************************************************************/

module NBIOT_RRC_Templates {

  import from NBIOT_RRC_ASN1_Definitions language "ASN.1:2002" all with {encode "UNALIGNED_PER_OctetAligned"};
  import from CommonDefs all;
  import from EUTRA_NB_CommonDefs all;
  import from EUTRA_NB_Parameters all;
  import from EUTRA_NB_RRC_Templates all;
  import from NBIOT_CommonDefs all;
  import from NBIOT_Imported_EUTRA_ASN1_Types all;

  const integer tsc_NBIOT_SRB1bis_LogicalChannelPriority := 1;  /* @status    APPROVED (NBIOT) */

  const integer tsc_NBIOT_SRB1_LogicalChannelPriority := 1;     /* @status    APPROVED (NBIOT) */

  const integer tsc_NBIOT_DRB_LogicalChannelPriority := 13;     /* @status    APPROVED (NBIOT) */

  const bitstring tsc_NBIOT_SupportedBands :=  /* @status    APPROVED (NBIOT) */
                                 '1'B &
                                 '1'B &
                                 '1'B &
                                 '0'B &//Band 4 is reserved
                                 '1'B &
                                 '0'B &//Band 6 is reserved
                                 '0'B &//Band 7 is reserved
                                 '1'B &
                                 '0'B &//Band 9 is reserved
                                 '0'B &//Band 10 is reserved
                                 '1'B & // @sic R5-175124 sic@
                                 '1'B &
                                 '1'B &
                                 '0'B &//Band 14 is reserved
                                 '0'B &//Band 15 is reserved
                                 '0'B &//Band 16 is reserved
                                 '1'B &
                                 '1'B &
                                 '1'B &
                                 '1'B &
                                 '1'B & // @sic R5-175124 sic@
                                 '0'B &//Band 22 is reserved
                                 '0'B &//Band 23 is reserved
                                 '0'B &//Band 24 is reserved
                                 '1'B & // @sic R5-175124 sic@
                                 '1'B &
                                 '0'B &//Band 27 is reserved
                                 '1'B &
                                 '0'B &//Band 29 is reserved
                                 '0'B &//Band 30 is reserved
                                 '1'B; // @sic R5-175124 sic@

  template (value) UplinkPowerControlDedicated_NB_r13 cs_UplinkPowerControlDedicated_NB_DEFAULT :=
  { // 36.508 Table 8.1.6.3-15
    /* @status    APPROVED (NBIOT) */
    p0_UE_NPUSCH_r13 := 0
  };

  template (present) UL_CCCH_Message_NB cr_RRCConnectionRequest_NB(template (present) EstablishmentCause_NB_r13 p_EstablishmentCause,
                                                                   template EUTRA_ASN1_InitialUE_Identity_Type p_InitialUE_Identity := cr_InitialUE_Identity_S_TMSIAny) :=
  { // 36.508 Table 8.1.6.1-10
    /* @status    APPROVED (NBIOT) */
    message_ := {
      c1 := {
        rrcConnectionRequest_r13 := {
          criticalExtensions := {
            rrcConnectionRequest_r13 := {
              ue_Identity_r13 := p_InitialUE_Identity,
              establishmentCause_r13 := p_EstablishmentCause,
              multiToneSupport_r13 := *,
              multiCarrierSupport_r13 := *,
              earlyContentionResolution_r14 := ?,   /* @sic R5s180412 BASELINE MOVING 2018-2, R5s180552 BASELINE MOVING 2018: cqi_NPDCCH_r14 and earlyContentionResolution_r14 swapped sic@ */
              cqi_NPDCCH_r14                := ?,   /* @sic R5s180412 BASELINE MOVING 2018-2, R5s180552 BASELINE MOVING 2018: cqi_NPDCCH_r14 and earlyContentionResolution_r14 swapped sic@ */
              spare := ?
            }
          }
        }
      }
    }
  };

  template (present) UL_CCCH_Message_NB cr_508_RRCConnectionRequest_NB := cr_RRCConnectionRequest_NB(cr_EstablishmentCause_NB_Any); /* @status    APPROVED (NBIOT) */

  template (present) EstablishmentCause_NB_r13 cr_EstablishmentCause_NB_Any := ?;  /* @status    APPROVED (NBIOT) */

  template (present) EUTRA_ASN1_InitialUE_Identity_Type cr_InitialUE_Identity_S_TMSIAny :=
  { /* @status    APPROVED (NBIOT) */
    s_TMSI := {
      mmec := ?,
      m_TMSI := ?
    }
  };

  template (value) DL_CCCH_Message_NB cs_508_RRCConnectionSetup_NB(EUTRA_ASN1_RRC_TransactionIdentifier_Type p_RRC_TI := tsc_RRC_TI_Def,
                                                                   template (value) SRB_ToAddModList_NB_r13 p_SRBList := { cs_SRB_ToAddMod_NB_DEFAULT },
                                                                   template (value) MAC_MainConfig_NB_r13 p_MAC_MainConfig := cs_MAC_MainConfig_NB_SRB,
                                                                   template (value) NPDCCH_ConfigDedicated_NB_r13 p_NPDCCH_ConfigDedicated_NB,
                                                                   template (value) NPUSCH_ConfigDedicated_NB_r13 p_NPUSCH_ConfigDedicated_NB,
                                                                   template (value) UplinkPowerControlDedicated_NB_r13 p_UplinkPowerControlDedicated_NB,
                                                                   template (omit)  PhysicalConfigDedicated_NB_r13.twoHARQ_ProcessesConfig_r14 p_TwoHARQ_ProcessesConfig_r14 := omit) :=
  { // 36.508 Table 8.1.6.1-14
    /* @status    APPROVED (NBIOT) */
    message_ := {
      c1 := {
        rrcConnectionSetup_r13 := {
          rrc_TransactionIdentifier := p_RRC_TI,
          criticalExtensions := {
            c1 := {
              rrcConnectionSetup_r13 := {
                radioResourceConfigDedicated_r13 := cs_RadioResourceConfigDedicated_NB_SRB(p_SRBList, p_MAC_MainConfig, p_NPDCCH_ConfigDedicated_NB, p_NPUSCH_ConfigDedicated_NB, p_UplinkPowerControlDedicated_NB, p_TwoHARQ_ProcessesConfig_r14),
                lateNonCriticalExtension := omit,
                nonCriticalExtension := omit
              }
            }
          }
        }
      }
    }
  };

  template (value) RadioResourceConfigDedicated_NB_r13 cs_RadioResourceConfigDedicated_NB_SRB(template (value) SRB_ToAddModList_NB_r13 p_SRBList := { cs_SRB_ToAddMod_NB_DEFAULT },
                                                                                              template (value) MAC_MainConfig_NB_r13 p_MAC_MainConfig := cs_MAC_MainConfig_NB_SRB,
                                                                                              template (value) NPDCCH_ConfigDedicated_NB_r13 p_NPDCCH_ConfigDedicated_NB,
                                                                                              template (value) NPUSCH_ConfigDedicated_NB_r13 p_NPUSCH_ConfigDedicated_NB,
                                                                                              template (value) UplinkPowerControlDedicated_NB_r13 p_UplinkPowerControlDedicated_NB,
                                                                                              template (omit)  PhysicalConfigDedicated_NB_r13.twoHARQ_ProcessesConfig_r14 p_TwoHARQ_ProcessesConfig_r14 := omit):=
  { // 36.508 Table 8.1.6.3-10
    /* @status    APPROVED (NBIOT) */
    srb_ToAddModList_r13 := p_SRBList,
    drb_ToAddModList_r13 := omit,
    drb_ToReleaseList_r13 := omit,
    mac_MainConfig_r13 := {
      explicitValue_r13 := p_MAC_MainConfig
    },
    physicalConfigDedicated_r13 := cs_PhysicalConfigDedicated_NB_DEFAULT(p_NPDCCH_ConfigDedicated_NB, p_NPUSCH_ConfigDedicated_NB, p_UplinkPowerControlDedicated_NB, p_TwoHARQ_ProcessesConfig_r14),
    rlf_TimersAndConstants_r13 := omit,
    schedulingRequestConfig_r15 := omit           /* @sic R5s180552 BASELINE MOVING 2018 sic@ */
  };

  template (value) RLC_Config_NB_v1430 cs_RLC_Config_NB_v1430(template (omit) RLC_Config_NB_v1430.t_Reordering_r14 p_T_Reordering_r14 := ms1600_v1310) :=
  { /* @status    APPROVED (NBIOT) */
    t_Reordering_r14 := p_T_Reordering_r14
  };

  template (value) SRB_ToAddMod_NB_r13 cs_SRB_ToAddMod_NB_DEFAULT(template (omit) RLC_Config_NB_v1430 p_RLC_Config_v1430 := omit) :=
  { // 36.508 Table 8.1.8.2.1.1-1
    /* @status    APPROVED (NBIOT) */
    /* @sic R5-188200: p_RLC_Config_v1430 sic@ */
    rlc_Config_r13 := {
      defaultValue := NULL
    },
    logicalChannelConfig_r13 := {
      defaultValue := NULL
    },
    rlc_Config_v1430 := p_RLC_Config_v1430                      /* @sic R5s170597 BASELINE MOVING 2017 sic@ */
  };

  template (value) SRB_ToAddMod_NB_r13 cs_SRB_ToAddMod_NB_RLC_Config(template (value) UL_AM_RLC_NB_r13 p_UL_AM_RLC_NB_r13,
                                                                     template (omit) DL_AM_RLC_NB_r13 p_DL_AM_RLC_NB_r13,
                                                                     template (omit) RLC_Config_NB_v1430 p_RLC_Config_v1430 := omit) :=
  { /* @status    APPROVED (NBIOT) */
    rlc_Config_r13 := {
      explicitValue := {
        am  := {
          ul_AM_RLC_r13 := p_UL_AM_RLC_NB_r13,
          dl_AM_RLC_r13 := p_DL_AM_RLC_NB_r13
        }
      }
    },
    logicalChannelConfig_r13 := {
      defaultValue := NULL
    },
    rlc_Config_v1430 := p_RLC_Config_v1430
  };

  template (value) MAC_MainConfig_NB_r13 cs_MAC_MainConfig_NB_SRB :=
  { // 36.508 Table 8.1.8.2.1.5-1
    /* @status    APPROVED (NBIOT) */
    ul_SCH_Config_r13 := {
      periodicBSR_Timer_r13 := pp4,    // VZW NBIOT: Changing from pp8 to pp4
      retxBSR_Timer_r13 := pp64   // VZW NBIOT: Changing from infinity to pp64
    },
   /* drx_Config_r13 := {
      release := NULL
    },*/
      /*drx_Config_r13 :=  {setup := {
      onDurationTimer_r13 := pp4,
      drx_InactivityTimer_r13 := pp4,
      drx_RetransmissionTimer_r13 := pp6,
      drx_Cycle_r13 := sf4096,
      drx_StartOffset_r13 := 0,
      drx_ULRetransmissionTimer_r13 := pp24
    }}*/drx_Config_r13 :=omit, // VZW NBIOT: Changing from NULL to omit
    timeAlignmentTimerDedicated_r13 := infinity_,
    /*logicalChannelSR_Config_r13 := {
      setup := {
        logicalChannelSR_ProhibitTimer_r13 := pp2048
      }
    },*/
      logicalChannelSR_Config_r13 := omit, // VZW NBIOT: Changing from setup to omit
    rai_Activation_r14 := omit,                   /* @sic R5s170597 BASELINE MOVING 2017 sic@ */
    dataInactivityTimerConfig_r14 := omit,        /* @sic R5s170597 BASELINE MOVING 2017 sic@ */
    drx_Cycle_v1430 := omit,                      /* @sic R5s170597 BASELINE MOVING 2017 sic@ */
    ra_CFRA_Config_r14 := omit                    /* @sic R5s180271 BASELINE MOVING 2018 Phase 1 sic@ */
  };

  template (value) NPDCCH_ConfigDedicated_NB_r13 cs_NPDCCH_ConfigDedicated_NB_DEFAULT :=
  { // 36.508 Table 8.1.6.3-3
    /* @status    APPROVED (NBIOT) */
    npdcch_NumRepetitions_r13 := r1,//VZW NBIOT: Changing from r16 to r1
    npdcch_StartSF_USS_r13 := v8,//VZW NBIOT: Changing from v4 to v8
    npdcch_Offset_USS_r13 := zero
  };

  template (value) NPUSCH_ConfigDedicated_NB_r13 cs_NPUSCH_ConfigDedicated_NB_DEFAULT :=
  { // 36.508 Table 8.1.6.3-7
    /* @status    APPROVED (NBIOT) */
    ack_NACK_NumRepetitions_r13 := omit,//VZW NBIOT: Changing from r8 to omit
    npusch_AllSymbols_r13 := true,
    groupHoppingDisabled_r13 := omit
  };

  template (value) PhysicalConfigDedicated_NB_r13 cs_PhysicalConfigDedicated_NB_DEFAULT(template (value) NPDCCH_ConfigDedicated_NB_r13 p_NPDCCH_ConfigDedicated_NB,
                                                                                        template (value) NPUSCH_ConfigDedicated_NB_r13 p_NPUSCH_ConfigDedicated_NB,
                                                                                        template (value) UplinkPowerControlDedicated_NB_r13 p_UplinkPowerControlDedicated_NB,
                                                                                        template (omit)  PhysicalConfigDedicated_NB_r13.twoHARQ_ProcessesConfig_r14 p_TwoHARQ_ProcessesConfig_r14 := omit) :=
  { // 36.508 Table 8.1.8.2.1.6-1
    /* @status    APPROVED (NBIOT) */
    carrierConfigDedicated_r13 := omit,
    npdcch_ConfigDedicated_r13 := p_NPDCCH_ConfigDedicated_NB,
    npusch_ConfigDedicated_r13 := p_NPUSCH_ConfigDedicated_NB,
    uplinkPowerControlDedicated_r13 := omit,//VZW NBIOT: Changing from p_UplinkPowerControlDedicated_NB to omit,
    twoHARQ_ProcessesConfig_r14 := p_TwoHARQ_ProcessesConfig_r14,           /* @sic R5s170597 BASELINE MOVING 2017 sic@ */
    interferenceRandomisationConfig_r14 := omit,    /* @sic R5s180271 BASELINE MOVING 2018 Phase 1 sic@ */
    npdcch_ConfigDedicated_v1530 := omit,           /* @sic R5s180552 BASELINE MOVING 2018 sic@ */
    additionalTxSIB1_Config_v1540 := omit           /* @sic R5s190xxx BASELINE MOVING FEB-2019 sic@ */
  };

  template (value) RLF_TimersAndConstants_NB_r13 cs_RLF_TimersAndConstants_Setup(RLF_TimersAndConstants_NB_r13.setup.t301_r13 p_T301,
                                                                                 RLF_TimersAndConstants_NB_r13.setup.t310_r13 p_T310 := ms1000) :=
  { /* @status    APPROVED (NBIOT) */
    setup := {
      t301_r13  := p_T301,
      t310_r13  := p_T310,  // @sic R5-176657 sic@
      n310_r13  := n20,
      t311_r13  := ms1000,
      n311_r13  := n1,
      t311_v1350 := omit,                         /* @sic R5s170597 BASELINE MOVING 2017 sic@ */
      t301_v1530 := omit,                         /* @sic R5s180552 BASELINE MOVING 2018 sic@ */
      t311_v1530 := omit                          /* @sic R5s180552 BASELINE MOVING 2018 sic@ */
    }
  };

  template (value) RLF_TimersAndConstants_NB_r13 cs_RLF_TimersAndConstants_Setup_CP := cs_RLF_TimersAndConstants_Setup(ms10000, ms8000); /* @status    APPROVED (NBIOT) @sic R5176657 sic@ */

  template (value) RLF_TimersAndConstants_NB_r13 cs_RLF_TimersAndConstants_Release :=
  { /* @status    APPROVED (NBIOT) */
    release := NULL
  };

  template (present) UL_DCCH_Message_NB cr_508_RRCConnectionSetupComplete_NB(EUTRA_ASN1_RRC_TransactionIdentifier_Type p_RRC_TI := tsc_RRC_TI_Def,
                                                                             template (present) integer p_SelectedPLMN_Id := 1,
                                                                             template EUTRA_ASN1_S_TMSI_Type p_S_TMSI := *,
                                                                             template EUTRA_ASN1_RegisteredMME_Type p_RegisteredMME := *,
                                                                             template (present) EUTRA_ASN1_DedicatedInfoNAS_Type p_NAS_DedicatedInformation := ?,
                                                                             template RRCConnSetupComplete_AttachWithoutPDN_Type p_AttachWithoutPDN := *) :=
  { // 36.508 Table 8.1.6.1-15
    /* @status    APPROVED (NBIOT) */
    message_ := {
      c1 := {
        rrcConnectionSetupComplete_r13 := {
          rrc_TransactionIdentifier := p_RRC_TI,
          criticalExtensions  := {
            rrcConnectionSetupComplete_r13 := {
              selectedPLMN_Identity_r13           := p_SelectedPLMN_Id,
              s_TMSI_r13                          := p_S_TMSI,
              registeredMME_r13                   := p_RegisteredMME,
              dedicatedInfoNAS_r13                := p_NAS_DedicatedInformation,
              attachWithoutPDN_Connectivity_r13   := p_AttachWithoutPDN,
              up_CIoT_EPS_Optimisation_r13        := *,
              lateNonCriticalExtension            := *,
              nonCriticalExtension                := *
            }
          }
        }
      }
    }
  };

  template (value) DL_DCCH_Message_NB cs_508_SecurityModeCommand_NB(EUTRA_ASN1_RRC_TransactionIdentifier_Type p_RRC_TI,
                                                                    IntegrityProtAlgorithm_Type p_IntegrityProtAlgorithm,
                                                                    EUTRA_ASN1_CipheringAlgorithm_r12_Type p_CipheringAlgorithm) :=
  { /* Default values according to 36.508 cl.4.6.1-19 */
    /* @status    APPROVED (NBIOT) */
    message_ := {
      c1 := {
        securityModeCommand_r13 := {
          rrc_TransactionIdentifier := p_RRC_TI,
          criticalExtensions := {
            c1 := {
              securityModeCommand_r8 := {
                securityConfigSMC := {
                  securityAlgorithmConfig := cs_SecurityAlgorithmConfig(p_IntegrityProtAlgorithm, p_CipheringAlgorithm)
                },
                nonCriticalExtension := omit
              }
            }
          }
        }
      }
    }
  };

  template (present) UL_DCCH_Message_NB cr_508_SecurityModeComplete_NB(EUTRA_ASN1_RRC_TransactionIdentifier_Type p_RRC_TI) :=
  { /* Default values according to 36.508 cl.4.6.1-20 */
    /* @status    APPROVED (NBIOT) */
    message_ := {
      c1 := {
        securityModeComplete_r13 := {
          rrc_TransactionIdentifier := p_RRC_TI,
          criticalExtensions := {
            securityModeComplete_r8 := {
              nonCriticalExtension := *
            }
          }
        }
      }
    }
  };

  template (present) UL_DCCH_Message_NB cr_508_SecurityModeFailure_NB(EUTRA_ASN1_RRC_TransactionIdentifier_Type p_RRC_TI) :=
  { /* @status    APPROVED (NBIOT) */
    message_ := {
      c1 := {
        securityModeFailure_r13 := {
          rrc_TransactionIdentifier := p_RRC_TI,
          criticalExtensions := {
            securityModeFailure_r8 := {
              nonCriticalExtension := *
            }
          }
        }
      }
    }
  };

  template (value) DL_DCCH_Message_NB cs_508_UeCapabilityEnquiry_NB(EUTRA_ASN1_RRC_TransactionIdentifier_Type p_RRC_TI) :=
  { /* According to 36.508 Table 8.1.6.1-16 */
    /* @status    APPROVED (NBIOT) */
    message_ := {
      c1 := {
        ueCapabilityEnquiry_r13 := {
          rrc_TransactionIdentifier := p_RRC_TI,
          criticalExtensions := {
            c1 := {
              ueCapabilityEnquiry_r13 := {
                lateNonCriticalExtension := omit,
                nonCriticalExtension := omit
              }
            }
          }
        }
      }
    }
  };

  template (present) UL_DCCH_Message_NB cr_UeCapabilityInformation_NB_Any(EUTRA_ASN1_RRC_TransactionIdentifier_Type p_RRC_TI) := cr_508_UeCapabilityInformation_NB(p_RRC_TI);   /* @status    APPROVED (NBIOT) */

  template (present) UL_DCCH_Message_NB cr_508_UeCapabilityInformation_NB(EUTRA_ASN1_RRC_TransactionIdentifier_Type p_RRC_TI,
                                                                          template (present) UE_Capability_NB_r13 p_UE_Capability := cr_UE_Capability_NB,
                                                                          template (present) UE_RadioPagingInfo_NB_r13 p_UE_RadioPagingInfo := cr_UE_RadioPagingInfo_NB) :=
  { /* According to 36.508 Table 8.1.6.1-17 */
    /* @status    APPROVED (NBIOT) */
    message_ := {
      c1 := {
        ueCapabilityInformation_r13 := {
          rrc_TransactionIdentifier := p_RRC_TI,
          criticalExtensions := {
            ueCapabilityInformation_r13 := {
              ue_Capability_r13 := p_UE_Capability,   // @sic R5-181277 sic@
              ue_RadioPagingInfo_r13 := p_UE_RadioPagingInfo,
              lateNonCriticalExtension := *, // @sic R5s180191 sic@
              nonCriticalExtension := * // @sic R5s180191 sic@
            }
          }
        }
      }
    }
  };

  template (present) UE_Capability_NB_r13 cr_UE_Capability_NB(template PDCP_Parameters_NB_r13 p_PDCP_Parameters_NB := *,
                                                              template (present) PhyLayerParameters_NB_r13 p_PhyLayerParameters_NB := cr_PhyLayerParameters_NB,
                                                              template (present) RF_Parameters_NB_r13 p_RF_Parameters_NB := ?) :=
  { // 36.508 Table 8.1.6.1-17
    /* @status    APPROVED (NBIOT) */
    accessStratumRelease_r13 := ?, // @sic R5-174291 sic@
    ue_Category_NB_r13       := *, // @sic R5-174291 sic@
    multipleDRB_r13          := *,
    pdcp_Parameters_r13      := p_PDCP_Parameters_NB,
    phyLayerParameters_r13   := p_PhyLayerParameters_NB,
    rf_Parameters_r13        := p_RF_Parameters_NB,
    dummy                    := *   // @sic R5-181277 sic@
  };

  template (present) UE_RadioPagingInfo_NB_r13 cr_UE_RadioPagingInfo_NB(template UE_RadioPagingInfo_NB_r13.ue_Category_NB_r13 p_UE_Category := *) :=
  { /* @status    APPROVED (NBIOT) */
    ue_Category_NB_r13 := p_UE_Category,
    multiCarrierPaging_r14 := *,                /* @sic R5s170597 BASELINE MOVING 2017, R5180414 sic@ */
    mixedOperationMode_r15 := *,                /* @sic R5s180552 BASELINE MOVING 2018 sic@ */
    wakeUpSignal_r15 := *,                      /* @sic R5s180552 BASELINE MOVING 2018 sic@ */
    wakeUpSignalMinGap_eDRX_r15 := *,           /* @sic R5s180552 BASELINE MOVING 2018 sic@ */
    multiCarrierPagingTDD_r15 := *              /* @sic R5s180552 BASELINE MOVING 2018 sic@ */
  };

  template (present) PDCP_Parameters_NB_r13 cr_PDCP_Parameters_NB(template (present) boolean p_Profile0x0002 := ?) :=
  { /* @status    APPROVED (NBIOT) */
    supportedROHC_Profiles_r13 := {
      profile0x0002 := p_Profile0x0002,
      profile0x0003 := ?,
      profile0x0004 := ?,
      profile0x0006 := ?,
      profile0x0102 := ?,
      profile0x0103 := ?,
      profile0x0104 := ?
    },
    maxNumberROHC_ContextSessions_r13 := *
  };

  template (present) PhyLayerParameters_NB_r13 cr_PhyLayerParameters_NB :=
  { /* @status    APPROVED (NBIOT) */
    multiTone_r13 := *,
    multiCarrier_r13 := *
  };

  template (value) DL_DCCH_Message_NB cs_RRCConnectionRelease_NB(EUTRA_ASN1_RRC_TransactionIdentifier_Type p_RRC_TI := tsc_RRC_TI_Def,
                                                                 ReleaseCause_NB_r13 p_Cause := other,
                                                                 template (omit) EUTRA_ASN1_ResumeIdentity_r13_Type p_ResumeIdentity := omit,
                                                                 template (omit) integer p_XtdWaitTime := omit,
                                                                 template (omit) RedirectedCarrierInfo_NB_r13 p_RedirectedCarrierInfo := omit) :=
  { // 36.508 Table 8.1.6.1-9
    /* @status    APPROVED (NBIOT) */
    message_ := {
      c1 := {
        rrcConnectionRelease_r13 := {
          rrc_TransactionIdentifier := p_RRC_TI,
          criticalExtensions := {
            c1 := {
              rrcConnectionRelease_r13 := {
                releaseCause_r13 := p_Cause,
                resumeIdentity_r13 := p_ResumeIdentity,
                extendedWaitTime_r13 := p_XtdWaitTime,
                redirectedCarrierInfo_r13 := p_RedirectedCarrierInfo,
                lateNonCriticalExtension := omit,
                nonCriticalExtension := omit
              }
            }
          }
        }
      }
    }
  };

  template (value) DL_DCCH_Message_NB cs_RRCConnectionResume_NB(EUTRA_ASN1_RRC_TransactionIdentifier_Type p_RRC_TI := tsc_RRC_TI_Def,
                                                                template (value) RadioResourceConfigDedicated_NB_r13 p_RadioResourceConfigDedicated := cs_NB_DRB_Config_Def,
                                                                template (omit) RRCConnectionResume_NB_r13_IEs.drb_ContinueROHC_r13 p_ContinueROHC := omit) :=
  { // 36.508 Table 8.1.6.1-11
    /* @status    APPROVED (NBIOT) */
    message_ := {
      c1 := {
        rrcConnectionResume_r13 := {
          rrc_TransactionIdentifier := p_RRC_TI,
          criticalExtensions := {
           c1 := {
            rrcConnectionResume_r13 := {
              radioResourceConfigDedicated_r13 := p_RadioResourceConfigDedicated,
              nextHopChainingCount_r13         := 0,
              drb_ContinueROHC_r13             := p_ContinueROHC,
              lateNonCriticalExtension         := omit,
              nonCriticalExtension             := omit
            }
           }
          }
        }
      }
    }
  };

  template (present) UL_DCCH_Message_NB cr_RRCConnectionResumeComplete_NB(EUTRA_ASN1_RRC_TransactionIdentifier_Type p_RRC_TI):=
  { // 36.508 Table 8.1.6.1-12
    /* @status    APPROVED (NBIOT) */
    message_ := {
      c1 := {
        rrcConnectionResumeComplete_r13 := {
          rrc_TransactionIdentifier := p_RRC_TI,
          criticalExtensions := {
            rrcConnectionResumeComplete_r13 := {
              selectedPLMN_Identity_r13        := *,
              dedicatedInfoNAS_r13             := *,
              lateNonCriticalExtension         := *,
              nonCriticalExtension             := *
            }
          }
        }
      }
    }
  };

  template (present) UL_CCCH_Message_NB cr_RRCConnectionResumeRequest_NB(template (present) EUTRA_ASN1_ResumeIdentity_r13_Type p_ResumeIdentity := ?,
                                                                         template (present) EUTRA_ASN1_ShortMAC_I_Type p_ShortMAC_I := ?,
                                                                         template (present) EstablishmentCause_NB_r13 p_ResumeCause := ?) :=
  { // 36.508 Table 8.1.6.1-13
    /* @status    APPROVED (NBIOT) */
    message_ := {
      c1 := {
        rrcConnectionResumeRequest_r13 := {
          criticalExtensions := {
            rrcConnectionResumeRequest_r13 := {
              resumeID_r13              := p_ResumeIdentity,
              shortResumeMAC_I_r13      := p_ShortMAC_I,
              resumeCause_r13           := p_ResumeCause,
              earlyContentionResolution_r14 := ?,   /* @sic R5s180412 BASELINE MOVING 2018-2, R5s180552 BASELINE MOVING 2018: cqi_NPDCCH_r14 and earlyContentionResolution_r14 swapped sic@ */
              cqi_NPDCCH_r14                := ?,   /* @sic R5s180412 BASELINE MOVING 2018-2, R5s180552 BASELINE MOVING 2018: cqi_NPDCCH_r14 and earlyContentionResolution_r14 swapped sic@ */
              spare                     := ?
            }
          }
        }
      }
    }
  };

  template (value) DL_CCCH_Message_NB cs_RRCConnectionReestablishment_NB(EUTRA_ASN1_RRC_TransactionIdentifier_Type p_RRC_TI,
                                                                         template (value) RadioResourceConfigDedicated_NB_r13 p_RadioResourceConfigDedicated) :=
  { /* 36.508 Table 8.1.6.1-5 */
    /* @status    APPROVED (NBIOT) */
    message_ := {
      c1 := {
        rrcConnectionReestablishment_r13 := {
          rrc_TransactionIdentifier := p_RRC_TI,
          criticalExtensions := {
            c1 := {
              rrcConnectionReestablishment_r13 := {
                radioResourceConfigDedicated_r13 := p_RadioResourceConfigDedicated,
                nextHopChainingCount_r13         := 0,
                lateNonCriticalExtension         := omit,
                nonCriticalExtension             := omit
              }
            }
          }
        }
      }
    }
  };

  template (present) UL_DCCH_Message_NB cr_RRCConnectionReestablishmentComplete_NB(EUTRA_ASN1_RRC_TransactionIdentifier_Type p_RRC_TI):=
  { /* @status    APPROVED (NBIOT) */
    // 36.508 Table 8.1.6.1-6
    message_ := {
      c1 := {
        rrcConnectionReestablishmentComplete_r13 := {
          rrc_TransactionIdentifier := p_RRC_TI,
          criticalExtensions := {
            rrcConnectionReestablishmentComplete_r13 := {
              lateNonCriticalExtension         := *,
              nonCriticalExtension             := *
            }
          }
        }
      }
    }
  };

  template (present) EUTRA_ASN1_ReestabUE_Identity_Type cr_ReestabUE_Identity_NB(EUTRA_ASN1_C_RNTI_Type p_C_RNTI,
                                                                                 EUTRA_ASN1_PhysCellId_Type p_PhysCellId,
                                                                                 EUTRA_ASN1_ShortMAC_I_Type p_ShortMAC_I) :=
  { /* @status    APPROVED (NBIOT) */
    c_RNTI := p_C_RNTI,
    physCellId := p_PhysCellId,
    shortMAC_I := p_ShortMAC_I
  };

  template (present) UL_CCCH_Message_NB cr_RRCConnectionReestablishmentRequest_NB(template (present) EUTRA_ASN1_ReestabUE_Identity_Type p_UE_Identity,
                                                                                  ReestablishmentCause_NB_r13 p_ReestablishmentCause) :=
  { /* 36.508 Table 8.1.6.1-7 */
    /* @status    APPROVED (NBIOT) */
    message_ := {
      c1 := {
        rrcConnectionReestablishmentRequest_r13 := {
          criticalExtensions := {
            rrcConnectionReestablishmentRequest_r13 := {
              ue_Identity_r13               := p_UE_Identity,
              reestablishmentCause_r13      := p_ReestablishmentCause,
              cqi_NPDCCH_r14                := ?,   /* @sic R5s180412 BASELINE MOVING 2018-2 sic@ */
              earlyContentionResolution_r14 := ?,   /* @sic R5s180412 BASELINE MOVING 2018-2 sic@ */
              spare                         := ?    /* @sic R5s180412 BASELINE MOVING 2018-2 sic@ */
            }
          }
        }
      }
    }
  };

  template (value) DL_CCCH_Message_NB cs_RRCConnectionReject_NB(integer p_WaitTime := 10,
                                                                template (omit) RRCConnectionReject_NB_r13_IEs.rrc_SuspendIndication_r13 p_SuspendIndication := true_) :=
  { // 36.508 Table 8.1.6.1-8
    /* @status    APPROVED (NBIOT) */
    message_ := {
      c1 := {
        rrcConnectionReject_r13 := {
          criticalExtensions := {
            c1 := {
              rrcConnectionReject_r13 := {
                extendedWaitTime_r13             := p_WaitTime,
                rrc_SuspendIndication_r13        := p_SuspendIndication,
                lateNonCriticalExtension         := omit,
                nonCriticalExtension             := omit
              }
            }
          }
        }
      }
    }
  };

  template (value) UL_AM_RLC_NB_r13 cs_UL_AM_RLC_NB_r13(T_PollRetransmit_NB_r13 p_T_PollRetransmit,
                                                        UL_AM_RLC_NB_r13.maxRetxThreshold_r13 p_MaxRetxThreshold) :=
  { /* @status    APPROVED (NBIOT) */
    t_PollRetransmit_r13 := p_T_PollRetransmit,     /* ms250, ms500, ms1000, ms2000, ms3000, ms4000,
                                                       ms6000, ms10000, ms15000, ms25000, ms40000, ms60000,
                                                       ms90000, ms120000, ms180000, spare1 */
    maxRetxThreshold_r13 := p_MaxRetxThreshold      /* t1, t2, t3, t4, t6, t8, t16, t32 */
  };

  template (value) DL_AM_RLC_NB_r13 cs_DL_AM_RLC_NB_r13(template (omit) DL_AM_RLC_NB_r13.enableStatusReportSN_Gap_r13 p_EnableStatusReportSN_Gap) :=
  { /* @status    APPROVED (NBIOT) */
    enableStatusReportSN_Gap_r13 := p_EnableStatusReportSN_Gap
  };

  template (value) DL_AM_RLC_NB_r13 cs_DL_AM_RLC_NB_r13_GapReport := cs_DL_AM_RLC_NB_r13(true_);    /* @status    APPROVED (NBIOT) */

  template (value) DL_AM_RLC_NB_r13 cs_DL_AM_RLC_NB_r13_NoGapReport := cs_DL_AM_RLC_NB_r13(omit);   /* @status    APPROVED (NBIOT) */

  template (value) UL_AM_RLC_NB_r13 cs_NB_TX_AM_RLC_SRB1_Def := cs_UL_AM_RLC_NB_r13(ms25000, t4);   /* for the UE the tx direction is the UL, for SS it is the DL;
                                                                                                       Default values as given in 36.331 cl. 9.2.1.1
                                                                                                       @status    APPROVED (NBIOT) */

  template (value) DL_AM_RLC_NB_r13 cs_NB_RX_AM_RLC_SRB1_Def := cs_DL_AM_RLC_NB_r13_NoGapReport;    /* for the UE the rx direction is the DL, for SS it is the UL
                                                                                                       see default values as given in 36.331 cl. 9.2.1.1
                                                                                                       @status    APPROVED (NBIOT) */

  template (value) DL_DCCH_Message_NB cs_RRCConnectionReconfiguration_NB_DRB_Est(EUTRA_ASN1_RRC_TransactionIdentifier_Type p_RRC_TI,
                                                                                 EUTRA_ASN1_DRB_Identity_Type p_DRB_Id := tsc_DRB1) :=
  /* Values according to 36.508 cl. 8.1.6.3 Table 8.1.6.3-11 (using condition 1 DRB) */
  /* @status    APPROVED (NBIOT) */
    cs_RRCConnectionReconfiguration_NB(p_RRC_TI, cs_NB_DRB_Config_Def(p_DRB_Id));

  template (value) DL_DCCH_Message_NB cs_RRCConnectionReconfiguration_NB(EUTRA_ASN1_RRC_TransactionIdentifier_Type p_RRC_TI,
                                                                         template (value) RadioResourceConfigDedicated_NB_r13 p_RadioResourceConfigDedicated) :=
  { // 36.508 Table 8.1.6.1-3
    /* @status    APPROVED (NBIOT) */
    message_ := {
      c1 := {
        rrcConnectionReconfiguration_r13 := {
          rrc_TransactionIdentifier := p_RRC_TI,
          criticalExtensions := {
            c1 := {
              rrcConnectionReconfiguration_r13 := {
                dedicatedInfoNASList_r13         := omit,
                radioResourceConfigDedicated_r13 := p_RadioResourceConfigDedicated,
                fullConfig_r13                   := omit,
                lateNonCriticalExtension         := omit,
                nonCriticalExtension             := omit
              }
            }
          }
        }
      }
    }
  };

  template (value) RadioResourceConfigDedicated_NB_r13 cs_NB_DRB_Config_Def(EUTRA_ASN1_DRB_Identity_Type p_DRB_Id := tsc_DRB1) :=
  /* Values according to 36.508 cl. 8.1.6.3 Table 8.1.6.3-11 (using condition 1 DRB) */
  /* @status    APPROVED (NBIOT) */
    cs_RadioResourceConfigDedicated_NB_DrbConfig({ cs_508_NB_DRB_ToAddMod_DEFAULT(p_DRB_Id) });

  template (value) DRB_ToAddMod_NB_r13 cs_508_NB_DRB_ToAddMod_DEFAULT(EUTRA_ASN1_DRB_Identity_Type p_DRB_Id) :=
  /* According to 36.508 cl. 8.1.8.2.1.7 Table 4.8.2.1.7-1 */
  /* @status    APPROVED (NBIOT) */
    cs_NB_DRB_ToAddMod_Common(p_DRB_Id);

  template (value) DRB_ToAddMod_NB_r13 cs_NB_DRB_ToAddMod_Common(EUTRA_ASN1_DRB_Identity_Type p_DRB_Id,
                                                                 template (omit) PDCP_Config_NB_r13  p_PDCP_Config := cs_NB_PDCP_Config_DRB,
                                                                 template (omit) RLC_Config_NB_r13   p_RLC_Config := cs_NB_RLC_Config_DRB,
                                                                 template (omit) LogicalChannelConfig_NB_r13 p_LogicalChannelConfig := cs_NB_LogicalChannelConfig_DRB) :=
  { /* common template which deals with assignment of eps_BearerIdentity and logicalChannelIdentity
       (36.508 Table 8.1.8.2.1.7-1) */
    /* @status    APPROVED (NBIOT) */
    eps_BearerIdentity_r13 := p_DRB_Id+4,
    drb_Identity_r13 := p_DRB_Id,
    pdcp_Config_r13 := p_PDCP_Config,
    rlc_Config_r13 := p_RLC_Config,
    logicalChannelIdentity_r13 := p_DRB_Id+3,        // @sic R5-171992 sic@
    logicalChannelConfig_r13 := p_LogicalChannelConfig,
    rlc_Config_v1430 := omit                      /* @sic R5s170597 BASELINE MOVING 2017 sic@ */
  };

  template (value) PDCP_Config_NB_r13.headerCompression_r13 cs_NB_PDCP_HeaderCompression_NotUsed :=
  { /* @status    APPROVED (NBIOT) */
    notUsed := NULL
  };

  template (value) PDCP_Config_NB_r13.headerCompression_r13 cs_NB_PDCP_HeaderCompression_ROHC(boolean p_Profile0x0002 := false,
                                                                                              boolean p_Profile0x0003 := false,
                                                                                              boolean p_Profile0x0004 := false,
                                                                                              boolean p_Profile0x0006 := false,
                                                                                              boolean p_Profile0x0102 := false,
                                                                                              boolean p_Profile0x0103 := false,
                                                                                              boolean p_Profile0x0104 := false) :=
  { /* @status    APPROVED (NBIOT) */
    /* @sic R5-185021: all profiles set to false as highest value shall be chosen depending on UE capability sic@ */
    rohc := {
      maxCID_r13 := omit,  // DEFAULT 15
      profiles_r13 := {
        profile0x0002 := p_Profile0x0002,
        profile0x0003 := p_Profile0x0003,
        profile0x0004 := p_Profile0x0004,
        profile0x0006 := p_Profile0x0006,
        profile0x0102 := p_Profile0x0102,
        profile0x0103 := p_Profile0x0103,
        profile0x0104 := p_Profile0x0104
      }
    }
  };

  template (value) PDCP_Config_NB_r13 cs_NB_PDCP_Config_DRB(PDCP_Config_NB_r13.discardTimer_r13 p_DiscardTimer := infinity_,
                                                            template (value) PDCP_Config_NB_r13.headerCompression_r13 p_HeaderCompression := cs_NB_PDCP_HeaderCompression_NotUsed) :=
  { /* 36.308 Table 8.1.8.2.1.2.1-1
       @status    APPROVED (NBIOT) */
    discardTimer_r13 := p_DiscardTimer,
    headerCompression_r13  := p_HeaderCompression
  };

  template (value) UL_AM_RLC_NB_r13 cs_NB_TX_AM_RLC_DRB := cs_UL_AM_RLC_NB_r13(ms25000, t8);   /* 36.308 Table 8.1.8.2.1.3.1-1
                                                                                                  @status    APPROVED (NBIOT) */

  template (value) DL_AM_RLC_NB_r13 cs_NB_RX_AM_RLC_DRB := cs_DL_AM_RLC_NB_r13_NoGapReport;    /* 36.308 Table 8.1.8.2.1.3.1-1
                                                                                                  @status    APPROVED (NBIOT) */

  template (value) RLC_Config_NB_r13 cs_NB_RLC_Config_DRB :=
  { /* 36.308 Table 8.1.8.2.1.3.1-1
       @status    APPROVED (NBIOT) */
    am  := {
      ul_AM_RLC_r13 := cs_NB_TX_AM_RLC_DRB,
      dl_AM_RLC_r13 := cs_NB_RX_AM_RLC_DRB
    }
  };

  template (value) LogicalChannelConfig_NB_r13 cs_NB_LogicalChannelConfig_DRB :=
  { /* 36.308 Table 8.1.8.2.1.4-1
       @status    APPROVED (NBIOT) */
    priority_r13                    := tsc_NBIOT_DRB_LogicalChannelPriority,
    logicalChannelSR_Prohibit_r13   := true
  };

  template (value) RadioResourceConfigDedicated_NB_r13 cs_RadioResourceConfigDedicated_NB_DrbConfig(template (omit) DRB_ToAddModList_NB_r13 p_DRB_ToAddModList := omit,
                                                                                                    template (omit) PhysicalConfigDedicated_NB_r13 p_PhysicalConfigDedicated := omit,
                                                                                                    template (omit) RLF_TimersAndConstants_NB_r13 p_RLF_TimersAndConstants := omit) :=
  { /* @status    APPROVED (NBIOT) */
    srb_ToAddModList_r13        := omit,
    drb_ToAddModList_r13        := p_DRB_ToAddModList,
    drb_ToReleaseList_r13       := omit,
    mac_MainConfig_r13          := omit,
    physicalConfigDedicated_r13 := p_PhysicalConfigDedicated,
    rlf_TimersAndConstants_r13 := p_RLF_TimersAndConstants,
    schedulingRequestConfig_r15 := omit           /* @sic R5s180552 BASELINE MOVING 2018 sic@ */
  };

  template (value) RadioResourceConfigDedicated_NB_r13 cs_RadioResourceConfigDedicated_NB_DrbRelease(EUTRA_ASN1_DRB_Identity_Type p_DRB_Id) :=
  { /* @status    APPROVED (NBIOT) */
    srb_ToAddModList_r13        := omit,
    drb_ToAddModList_r13        := omit,
    drb_ToReleaseList_r13       := {p_DRB_Id},
    mac_MainConfig_r13          := omit,
    physicalConfigDedicated_r13 := omit,
    rlf_TimersAndConstants_r13  := omit,
    schedulingRequestConfig_r15 := omit           /* @sic R5s180552 BASELINE MOVING 2018 sic@ */
  };

  template (value) DL_DCCH_Message_NB cs_RRCConnectionReconfiguration_NB_DrbRelease(EUTRA_ASN1_RRC_TransactionIdentifier_Type p_RRC_TI,
                                                                                    EUTRA_ASN1_DRB_Identity_Type p_DRB_Id) :=
  /* @status    APPROVED (NBIOT) */
    cs_RRCConnectionReconfiguration_NB(p_RRC_TI, cs_RadioResourceConfigDedicated_NB_DrbRelease(p_DRB_Id));

  template (present) UL_DCCH_Message_NB cr_RRCConnectionReconfigurationComplete_NB(EUTRA_ASN1_RRC_TransactionIdentifier_Type p_RRC_TI):=
  { // 36.508 Table 8.1.6.1-4
    /* @status    APPROVED (NBIOT) */
    message_ := {
      c1 := {
        rrcConnectionReconfigurationComplete_r13 := {
          rrc_TransactionIdentifier := p_RRC_TI,
          criticalExtensions := {
            rrcConnectionReconfigurationComplete_r13 := {
              lateNonCriticalExtension := *,
              nonCriticalExtension := *
            }
          }
        }
      }
    }
  };

  /*
   * @desc      Function to convert the PICS parameters pc_eBandX_Supp from single booleans to a bitstring.
   *            The leftmost bit represent eBand1 support ('1'B if supported according to PICS pc_eBand1_Supp
   *            and '0'B if not supported according to PICS pc_eBand1_Supp).
   * @return    bitstring
   * @status    APPROVED (NBIOT)
   */
  function f_DeriveSuppNBIoTBandsFromPics() return bitstring
  {
    var bitstring v_Bitstring := f_ConvertBoolToBit(pc_eBand1_Supp) &
                                 f_ConvertBoolToBit(pc_eBand2_Supp) &
                                 f_ConvertBoolToBit(pc_eBand3_Supp) &
                                 '0'B &//Band 4 is reserved
                                 f_ConvertBoolToBit(pc_eBand5_Supp) &
                                 '0'B &//Band 6 is reserved
                                 '0'B &//Band 7 is reserved
                                 f_ConvertBoolToBit(pc_eBand8_Supp) &
                                 '0'B &//Band 9 is reserved
                                 '0'B &//Band 10 is reserved
                                 f_ConvertBoolToBit(pc_eBand11_Supp)  &// @sic R5-175124 sic@
                                 f_ConvertBoolToBit(pc_eBand12_Supp) &
                                 f_ConvertBoolToBit(pc_eBand13_Supp) &
                                 '0'B &//Band 14 is reserved
                                 '0'B &//Band 15 is reserved
                                 '0'B &//Band 16 is reserved
                                 f_ConvertBoolToBit(pc_eBand17_Supp) &
                                 f_ConvertBoolToBit(pc_eBand18_Supp) &
                                 f_ConvertBoolToBit(pc_eBand19_Supp) &
                                 f_ConvertBoolToBit(pc_eBand20_Supp) &
                                 f_ConvertBoolToBit(pc_eBand21_Supp)  &// @sic R5-175124 sic@
                                 '0'B &//Band 22 is reserved
                                 '0'B &//Band 23 is reserved
                                 '0'B &//Band 24 is reserved
                                 f_ConvertBoolToBit(pc_eBand25_Supp)  &// @sic R5-175124 sic@
                                 f_ConvertBoolToBit(pc_eBand26_Supp) &
                                 '0'B &//Band 27 is reserved
                                 f_ConvertBoolToBit(pc_eBand28_Supp) &
                                 '0'B &//Band 29 is reserved
                                 '0'B &//Band 30 is reserved
                                 f_ConvertBoolToBit(pc_eBand31_Supp) & // @sic R5-175124 sic@
                                 '0000000000000000000000000000000000'B &
                                 f_ConvertBoolToBit(pc_eBand66_Supp) & // @sic R5-175124 sic@
                                 '0'B &//Band 67 is reserved
                                 '0'B &//Band 68 is reserved
                                 '0'B &//Band 69 is reserved
                                 f_ConvertBoolToBit(pc_eBand70_Supp); // @sic R5-175124 sic@

    return v_Bitstring;
  }

}
