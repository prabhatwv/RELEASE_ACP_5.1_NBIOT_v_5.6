/******************************************************************************/
// @copyright   Copyright Notification
//   No part may be reproduced except as authorized by written permission.
//   The copyright and the foregoing restriction extend to reproduction in all media.
//   Trademark 2019, 3GPP Organizational Partners (ARIB, ATIS, CCSA, ETSI, TSDSI, TTA, TTC).
//   All rights reserved.
// @version: 36.523-3 v15.3.0
// $Date: 2019-03-16 19:54:08 +0100 (Sat, 16 Mar 2019) $
// $Rev: 24094 $
/******************************************************************************/
module Common4G5G_Templates {

  import from CommonAspDefs all;
  import from CommonDefs all;

  const IndicationAndControlMode_Type tsc_L1Mac_IndicationMode_Enable := enable;
  template (value) TransmissionRepetition_Type cs_TransmissionRepetition_Continuous :=
  { /* Default UL Auto PUCCH Synch to be continous */
    /* @status    APPROVED (ENDC, IMS, IMS_IRAT, LTE, LTE_A_IRAT, LTE_A_PRO, LTE_A_R10_R11, LTE_A_R12, LTE_IRAT, NBIOT, POS) */
    Continuous := true
  };

  template (value) TransmissionRepetition_Type cs_TransmissionRepetitionCyclic(integer p_Cycles) :=
  { /* @status    APPROVED (ENDC, IMS, IMS_IRAT, LTE, LTE_A_IRAT, LTE_A_PRO, LTE_A_R10_R11, LTE_A_R12, LTE_IRAT, NBIOT, POS) */
    NumOfCycles := p_Cycles
  };

  template (value) RAR_RapIdCtrl_Type cs_RapIdCtrl_Auto :=
  { /* @status    APPROVED (ENDC, LTE, LTE_A_PRO, NBIOT) */
    Automatic := true   /* SS shall automatically use same RAPID as received from the UE */
  };

  template (value) TimingInfo_Type cs_TimingInfo_Now := {Now := true};   /* @status    APPROVED (ENDC, IMS, IMS_IRAT, LTE, LTE_A_IRAT, LTE_A_PRO, LTE_A_R10_R11, LTE_A_R12, LTE_IRAT, NBIOT, POS) */

  template (value) TimingInfo_Type cs_TimingInfo_None := {None := true}; /* @status    APPROVED (NBIOT) */

  template (value) TimingInfo_Type cs_TimingInfo_EUTRA_NB(SubFrameTiming_Type p_TimingInfo) :=
  { /* @sic R5w160204r1 sic@ */
    /* @sic R5s180420: renamed from cs_TimingInfo sic@ */
    /* @status    APPROVED (ENDC, IMS, IMS_IRAT, LTE, LTE_A_IRAT, LTE_A_PRO, LTE_A_R10_R11, LTE_A_R12, LTE_IRAT, NBIOT, POS) */
    SubFrame := {
      SFN := p_TimingInfo.SFN,
      Subframe := p_TimingInfo.Subframe,
      HSFN := p_TimingInfo.HSFN,
      Slot :=  cs_SlotTimingInfo_FirstSlot  /* @sic R5w180111: Slot sic@
                                               @sic R5s180420: cs_SlotTimingInfo_FirstSlot sic@ */
    }
  };

  template (value) TimingInfo_Type cs_TimingInfo_NR(SubFrameTiming_Type p_TimingInfo) :=
  { /* @status    APPROVED (ENDC, IMS, IMS_IRAT, LTE, LTE_A_IRAT, LTE_A_PRO, LTE_A_R10_R11, LTE_A_R12, LTE_IRAT, NBIOT, POS) */
    SubFrame := {
      SFN := p_TimingInfo.SFN,
      Subframe := p_TimingInfo.Subframe,
      HSFN := p_TimingInfo.HSFN,
      Slot := p_TimingInfo.Slot
    }
  };

  template (value) TimingInfo_Type cs_TimingInfo_SubFrameFull(template (value) SubFrameTiming_Type p_SubFrameTiming) :=
  { /* @status    APPROVED (NBIOT) */
    SubFrame := p_SubFrameTiming
  };

  template (value) SlotTimingInfo_Type cs_SlotTimingInfo_FirstSlot :=
  { /* @status    APPROVED (ENDC, IMS, IMS_IRAT, LTE, LTE_A_IRAT, LTE_A_PRO, LTE_A_R10_R11, LTE_A_R12, LTE_IRAT, NBIOT, POS) */
    FirstSlot := true
  };

  template (value) TimingInfo_Type cs_TimingInfo_SFN(SystemFrameNumber_Type p_SFN,
                                                     SystemFrameNumber_Type p_HSFN) :=
  { /* @status    APPROVED (LTE, LTE_A_PRO, LTE_A_R10_R11, LTE_A_R12, LTE_IRAT, NBIOT) */
    SubFrame := {
      SFN := { Number := p_SFN },
      Subframe := { Any := true },
      HSFN := { Number := p_HSFN },         /* @sic R5w160204r1 sic@ */
      Slot := cs_SlotTimingInfo_FirstSlot   /* @sic R5w180111: Slot sic@ */
    }
  };

  template (present) TimingInfo_Type cr_TimingInfo_Any :=
  { /* @status    APPROVED (ENDC, IMS, IMS_IRAT, LTE, LTE_A_IRAT, LTE_A_PRO, LTE_A_R10_R11, LTE_A_R12, LTE_IRAT, NBIOT, POS) */
    SubFrame := {
      SFN := ? ,
      Subframe := ?,
      HSFN := ?,   /* @sic R5w160204r1 sic@ */
      Slot := ?    /* @sic R5w180111: Slot sic@ */
    }
  };

  /*
   * @desc      auxiliary function to check whether timing info is "now"
   * @param     p_TimingInfo
   * @return    boolean
   * @status    APPROVED (ENDC, IMS, IMS_IRAT, LTE, LTE_A_IRAT, LTE_A_PRO, LTE_A_R10_R11, LTE_A_R12, LTE_IRAT, NBIOT, POS)
   */
  function f_TimingInfo_IsNow(template (value) TimingInfo_Type p_TimingInfo) return boolean
  {
    return match(valueof(p_TimingInfo), cs_TimingInfo_Now);
  }

  template (value) IndicationStatus_Type cs_IndicationStatus_NoError :=
  { /* @status    APPROVED (ENDC, NBIOT) */
    Ok := true
  };

  template (value) IndicationStatus_Type cs_IndicationStatus_NasIntegrityError :=
  { /* @status    APPROVED (ENDC, IMS, IMS_IRAT, LTE, LTE_A_IRAT, LTE_A_PRO, LTE_A_R10_R11, LTE_A_R12, LTE_IRAT, NBIOT, POS, UTRAN) */
    Error := {
      Integrity := {
        Nas := true,
        Pdcp := false
      },
      System := 0
    }
  };

  /*
   * @desc      auxiliary function to set CNF flag for configuration ASPs
   * @param     p_TimingInfo
   * @param     p_CnfFlag           (default value: omit)
   * @return    boolean
   * @status    APPROVED (ENDC, IMS, IMS_IRAT, LTE, LTE_A_IRAT, LTE_A_PRO, LTE_A_R10_R11, LTE_A_R12, LTE_IRAT, NBIOT, POS)
   */
  function f_CnfFlag_Set(template (value) TimingInfo_Type p_TimingInfo,
                         template (omit) boolean p_CnfFlag := omit) return boolean
  {
    var boolean v_CnfFlag;
    
    if (isvalue(p_CnfFlag)) {
      v_CnfFlag := valueof(p_CnfFlag);
    } else {
      v_CnfFlag := f_TimingInfo_IsNow(p_TimingInfo);
    }
    return v_CnfFlag;
  }

}
